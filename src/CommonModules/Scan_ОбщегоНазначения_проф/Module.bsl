/////////////////////////////////////////////////////////////////////////////////////
// ФУНКЦИИ РАБОТЫ С БИЗНЕС-ПРОЦЕССАМИ

// Функция возвращает флаг согласования документа
//
Функция ДокументСогласован(ДокументСсылка) Экспорт
	Запрос = Новый Запрос(
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	Scan_Согласование.Ссылка
	|ИЗ
	|	БизнесПроцесс.Scan_Согласование КАК Scan_Согласование
	|ГДЕ
	|	Scan_Согласование.Предмет = &ДокументСсылка
	|	И Scan_Согласование.Завершен
	|	И (Scan_Согласование.РезультатСогласования = ЗНАЧЕНИЕ(Перечисление.Scan_РезультатыСогласования.Согласовано)
	|			ИЛИ Scan_Согласование.РезультатСогласования = ЗНАЧЕНИЕ(Перечисление.Scan_РезультатыСогласования.СогласованоСЗамечаниями))
	|	И НЕ Scan_Согласование.ПометкаУдаления");
	Запрос.УстановитьПараметр("ДокументСсылка", ДокументСсылка);
	РезЗапроса = Запрос.Выполнить();
	
	Возврат (НЕ РезЗапроса.Пустой());
КонецФункции

// Функция возвращает статус согласования документа
//Возвращаемое значение:
//	Структура с ключами
//		- Код - код статуса
//		- Статус - строковое представление статуса
//		- Ссылка - ссылка на бизнес-процесс согласования
//
Функция СтатусСогласования(ДокументСсылка) Экспорт
	Рез = Новый Структура("Код, Статус, Ссылка");
	Запрос = Новый Запрос(
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	Scan_Согласование.Ссылка,
	|	Scan_Согласование.Стартован,
	|	Scan_Согласование.Завершен
	|ИЗ
	|	БизнесПроцесс.Scan_Согласование КАК Scan_Согласование
	|ГДЕ
	|	Scan_Согласование.Предмет = &ДокументСсылка
	|	И НЕ Scan_Согласование.ПометкаУдаления");
	Запрос.УстановитьПараметр("ДокументСсылка", ДокументСсылка);
	РезЗапроса = Запрос.Выполнить();
	
	Если РезЗапроса.Пустой() Тогда
		Рез.Код = 0;
		Рез.Статус = "Не согласован";
	Иначе
		Выборка = РезЗапроса.Выбрать();
		Выборка.Следующий();
		Если НЕ Выборка.Стартован Тогда
			Рез.Код = 1;
			Рез.Статус = "Не согласован";
			Рез.Ссылка = Выборка.Ссылка;
		ИначеЕсли Выборка.Завершен Тогда
			Рез.Код = 3;
			Рез.Статус = "Согласован";
			Рез.Ссылка = Выборка.Ссылка;
		Иначе
			Рез.Код = 2;
			Рез.Статус = "На согласовании";
			Рез.Ссылка = Выборка.Ссылка;
		КонецЕсли;	
	КонецЕсли;
	
	Возврат Рез;
КонецФункции

// Функция создает новый бизнес-процесс
//
Процедура СоздатьБизнесПроцессСогласования(ДокументСсылка,Настройка) Экспорт
	
	СпрСсылка = Scan_ПраваИНастройки.Scan_ПолучитьПраваИНастройкиПользователя(ДокументСсылка.Организация,Настройка);
	Если Не ЗначениеЗаполнено(СпрСсылка) Тогда
		//rarus FominskiyAS 08.07.2019  mantis 14191 +++
		Если ТекущийЯзык()="en" тогда
			Сообщить("These documents are being processed", СтатусСообщения.Внимание);
		иначе
			Сообщить("Для данных документов в обработке ""Установка прав и настроек"" 
			|не выбран ""Шаблон согласования документов"".
			|Согласование не было создано!", СтатусСообщения.Внимание);
		КонецЕсли;
		//rarus FominskiyAS 08.07.2019  mantis 14191 ---  
		Возврат;
	КонецЕсли;
				
	НовыйБП = БизнесПроцессы.Scan_Согласование.СоздатьБизнесПроцесс();
	НовыйБП.Дата = ТекущаяДата();
	НовыйБП.УстановитьНовыйНомер();
	НовыйБП.Заполнить(ДокументСсылка);
		
	Попытка
		НовыйБП.Записать();
		НовыйБП.Старт();
		//rarus FominskiyAS 08.07.2019  mantis 14191 +++
		//Сообщить("Для документа было создано согласование """ + НовыйБП.Ссылка + """");
		Сообщить(НСтр("ru = 'Для документа было создано согласование ""'; en = '""Approval"" was created for this document ""'") + НовыйБП.Ссылка + """");
		//rarus FominskiyAS 08.07.2019  mantis 14191 ---  
	Исключение
	//rarus FominskiyAS 08.07.2019  mantis 14191 +++
	//Сообщить("Для документа не удалось создать согласование!", СтатусСообщения.Внимание);
	Сообщить(НСтр("ru = 'Для документа не удалось создать согласование!'; en = 'Action failed'"), СтатусСообщения.Внимание);
	//rarus FominskiyAS 08.07.2019  mantis 14191 ---  	
	КонецПопытки;
	
КонецПроцедуры
