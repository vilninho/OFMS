
// Получает текущие значения прав и настроек из регистра ПраваИНастройки.
// Выбираются все права и настройки текущего пользователя, всех организаций и всех подразделений,
//права и настройки других пользователей не кэшируются в целях защиты информации
//
Функция ПолучитьЗначенияВсехПравНастроек(Знач ТекущийПользователь) Экспорт
	УстановитьПривилегированныйРежим(ИСТИНА);
	
	ЗапросПраваНастройки = Новый Запрос(
	"ВЫБРАТЬ
	|	ПланВидовХарактеристикПраваИНастройки.Ссылка,
	|	ПланВидовХарактеристикПраваИНастройки.ТипЗначения,
	|	ПланВидовХарактеристикПраваИНастройки.Назначение,
	|	ПланВидовХарактеристикПраваИНастройки.ЗначениеПоУмолчанию,
	//|	ПланВидовХарактеристикПраваИНастройки.НастройкаКомпании,
	|	ПланВидовХарактеристикПраваИНастройки.НастройкаОрганизации,
	|	ПланВидовХарактеристикПраваИНастройки.НастройкаПодразделения,
	|	ПланВидовХарактеристикПраваИНастройки.НастройкаПользователя
	|ИЗ
	|	ПланВидовХарактеристик.Scan_ПраваИНастройки КАК ПланВидовХарактеристикПраваИНастройки
	|ГДЕ
	|	(ПланВидовХарактеристикПраваИНастройки.Предопределенный
	|	ИЛИ ПланВидовХарактеристикПраваИНастройки.Родитель = &ГруппаПВХНастройкаДоступностиСправочники
	|	ИЛИ	ПланВидовХарактеристикПраваИНастройки.Родитель = &ГруппаПВХНастройкаДоступностиДокументы)
	|	И (НЕ ПланВидовХарактеристикПраваИНастройки.ЭтоГруппа)");
	ЗапросПраваНастройки.УстановитьПараметр("ГруппаПВХНастройкаДоступностиСправочники", Scan_ПраваИНастройки.Scan_ПолучитьСсылкуПВХПравИНастроек("СПРАВОЧНИКИ"));
	ЗапросПраваНастройки.УстановитьПараметр("ГруппаПВХНастройкаДоступностиДокументы", Scan_ПраваИНастройки.Scan_ПолучитьСсылкуПВХПравИНастроек("ДОКУМЕНТЫ"));
	ВыборкаПраваНастройки = ЗапросПраваНастройки.Выполнить().Выбрать();
	
	НЗЗначенияПравИНастроек = РегистрыСведений.Scan_ПраваИНастройки.СоздатьНаборЗаписей();
	НЗЗначенияПравИНастроек.Прочитать();
	тблЗначенияПравИНастроек = НЗЗначенияПравИНастроек.Выгрузить();
	
	СтруктураПравНастроек = Новый Соответствие;
	
	Пока ВыборкаПраваНастройки.Следующий() Цикл
		Если ВыборкаПраваНастройки.НастройкаПользователя Тогда
			СтрокиЗначенияПравИНастроек = тблЗначенияПравИНастроек.НайтиСтроки(Новый Структура("ПравоНастройка, Объект", ВыборкаПраваНастройки.Ссылка, ТекущийПользователь));
		Иначе
			СтрокиЗначенияПравИНастроек = тблЗначенияПравИНастроек.НайтиСтроки(Новый Структура("ПравоНастройка", ВыборкаПраваНастройки.Ссылка));
		КонецЕсли;
		
		СОбъектыПрава = Новый Соответствие;
		Для Каждого ТекСтрокаЗначение Из СтрокиЗначенияПравИНастроек Цикл
			СОбъектыПрава.Вставить(ТекСтрокаЗначение.Объект, ТекСтрокаЗначение.Значение);
		КонецЦикла;
		
		// добавляем значение по умолчанию
		ЗначениеПоУмолчанию = ВыборкаПраваНастройки.ЗначениеПоУмолчанию;
		// если и по умолчанию ничего нет, то типизируем (на всякий случай)
		Если НЕ ЗначениеЗаполнено(ЗначениеПоУмолчанию) Тогда
			ЗначениеПоУмолчанию = ВыборкаПраваНастройки.ТипЗначения.ПривестиЗначение(Неопределено);
		КонецЕсли;
		СОбъектыПрава.Вставить("ЗначениеПоУмолчанию", ЗначениеПоУмолчанию);
		
		СтруктураПравНастроек.Вставить(ВыборкаПраваНастройки.Ссылка, СОбъектыПрава);
	КонецЦикла;
	
	// Запрос для кэширования дополнительных настроек доступа справочников и документов
	// из регистров сведений ДоступКСправочникам и ДоступКДокументам
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДоступКСправочникам.Объект КАК Объект,
	|	ДоступКСправочникам.ДоступЕсть КАК ДоступЕсть,
	|	ДоступКСправочникам.Право КАК Право
	|ИЗ
	|	РегистрСведений.Scan_ПраваИНастройки КАК ПраваИНастройки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.Scan_ДоступКСправочникам КАК ДоступКСправочникам
	|		ПО ПраваИНастройки.Объект = ДоступКСправочникам.Пользователь
	|			И ПраваИНастройки.ПравоНастройка = ДоступКСправочникам.Право
	|ГДЕ
	|	ПраваИНастройки.Объект = &Пользователь
	|	И ПраваИНастройки.Значение = &ВыбЗначенияПравСправочников
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДоступКДокументам.Объект,
	|	ДоступКДокументам.ДоступЕсть,
	|	ДоступКДокументам.Право
	|ИЗ
	|	РегистрСведений.Scan_ПраваИНастройки КАК ПраваИНастройки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.Scan_ДоступКДокументам КАК ДоступКДокументам
	|		ПО ПраваИНастройки.Объект = ДоступКДокументам.Пользователь
	|			И ПраваИНастройки.ПравоНастройка = ДоступКДокументам.Право
	|ГДЕ
	|	ПраваИНастройки.Объект = &Пользователь
	|	И ПраваИНастройки.Значение В(&ВыбЗначенияПравДокументов)
	|ИТОГИ
	|	МАКСИМУМ(ДоступЕсть)
	|ПО
	|	Право,
	|	Объект");
	Запрос.УстановитьПараметр("Пользователь", ТекущийПользователь);
	Запрос.УстановитьПараметр("ВыбЗначенияПравСправочников", Перечисления.Scan_ВидыПравДляСправочников.РедактированиеПоГруппам);
	масВидыПравДляДокументов = Новый Массив;
	масВидыПравДляДокументов.Добавить(Перечисления.Scan_ВидыПравДляДокументов.РедактированиеПоПользователям); 
	Запрос.УстановитьПараметр("ВыбЗначенияПравДокументов", масВидыПравДляДокументов);
	
	РезультатПраво = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	// Перебор полученных значений и формирование соответствия прав
	Пока РезультатПраво.Следующий() Цикл
		РезультатОбъект = РезультатПраво.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		СОбъектыПрава = Новый Соответствие;
		Пока РезультатОбъект.Следующий() Цикл
			СОбъектыПрава.Вставить(РезультатОбъект.Объект, РезультатОбъект.ДоступЕсть);
		КонецЦикла;
		СтруктураПравНастроек.Вставить("Значения " + РезультатПраво.Право.Наименование, СОбъектыПрава);
	КонецЦикла;
	
	Возврат СтруктураПравНастроек;
КонецФункции
