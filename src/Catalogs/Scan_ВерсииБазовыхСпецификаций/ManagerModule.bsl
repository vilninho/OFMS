Функция РазобратьСтрокуОпций (СтрокаОпций) Экспорт //rarus bonmak 08.10.2019 14177 ++
	ТаблицаОпций = Новый ТаблицаЗначений;
	ТаблицаОпций.Колонки.Добавить("Опция", Новый ОписаниеТипов("СправочникСсылка.Scan_ОпцииПродуктов")); //ссылка на элемент справочника
	
	Пока СтрДлина(СтрокаОпций) > 1 Цикл 
		ТекОпция = СокрЛП(Лев(СтрокаОпций, 7));
		СсылкаОпции = Справочники.Scan_ОпцииПродуктов.НайтиПоКоду(ТекОпция);
		Если НЕ СсылкаОпции.Пустая() Тогда
			СтрокаОпции = ТаблицаОпций.Добавить();
			СтрокаОпции.Опция = СсылкаОпции;
		КонецЕсли;
		СтрокаОпций = Прав(СтрокаОпций, СтрДлина(СтрокаОпций)-7);
	КонецЦикла;
	Возврат ТаблицаОпций; 
КонецФункции //rarus bonmak 08.10.2019 14177 --

//Процедура создает спецификацию вида SPORT, для std моделей
//В данной процедуре, продукт запрещено изменять
Процедура СоздатьСпецификациюSport(ПродуктСсылка, СпецификацияПродуктаОбщая, СОПСсылка) Экспорт //rarus bonmak 17615 31.05.2021 ++
	ДатаСозданияСпец = ТекущаяДатаСеанса();
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Scan_СпецификацияСтандартнойМодели.Ссылка КАК ДокументСсылка,
		|	ВЫБОР
		|		КОГДА НаличиеФайлов.ЕстьФайлы ЕСТЬ NULL
		|			ТОГДА 0
		|		КОГДА НаличиеФайлов.ЕстьФайлы
		|			ТОГДА 1
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК ЕстьФайлы
		|ПОМЕСТИТЬ ДокументыССМ
		|ИЗ
		|	Документ.Scan_СпецификацияСтандартнойМодели КАК Scan_СпецификацияСтандартнойМодели
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НаличиеФайлов КАК НаличиеФайлов
		|		ПО Scan_СпецификацияСтандартнойМодели.Ссылка = НаличиеФайлов.ОбъектСФайлами
		|ГДЕ
		|	Scan_СпецификацияСтандартнойМодели.ДействуетСДаты <= &ДействуетСДаты
		|	И Scan_СпецификацияСтандартнойМодели.Статус = ЗНАЧЕНИЕ(Справочник.Scan_СтатусыЗаявокНаДействие.Вработе)
		|	И Scan_СпецификацияСтандартнойМодели.Модель = &Модель
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	ДокументыССМ.ДокументСсылка КАК ДокументСсылка
		|ИЗ
		|	ДокументыССМ КАК ДокументыССМ
		|ГДЕ
		|	ДокументыССМ.ЕстьФайлы > 0
		|
		|УПОРЯДОЧИТЬ ПО
		|	ДокументыССМ.ДокументСсылка.Дата УБЫВ";
	
	Запрос.УстановитьПараметр("ДействуетСДаты", ДатаСозданияСпец);
	Запрос.УстановитьПараметр("Модель", 		ПродуктСсылка.МодельПродукта);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		ВыборкаДетальныеЗаписи.Следующий();
		
		ДокументССМ 		  		 = ВыборкаДетальныеЗаписи.ДокументСсылка;
		ТаблицаОпцийДокумента		 = ДокументССМ.Спецификация.Выгрузить();
		СтрокаОпцийДокумента 		 = СоздатьСтрокуОпций(ТаблицаОпцийДокумента);
		
		СпецSport					 = Справочники.Scan_ВерсииБазовыхСпецификаций.СоздатьЭлемент();
		СпецSport.Опции				 = СтрокаОпцийДокумента;
		СпецSport.РасшифровкаОпций.Загрузить(ТаблицаОпцийДокумента);
		СпецSport.Наименование		 = "" + ПродуктСсылка.МодельПродукта + " " + ПродуктСсылка.Наименование + " " + ДокументССМ.НомерСпецификации;
		СпецSport.ДатаСоздания		 = ДатаСозданияСпец;
		СпецSport.ВерсияСпецификации = 1;
		СпецSport.ВидСпецификации 	 = Справочники.Scan_ВидыСпецификацийПродуктов.SPORTСпецификация;
		СпецSport.Владелец 			 = СпецификацияПродуктаОбщая;
		СпецSport.ВерсияСпецификации = 1;
		//rarus bonmak 23.09.2021 АПК исправление
		НоваяСтрокаИспользование					 = СпецSport.Использование.Добавить();	
		НоваяСтрокаИспользование.ВерсияСпецификации  = 1;
		НоваяСтрокаИспользование.ОбъектИспользования = СОПСсылка;
		
		Попытка 
			СпецSport.Записать();
			СтруктураРеквизитов = Новый Структура;
			СтруктураРеквизитов.Вставить("Спецификация", 		   СпецSport.Ссылка);
			СтруктураРеквизитов.Вставить("ВидСпецификации", 	   СпецSport.ВидСпецификации);
			СтруктураРеквизитов.Вставить("ВерсияСпецификации", 	   СпецSport.ВерсияСпецификации);
			СтруктураРеквизитов.Вставить("Опции",				   СпецSport.Опции);
			СтруктураРеквизитов.Вставить("ДатаВерсииСпецификации", ДатаСозданияСпец);
			
			РегистрыСведений.Scan_ВерсииСпецификаций.ЗаписьЗначенияРегистраСведения(СтруктураРеквизитов); //rarus bonmak 20.03.2020 14177
			РегистрыСведений.Scan_ОпределяемыеПараметрыПоОпциямПродуктов.ОбновитьОпределяемыеПараметры(ПродуктСсылка);
		Исключение
			СтрокаСообщения = НСтр("ru = 'Произошла ошибка при создании спецификации SPORT для STD модели. Продукт -  %1. Соглашение о поставке - %2. Причина: %3 '");
			СтрокаСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(СтрокаСообщения, 
			ПродуктСсылка.IDExternalSystemProduct, СОПСсылка, ОписаниеОшибки());
			
			ЗаписьЖурналаРегистрации(Нстр("ru = 'Запись спецификации SPORT'", ОбщегоНазначения.КодОсновногоЯзыка()), УровеньЖурналаРегистрации.Ошибка,,
			СОПСсылка, СтрокаСообщения); // Rarus tenkam 28.06.2022 mantis 18726 АПК + (Код основного языка)
		КонецПопытки;	
	КонецЕсли;
	
КонецПроцедуры //rarus bonmak 17615 31.05.2021 --

Функция СоздатьСтрокуОпций (ТаблицаОпций) Экспорт //rarus bonmak 17615 31.05.2021 ++
	СтрокаОпций = "";
	Для Каждого СтрОпция Из ТаблицаОпций Цикл
		   СтрокаОпций = СтрокаОпций + СтрОпция.Опция.Код + " ";
	КонецЦикла;
	Возврат СокрЛП(СтрокаОпций); 
КонецФункции //rarus bonmak 17615 31.05.2021 --
