//rarus vikhle 04.08.2020 mt 16181 +++

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	//rarus vikhle 14.02.2023 m 19839 +++
	Если НЕ Scan_ПраваИНастройки.Scan_Право("РазрешатьРедактированиеДоступныхМарокДилера") Тогда
		МассивМарок = Справочники.Scan_МаркиПродуктов.МаркиДоступныеПользователю(Пользователи.ТекущийПользователь());
		ФиксМассивМарок = Новый ФиксированныйМассив(МассивМарок);
		
		МассивПараметровВыбора = Новый Массив;
		МассивПараметровВыбора.Добавить(Новый ПараметрВыбора("Отбор.МаркаПродукта", ФиксМассивМарок));
		НовыеПараметрыВыбора =  Новый ФиксированныйМассив(МассивПараметровВыбора);
		Элементы.Модель.ПараметрыВыбора = НовыеПараметрыВыбора;
		
		Если Параметры.ЗначенияЗаполнения.Свойство("Модель") Тогда
			
			МаркаМодели = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Модель, "МаркаПродукта");
			Если МассивМарок.Найти(МаркаМодели) = Неопределено Тогда
				Объект.Модель = Неопределено; 
			КонецЕсли;
			
		КонецЕсли;	
		
	КонецЕсли;	
	//rarus vikhle 14.02.2023 m 19839 ---
	
	Если Объект.Ссылка.Пустая() Тогда
		УправлениеДиалогом();
	КонецЕсли;
		
	Scan_СборСтатистики.Scan_ПриОткрытии("Справочники", РеквизитФормыВЗначение("Объект").Метаданные().Синоним);	

КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	Если ТекущийОбъект.Ссылка.Пустая() Тогда	
		Если Не НаименованиеУникально(ТекущийОбъект.Наименование) Тогда
			Отказ = Истина;
			ВывестиСообщениеПол("В системе уже существует драфт продукта %1. Элемент не записан.",,,,,ТекущийОбъект.Наименование);
		КонецЕсли;
	Иначе
		Если Не НаименованиеУникально(ТекущийОбъект.Наименование,ТекущийОбъект.Ссылка) Тогда
			Отказ = Истина;
			ВывестиСообщениеПол("В системе уже существует драфт продукта %1. Элемент не записан.",,,,,ТекущийОбъект.Наименование);
		КонецЕсли;
	КонецЕсли;	
	// Rarus tenkam 11.04.2022 mantis 18433 +++
	Если Объект.Ссылка.Пустая() Тогда
		Scan_СборСтатистики.Scan_ПередЗаписьюСправочника(РеквизитФормыВЗначение("Объект").Метаданные().Синоним, Истина, "Создание нового элемента");
	КонецЕсли;
	// Rarus tenkam 11.04.2022 mantis 18433 --- 

КонецПроцедуры

&НаКлиенте
Процедура МодельПриИзменении(Элемент)
	УправлениеДиалогом();
КонецПроцедуры

&НаКлиенте
Процедура СегментРынкаПродуктовПриИзменении(Элемент)
	УправлениеДиалогом();
КонецПроцедуры

&НаСервере
Процедура УправлениеДиалогом()
	//Объект.Наименование = СокрЛП(?(ЗначениеЗаполнено(Объект.МаркетинговыйТип),Объект.МаркетинговыйТип.Наименование + " ","")
	Объект.Наименование = СокрЛП(?(ЗначениеЗаполнено(Объект.СегментРынкаПродуктов),Объект.СегментРынкаПродуктов.Наименование + " ","") //rarus vikhle 05.11.2020 mt 16723 
						  + ?(ЗначениеЗаполнено(Объект.Модель.МаркаПродукта),Объект.Модель.МаркаПродукта.Наименование + " ","") 
						  + ?(ЗначениеЗаполнено(Объект.Модель),Объект.Модель.Наименование,""));
КонецПроцедуры	

&НаСервереБезКонтекста
Функция НаименованиеУникально(ТекущееНаименование,ТекущаяСсылка = Неопределено)
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	Scan_ДрафтыПродуктов.Ссылка КАК Ссылка
	               |ИЗ
	               |	Справочник.Scan_ДрафтыПродуктов КАК Scan_ДрафтыПродуктов
	               |ГДЕ
	               |	Scan_ДрафтыПродуктов.Наименование = &ТекущееНаименование";
	Запрос.УстановитьПараметр("ТекущееНаименование",ТекущееНаименование);
	Если НЕ ТекущаяСсылка = Неопределено Тогда
		Запрос.Текст = Запрос.Текст + " И НЕ Scan_ДрафтыПродуктов.Ссылка = &ТекущаяСсылка";
		Запрос.УстановитьПараметр("ТекущаяСсылка",ТекущаяСсылка);	
	КонецЕсли;	
	
	Возврат Запрос.Выполнить().Пустой();
КонецФункции	


//rarus vikhle 04.08.2020 mt 16181 ---