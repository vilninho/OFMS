Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	//rarus BProg_Gladkov 03.05.2020 0015962 ++ ОбработкаПроверкиЗаполнения
	Если НЕ ЭтоГруппа тогда
		Если ПисьмоСПФ тогда
			ПроверяемыеРеквизиты.Добавить("ИмяПФ");
		КонецЕсли;
		
		Если ПисьмоСТабДок тогда
			ПроверяемыеРеквизиты.Добавить("ИмяТабДок");
		КонецЕсли;
	КонецЕсли;
	//rarus BProg_Gladkov 03.05.2020 0015962 -- 
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	//rarus BProg_Gladkov 03.05.2020 0015962 ++ ПередЗаписью 
	Если НЕ ЭтоГруппа тогда
		Если ПометкаУдаления тогда
			ПроверитьДопустимостьПометкиУдаления(Отказ);
			Если Отказ тогда
				Возврат;
			КонецЕсли;
			
			Действует = Ложь;
		КонецЕсли;
		
		Если ПисьмоСПФ тогда
			Если Действует тогда
				СнятьФлагДействуетВДругомДействующемШаблонеПоИмениПФ();
			КонецЕсли;
		Иначе
			ИмяПФ = "";
		КонецЕсли;
		
		Если НЕ ПисьмоСТабДок тогда
			ИмяТабДок = "";
		КонецЕсли;
	КонецЕсли;
	//rarus BProg_Gladkov 03.05.2020 0015962 -- 
КонецПроцедуры

Функция ЭтотШаблонИспользуетсяВДругихДейсвтвующихШаблонах() //rarus BProg_Gladkov 07.05.2020 0015962 +- 
	Запрос = Новый Запрос("ВЫБРАТЬ Ссылка ИЗ Справочник.Scan_ШаблоныПисем ГДЕ Действует И (Заголовок = &ЭтаСсылка ИЛИ Получатели = &ЭтаСсылка ИЛИ Копии = &ЭтаСсылка)");
	Запрос.УстановитьПараметр("ЭтаСсылка", Ссылка);
	Выборка = Запрос.Выполнить().Выбрать();
	ЭтотШаблонИспользуетсяВДругихДейсвтвующихШаблонах = Выборка.Следующий();
	
	Возврат ЭтотШаблонИспользуетсяВДругихДейсвтвующихШаблонах;
КонецФункции

Процедура ПроверитьДопустимостьПометкиУдаления(Отказ) //rarus BProg_Gladkov 07.05.2020 0015962 +- 

	Если ЭтотШаблонИспользуетсяВДругихДейсвтвующихШаблонах() тогда
		ТекстСообщения = НСтр("ru = 'Текущий шаблон используется в действующем шаблоне. Пометка на удаление запрещена.'; 
						 	  |en = 'This template is used in the valid template. Marking for deletion is prohibited.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Ссылка, , , Отказ);
		Возврат;
	КонецЕсли;
	
	Если Справочники.Scan_ШаблоныПисем.ШаблонИспользуетсяВПравахИНастройках(Ссылка) тогда
		ТекстСообщения = НСтр("ru = 'Текущий шаблон используется в правах и настройках. Пометка на удаление запрещена.'; 
						 	  |en = 'This template is used in permissions and settings. Marking for deletion is prohibited.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Ссылка, , , Отказ);
		Возврат;
	КонецЕсли;

КонецПроцедуры
 
Процедура СнятьФлагДействуетВДругомДействующемШаблонеПоИмениПФ() //rarus BProg_Gladkov 07.05.2020 0015962 +- 
	ТекущийДействующийШаблон = Scan_ОтправкаПисемПоЭлектроннойПочте.ШаблонПоИмениПФ(ИмяПФ);
	Если ЗначениеЗаполнено(ТекущийДействующийШаблон) И ТекущийДействующийШаблон <> Ссылка тогда
		ТекущийДействующийШаблонОбъект = ТекущийДействующийШаблон.ПолучитьОбъект();
		ТекущийДействующийШаблонОбъект.Действует = Ложь;
		ТекущийДействующийШаблонОбъект.Записать();
	КонецЕсли;
КонецПроцедуры
 