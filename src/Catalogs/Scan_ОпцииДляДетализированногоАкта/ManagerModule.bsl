// Rarus tenkam 28.06.2022 mantis 18726 АПК + (Стандартные области)
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Автоматически нумерует опции, следующие после начального элемента
//
// Параметры:
//  Родитель		 - СправочникСсылка.Scan_ОпцииДляДетализированногоАкта - опция или группа - родительский элемент
//  НачальныйЭлемент - СправочникСсылка.Scan_ОпцииДляДетализированногоАкта - начальная опция
//  НачальныйНомер	 - Число - порядковый номер в детализированным акте
//
Процедура ПеренумероватьЭлементыСправочника(Родитель, НачальныйЭлемент, НачальныйНомер) Экспорт
	
	НачатьТранзакцию();
	
	УспешноПеренумеровано = Истина;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("П1", Родитель);
	Запрос.УстановитьПараметр("П2", НачальныйЭлемент);
	Запрос.УстановитьПараметр("П3", НачальныйНомер);
	Запрос.Текст = "ВЫБРАТЬ
	|	Scan_ОпцииДляДетализированногоАкта.Ссылка КАК Ссылка,
	|	Scan_ОпцииДляДетализированногоАкта.ПорядковыйНомерВДетализированномАкте КАК ПорядковыйНомерВДетализированномАкте
	|ИЗ
	|	Справочник.Scan_ОпцииДляДетализированногоАкта КАК Scan_ОпцииДляДетализированногоАкта
	|ГДЕ
	|	НЕ Scan_ОпцииДляДетализированногоАкта.ЭтоГруппа
	|	И НЕ Scan_ОпцииДляДетализированногоАкта.ПометкаУдаления
	|	И Scan_ОпцииДляДетализированногоАкта.Родитель = &П1
	|	И Scan_ОпцииДляДетализированногоАкта.Ссылка <> &П2
	|	И Scan_ОпцииДляДетализированногоАкта.ПорядковыйНомерВДетализированномАкте >= &П3
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПорядковыйНомерВДетализированномАкте";
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Итератор = НачальныйНомер + 1;
	Пока Выборка.Следующий() Цикл
		ОпцияОбъект = Выборка.Ссылка.ПолучитьОбъект();
		Попытка
			ОпцияОбъект.Заблокировать();
			
			ТекущийНомерЭлемента = ОпцияОбъект.ПорядковыйНомерВДетализированномАкте;
			НовыйНомерЭлемента   = Итератор;
			
			ОпцияОбъект.ПорядковыйНомерВДетализированномАкте = Итератор;
			ОпцияОбъект.Записать();
			ОпцияОбъект.Разблокировать();
			
			ТекстСообщения = СтрШаблон(Нстр("ru = 'Порядковый номер элемента %1 обновлен с %2 на %3'; 
			|en = 'Sequence number of %1 item updated from %2 to %3'"), ОпцияОбъект.Наименование, ТекущийНомерЭлемента, НовыйНомерЭлемента);
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		Исключение
			ОтменитьТранзакцию(); //Rarus bonmak 29.07.2022 18726 АПК
			
			ТекстСообщения = СтрШаблон(Нстр("ru = 'Не удалось обновить порядковый номер элемента %1'; 
			|en = 'Failed to update item sequence number %1'"), ОпцияОбъект.Ссылка);
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
			
			УспешноПеренумеровано = Ложь;
			Возврат;
		КонецПопытки;
		
		Итератор = Итератор + 1;
	КонецЦикла;
	
	Если УспешноПеренумеровано Тогда
		ЗафиксироватьТранзакцию();
	Иначе
		ТекстСообщения = НСтр("ru = 'Перенумерация не выполнена.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
