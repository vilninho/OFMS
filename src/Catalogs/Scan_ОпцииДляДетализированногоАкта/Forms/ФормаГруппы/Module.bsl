
#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ПорядковыйНомерГруппыВДетализированномАктеПриИзменении(Элемент)
	
	Если Объект.Ссылка.Пустая() Тогда
		ПоказатьВопрос(Новый ОписаниеОповещения("ПорядковыйНомерГруппыВДетализированномАктеПриИзмененииФрагмент", ЭтотОбъект),
		Нстр("ru = 'Перед изменением порядкового номера необходимо записать группу. Продолжить?'; en = 'It is necessary to save the group. Proceed?'"), РежимДиалогаВопрос.ДаНет);
	Иначе
		ПоказатьВопросОНайденныхГруппах();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПорядковыйНомерГруппыВДетализированномАктеПриИзмененииФрагмент(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если  РезультатВопроса = КодВозвратаДиалога.Да 
		И Записать()
		Тогда
		ПоказатьВопросОНайденныхГруппах();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПорядковыйНомерГруппыВДетализированномАктеПриИзмененииЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ПорядковыйНомерГруппыВДетализированномАктеПриИзмененииНаСервере(ДополнительныеПараметры);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПоказатьВопросОНайденныхГруппах()
	
	ПоследующиеНомераГрупп = ПоследующиеНомераГрупп(Объект.Ссылка);
	Если  Объект.ПорядковыйНомерГруппыВДетализированномАкте <> 0 
		И ПоследующиеНомераГрупп.Найти(Объект.ПорядковыйНомерГруппыВДетализированномАкте) <> Неопределено
		Тогда
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("ПоследующиеНомераГрупп", ПоследующиеНомераГрупп);
		
		ПоказатьВопрос(Новый ОписаниеОповещения("ПорядковыйНомерГруппыВДетализированномАктеПриИзмененииЗавершение", ЭтотОбъект, ДополнительныеПараметры),
		Нстр("ru = 'Найдены группы с равным или бОльшим порядковым номером. 
					|Номера этих групп будут увеличены на 1. 
					|Продолжить?'; 
			 |en = 'Found groups with redundant or higher sequence number. 
					|They will be increased by 1
					|Proceed?'"), РежимДиалогаВопрос.ДаНет);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПоследующиеНомераГрупп(СсылкаНаГруппу)
	
	НомерГруппы = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СсылкаНаГруппу, "ПорядковыйНомерГруппыВДетализированномАкте");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("П1", СсылкаНаГруппу);
	Запрос.УстановитьПараметр("П2", НомерГруппы);
	Запрос.Текст = "ВЫБРАТЬ
	|	Scan_ОпцииДляДетализированногоАкта.ПорядковыйНомерГруппыВДетализированномАкте КАК НомерГруппы
	|ИЗ
	|	Справочник.Scan_ОпцииДляДетализированногоАкта КАК Scan_ОпцииДляДетализированногоАкта
	|ГДЕ
	|	Scan_ОпцииДляДетализированногоАкта.ЭтоГруппа
	|	И НЕ Scan_ОпцииДляДетализированногоАкта.ПометкаУдаления
	|	И Scan_ОпцииДляДетализированногоАкта.Ссылка <> &П1
	|	И Scan_ОпцииДляДетализированногоАкта.ПорядковыйНомерГруппыВДетализированномАкте >= &П2
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерГруппы";
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("НомерГруппы");
	
КонецФункции


&НаСервере
Процедура ПорядковыйНомерГруппыВДетализированномАктеПриИзмененииНаСервере(ДополнительныеПараметры)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("П1", Объект.Ссылка);
	Запрос.УстановитьПараметр("П2", ДополнительныеПараметры.ПоследующиеНомераГрупп);
	Запрос.Текст = "ВЫБРАТЬ
	|	Scan_ОпцииДляДетализированногоАкта.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.Scan_ОпцииДляДетализированногоАкта КАК Scan_ОпцииДляДетализированногоАкта
	|ГДЕ
	|	Scan_ОпцииДляДетализированногоАкта.ЭтоГруппа
	|	И НЕ Scan_ОпцииДляДетализированногоАкта.ПометкаУдаления
	|	И Scan_ОпцииДляДетализированногоАкта.Ссылка <> &П1
	|	И Scan_ОпцииДляДетализированногоАкта.ПорядковыйНомерГруппыВДетализированномАкте В(&П2)";
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		ОпцияОбъект = Выборка.Ссылка.ПолучитьОбъект();
		
		ТекущийНомерГруппы = ОпцияОбъект.ПорядковыйНомерГруппыВДетализированномАкте;
		НовыйНомерГруппы   = ТекущийНомерГруппы + 1;
		
		ОпцияОбъект.ПорядковыйНомерГруппыВДетализированномАкте = НовыйНомерГруппы;
		
		Попытка
			ОпцияОбъект.Записать();
			Сообщить(СтрШаблон(Нстр("ru = 'Порядковый номер группы %1 обновлен с %2 на %3'; 
									|en = 'Sequence number of %1 group updated from %2 to %3'"), ОпцияОбъект.Ссылка, ТекущийНомерГруппы, НовыйНомерГруппы));
		Исключение
			Сообщить(СтрШаблон(Нстр("ru = 'Не удалось обновить порядковый номер группы %1'; 
									|en = 'Failed to update group sequence number %1'"), ОпцияОбъект.Ссылка));
		КонецПопытки;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
