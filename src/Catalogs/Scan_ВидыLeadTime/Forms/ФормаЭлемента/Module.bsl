
#Область ОбработчикиСобытийЭлементовФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Scan_СборСтатистики.Scan_ПриОткрытии("Справочники", РеквизитФормыВЗначение("Объект").Метаданные().Синоним);	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи) // Rarus tenkam 11.04.2022 mantis 18433 +++

	Если Объект.Ссылка.Пустая() Тогда
		Scan_СборСтатистики.Scan_ПередЗаписьюСправочника(РеквизитФормыВЗначение("Объект").Метаданные().Синоним, Истина, "Создание нового элемента");
	КонецЕсли;
КонецПроцедуры 	// Rarus tenkam 11.04.2022 mantis 18433 --- 

&НаКлиенте
Процедура ИспользуетсяПриИзменении(Элемент)
	
	Если  Не Объект.Ссылка.Пустая()
		И Не Объект.Используется
		И ПоВидуLeadTimeЕстьКонтрольныеТочки(Объект.Ссылка)
		Тогда
		ПоказатьВопрос(Новый ОписаниеОповещения("ИспользуетсяПриИзмененииЗавершение", ЭтотОбъект),
						Нстр("ru = 'Для данного вида Lead Time заданы контрольные точки. Продолжить?'; en = 'Control points are set for this type of Lead Time. Proceed?'"), РежимДиалогаВопрос.ДаНет);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользуетсяПриИзмененииЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Объект.Используется = РезультатВопроса <> КодВозвратаДиалога.Да;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ПоВидуLeadTimeЕстьКонтрольныеТочки(ВидLeadTimeСсылка)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("П1", ВидLeadTimeСсылка);
	Запрос.Текст = "ВЫБРАТЬ
	|	Scan_КонтрольныеТочкиLeadTimeВидыLeadTime.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.Scan_КонтрольныеТочкиLeadTime.ВидыLeadTime КАК Scan_КонтрольныеТочкиLeadTimeВидыLeadTime
	|ГДЕ
	|	Scan_КонтрольныеТочкиLeadTimeВидыLeadTime.Ссылка.Используется
	|	И НЕ Scan_КонтрольныеТочкиLeadTimeВидыLeadTime.Ссылка.ПометкаУдаления
	|	И Scan_КонтрольныеТочкиLeadTimeВидыLeadTime.ВидLeadTime = &П1";
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат Не РезультатЗапроса.Пустой();
	
КонецФункции

#КонецОбласти