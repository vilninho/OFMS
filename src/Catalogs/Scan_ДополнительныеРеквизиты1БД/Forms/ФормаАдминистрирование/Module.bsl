#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка) //rarus bonmak 17468 23.09.2021 ++
	ЗаполнитьДанныеВТаблице();
КонецПроцедуры //rarus bonmak 17468 23.09.2021 --

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписокДопРеквизитов

&НаКлиенте
Процедура СписокДопРеквизитовТипДанныхOFMSНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка) //rarus bonmak 17468 23.09.2021 ++
	СтандартнаяОбработка = Ложь;
	СписокВыбора = ОбработатьСписокВыбора(); 
	СписокВыбора.ПоказатьВыборЭлемента(Новый ОписаниеОповещения("ОбработатьРезультатВыгрузки", ЭтотОбъект, ),"Выберите тип данных",);
КонецПроцедуры //rarus bonmak 17468 23.09.2021 --

&НаСервере
Функция ОбработатьСписокВыбора() //rarus bonmak 17468 23.09.2021 ++
	Возврат Справочники.Scan_ДополнительныеРеквизиты1БД.ПолучитьСписокВыбора();
КонецФункции //rarus bonmak 17468 23.09.2021 --

&НаКлиенте
Процедура ОбработатьРезультатВыгрузки(ВыбранныйЭлемент,ДополнительныеПараметры) Экспорт //rarus bonmak 17468 23.09.2021 ++    
	Если ВыбранныйЭлемент = Неопределено Тогда 
		Возврат;
	КонецЕсли;   
	
	УстановитьЗначениеРеквизитаНаСервере(ВыбранныйЭлемент.Значение);        
КонецПроцедуры //rarus bonmak 17468 23.09.2021 --

&НаСервере
Процедура УстановитьЗначениеРеквизитаНаСервере(Значение) //rarus bonmak 17468 23.09.2021 ++
	ИдентификаторСтроки = Элементы.СписокДопРеквизитов.ТекущаяСтрока;
	СтрокаТЗ = СписокДопРеквизитов.НайтиПоИдентификатору(ИдентификаторСтроки);
	СтрокаТЗ.ТипДанныхOFMS = Значение;
КонецПроцедуры //rarus bonmak 17468 23.09.2021 --

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыполнитьОбработку(Команда) //rarus bonmak 17468 23.09.2021 ++
	ВыполнитьНаСервере();
КонецПроцедуры //rarus bonmak 17468 23.09.2021 --

&НаСервере
Процедура ВыполнитьНаСервере() //rarus bonmak 17468 23.09.2021 ++
	СтруктураОтбора = Новый Структура("Флаг", Истина);
	НайденныеСтроки = СписокДопРеквизитов.НайтиСтроки(СтруктураОтбора);
	ЕстьОшибки = ВыполнитьПроверки(НайденныеСтроки);
	
	Если ЕстьОшибки Тогда
		Возврат;
	КонецЕсли;
	
	ПривязатьПерепривязатьЭлементыИз1БД(НайденныеСтроки);
КонецПроцедуры //rarus bonmak 17468 23.09.2021 --

&НаСервере
Функция ВыполнитьПроверки(НайденныеСтроки) //rarus bonmak 17468 23.09.2021 ++
	ЕстьОшибки = Ложь;
	Если НайденныеСтроки.Количество() = 0 Тогда
		ТекстОшибки = "Не установлен ни один флажок";
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
		ЕстьОшибки = Истина;
	КонецЕсли;
	
	Для Каждого ЭлементМассива Из НайденныеСтроки Цикл
		Если ЭлементМассива.ЭлементOFMS.Пустая()
			ИЛИ ПустаяСтрока(ЭлементМассива.ТипДанныхOFMS)
			ИЛИ (НЕ ЭлементМассива.Получение И НЕ ЭлементМассива.Отправка) Тогда
			ТекстОшибки = "У элемента " + ЭлементМассива.Элемент1БД  + " не заполнено обязательное поле «Элемент OFMS» или «Получение или Отправка» или «Тип данных OFMS»";
			ИндексСтроки = СписокДопРеквизитов.Индекс(ЭлементМассива);
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки,,"СписокДопРеквизитов[" + ИндексСтроки + "].Элемент1БД");
			ЕстьОшибки = Истина;	
		КонецЕсли;	
	КонецЦикла;
	
	Возврат ЕстьОшибки;
КонецФункции //rarus bonmak 17468 23.09.2021 --

&НаСервере
Процедура ПривязатьПерепривязатьЭлементыИз1БД(НайденныеСтроки) //rarus bonmak 17468 23.09.2021 ++
	СтруктураПараметров = Scan_ВебСервисы.ПолучитьСтруктуруПараметровМетода(, Ложь);
	ЕстьОшибки = Ложь;
	Для Каждого ЭлементМассива Из НайденныеСтроки Цикл
		ЭлементОбъект = ЭлементМассива.ЭлементOFMS.ПолучитьОбъект();
		ЭлементОбъект.IDExternalSystem 				= ЭлементМассива.GUID1БД;
		ЭлементОбъект.Наименование 					= ЭлементМассива.Элемент1БД;
		ЭлементОбъект.ИспользуетсяВOFMS 			= ЭлементМассива.Получение;
		ЭлементОбъект.ИспользуетсяВOFMSДляОтправки 	= ЭлементМассива.Отправка;
		ЭлементОбъект.ТипДопРеквизитаOFMS 			= ЭлементМассива.ТипДанныхOFMS;
		ЭлементОбъект.Пользователь 					= ПользователиСлужебный.АвторизованныйПользователь();
		Попытка
			ЭлементОбъект.Записать();	
		Исключение
			ТекстОшибки = "Не удалось записать " + ЭлементМассива.Элемент1БД + " (" + ЭлементМассива.GUID1БД + ") по причине: " + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			ИндексСтроки = СписокДопРеквизитов.Индекс(ЭлементМассива);
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки,,"СписокДопРеквизитов[" + ИндексСтроки + "].Элемент1БД");	
			ЕстьОшибки = Истина;
			Продолжить;
		КонецПопытки;
		
		СтруктураПараметров.GUID = ЭлементОбъект.IDExternalSystem;
		Scan_ВебСервисыРазборОтветов.ВызватьМетод_GetAdditionalProperty(СтруктураПараметров);	
	КонецЦикла;
	
	Если НЕ ЕстьОшибки Тогда
		ТекстОшибки = "Привязка/перепривязка доп. реквизитов успешно выполнена";
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
		ЗаполнитьДанныеВТаблице();
	КонецЕсли;
КонецПроцедуры //rarus bonmak 17468 23.09.2021 --

&НаКлиенте
Процедура Перезаполнить(Команда) //rarus bonmak 17468 23.09.2021 ++
	ЗаполнитьДанныеВТаблице();
КонецПроцедуры //rarus bonmak 17468 23.09.2021 --

&НаСервере
Процедура ЗаполнитьДанныеВТаблице() //rarus bonmak 17468 23.09.2021 ++
	СписокДопРеквизитов.Очистить();
	
	СтруктураПараметров = Новый Структура("ПолучитьСписокБезОбработки, ТаблицаДопРеквизитовИзмененная", Истина, СписокДопРеквизитов.Выгрузить());
	Scan_ВебСервисыРазборОтветов.ВызватьМетод_GetAdditionalProperty(СтруктураПараметров);
	СписокДопРеквизитов.Загрузить(СтруктураПараметров.ТаблицаДопРеквизитовИзмененная);
КонецПроцедуры //rarus bonmak 17468 23.09.2021 --

&НаКлиенте
Процедура УстановитьФлажки(Команда) //rarus bonmak 17468 23.09.2021 ++
	СнятьУстановитьФлажки(Истина);
КонецПроцедуры //rarus bonmak 17468 23.09.2021 --

&НаКлиенте
Процедура СнятьФлажки(Команда) //rarus bonmak 17468 23.09.2021 ++
	СнятьУстановитьФлажки(Ложь);
КонецПроцедуры //rarus bonmak 17468 23.09.2021 --

&НаСервере
Процедура СнятьУстановитьФлажки(Значение) //rarus bonmak 17468 23.09.2021 ++
	ТЗ = РеквизитФормыВЗначение("СписокДопРеквизитов");	
	ТЗ.ЗаполнитьЗначения(Значение, "Флаг");
	ЗначениеВРеквизитФормы(ТЗ, "СписокДопРеквизитов");
КонецПроцедуры //rarus bonmak 17468 23.09.2021 --

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#КонецОбласти
