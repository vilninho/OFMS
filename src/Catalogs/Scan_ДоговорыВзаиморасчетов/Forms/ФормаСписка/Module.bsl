//rarus tenkam 24.01.2017 mantis 12676 +++
&НаКлиенте
Процедура ПоказатьВсеПриИзменении(Элемент)
	Для Каждого ТекЭлемент Из Список.Отбор.Элементы Цикл
		Если ТипЗнч(ТекЭлемент) = Тип("ГруппаЭлементовОтбораКомпоновкиДанных") Тогда
			Для Каждого ТекЭлементГруппы Из ТекЭлемент.Элементы Цикл
				Если ТекЭлементГруппы.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДатаОкончания") Тогда
					ТекЭлементГруппы.Использование = НЕ ПоказатьВсе;	
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Scan_ВспомогательныеФункцииКлиент.ПроверитьПользователяПортала();//rarus vikhle 07.05.2020 mt 15695
	
	ПоказатьВсе = Ложь;  	
		
	ГруппаОтбора = Список.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаОтбора.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
	
	ЭлементОтбора = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДатаОкончания");
	ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.БольшеИлиРавно;
	ЭлементОтбора.Использование = Истина;
	ЭлементОтбора.ПравоеЗначение = ТекущаяДата();  	
	
	ЭлементОтбора2 = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора2.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДатаОкончания");
	ЭлементОтбора2.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ЭлементОтбора2.Использование = Истина;
	ЭлементОтбора2.ПравоеЗначение = Дата('00010101');	

КонецПроцедуры
//rarus tenkam 24.01.2018 mantis 12676 ---

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ОформитьИСкрытьНедействительных();	// rarus tenkam 02.09.2020 mantis 16401 +
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьНедействительныхПриИзменении(Элемент)	// rarus tenkam 02.09.2020 mantis 16401 +++
	ПереключитьОтображениеНедействительных(ПоказатьНедействительных);             	
КонецПроцедуры	// rarus tenkam 02.09.2020 mantis 16401 ---

&НаКлиенте
Процедура ПереключитьОтображениеНедействительных(ПоказатьНедействительных)	// rarus tenkam 02.09.2020 mantis 16401 +++
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		Список, "Недействителен", Ложь, , ,
		НЕ ПоказатьНедействительных);
	
КонецПроцедуры	// rarus tenkam 02.09.2020 mantis 16401 ---
	
&НаСервере
Процедура ОформитьИСкрытьНедействительных()	// rarus tenkam 02.09.2020 mantis 16401 +++
	
	// Оформление.
	ЭлементОформления = Список.КомпоновщикНастроек.Настройки.УсловноеОформление.Элементы.Добавить();
	ЭлементОформления.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	
	ЭлементЦветаОформления = ЭлементОформления.Оформление.Элементы.Найти("TextColor");
	ЭлементЦветаОформления.Значение = Метаданные.ЭлементыСтиля.ТекстЗапрещеннойЯчейкиЦвет.Значение;
	ЭлементЦветаОформления.Использование = Истина;
	
	ЭлементОтбора = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("Недействителен");
	ЭлементОтбора.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;
	ЭлементОтбора.ПравоеЗначение = Истина;
	ЭлементОтбора.Использование  = Истина;
	
	// Скрытие.
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		Список, "Недействителен", Ложь, , , Истина);
	
КонецПроцедуры	// rarus tenkam 02.09.2020 mantis 16401 ---

