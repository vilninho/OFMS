// rarus tenkam 17.08.2020 mantis 16422 +++
Функция ПолучитьПризнакТягачШасси(СтрокаМодель) Экспорт
	
	Признак = ПредопределенноеЗначение("Перечисление.Scan_ПризнакТягачШасси.ПустаяСсылка");
	
	НаименованиеМодели = СокрЛП(СтрокаМодель);
	НаименованиеМодели = ВРег(НаименованиеМодели);
	НаименованиеМодели = СтрЗаменить( СтрЗаменить( СтрЗаменить(НаименованиеМодели, "Х","X"), "А","A"), "В","B");
	
	// В наименовании модели выполнить поиск символа «X»
	СимволХ = СтрНайти(НаименованиеМодели,"X");
	Если СимволХ = 0 Тогда
		Возврат Признак;
	КонецЕсли;
	
	// Если присутствует символ «X», то для дальнейшего поиска используется только фрагмент наименования слева до этого символа
	НаименованиеМодели = Лев(НаименованиеМодели,СимволХ-1);
	
	// В оставшемся фрагменте наименования выполнить поиск символов «A» и «B»;
	СимволПризнака = СтрНайти(НаименованиеМодели,"A");
	Если СимволПризнака = 0 Тогда
		СимволПризнака = СтрНайти(НаименованиеМодели,"B");
		// Если во фрагменте наименования не присутствует ни один из символов, алгоритм далее не выполняется
		Если СимволПризнака	= 0 Тогда
			Возврат Признак;
		КонецЕсли;
	КонецЕсли;
	
	// Если хотя бы один из символов присутствует, из оставшегося фрагмента наименования удалить все числовые символы
	НаименованиеМодели = СтрЗаменить( СтрЗаменить( СтрЗаменить( СтрЗаменить( СтрЗаменить(
	СтрЗаменить( СтрЗаменить( СтрЗаменить( СтрЗаменить( СтрЗаменить( 
	НаименованиеМодели, "0", ""), "1", ""), "2", ""), "3", ""), "4", ""), "5", ""), "6", ""), "7", ""), "8", ""), "9", "");
	
	// Первый символ справа в оставшемся фрагменте является искомым.
	СимволПризнак = Прав(НаименованиеМодели, 1);
	Если СимволПризнак = "A" Тогда
		Признак = ПредопределенноеЗначение("Перечисление.Scan_ПризнакТягачШасси.A");
	ИначеЕсли СимволПризнак = "B" Тогда
		Признак = ПредопределенноеЗначение("Перечисление.Scan_ПризнакТягачШасси.B");		
	КонецЕсли;	
	
	Возврат Признак;
КонецФункции 
// rarus tenkam 17.08.2020 mantis 16422 ---


Функция ЭтоМодельАвтобуса(МодельСсылка) Экспорт // rarus tenkam 16.06.2021 mantis 17741 +++
	
	Если НЕ ЗначениеЗаполнено(МодельСсылка) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ЭтоАвтобус = Ложь;
	НаименованиеМодели = СокрЛП(МодельСсылка.Наименование);
	НаименованиеМодели = ВРег(НаименованиеМодели);
	НаименованиеМодели = СтрЗаменить( СтрЗаменить(НаименованиеМодели, "Х","X"), "К","K");
	
	// В наименовании модели выполнить поиск символа «X»
	СимволХ = СтрНайти(НаименованиеМодели,"X");
	Если СимволХ = 0 Тогда
		Возврат ЭтоАвтобус;
	КонецЕсли;
	
	// Если присутствует символ «X», то для дальнейшего поиска используется только фрагмент наименования слева до этого символа
	НаименованиеМодели = Лев(НаименованиеМодели,СимволХ-1);
	
	// В оставшемся фрагменте наименования выполнить поиск символов «K»;
	СимволПризнака = СтрНайти(НаименованиеМодели,"K");
	Если СимволПризнака = 0 Тогда
		Возврат Ложь;
	Иначе
		// Проверим, что после К следуют числа
		ОстатокНаименования = Прав(НаименованиеМодели,СтрДлина(НаименованиеМодели) - СимволПризнака);
		Если СтрДлина(ОстатокНаименования) = 0 Тогда
			Возврат Ложь;
		Иначе
			ПервыйСимволПослеК = Лев(ОстатокНаименования,1);
			ОписаниеЧисла = Новый ОписаниеТипов("Число");
			
			Если ПервыйСимволПослеК = "0" ИЛИ ОписаниеЧисла.ПривестиЗначение(ПервыйСимволПослеК) <> 0 Тогда
				Возврат Истина;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли; 	 
	
	Возврат ЭтоАвтобус;
КонецФункции	// rarus tenkam 16.06.2021 mantis 17741 ---
