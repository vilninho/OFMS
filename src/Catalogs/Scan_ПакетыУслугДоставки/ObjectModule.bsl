// rarus tenkam 07.08.2017 mantis 10589 +++
Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	// Нельзя указывать Доставку
	СтрокаДоставка = СоставУслугПакета.Найти(Справочники.Scan_НоменклатураУслуг.Доставка);
	Если ЗначениеЗаполнено(СтрокаДоставка) Тогда
		Сообщение = Новый СообщениеПользователю();
		//rarus FominskiyAS 24.04.2019  mantis 14191 +++
		//Сообщение.Текст = "Услуга ""Доставка"" не может быть указана в составе пакета!";
		Сообщение.Текст = НСтр("ru = 'Услуга ""Доставка"" не может быть указана в составе пакета!'; en = 'Service ""Delivery"" can not be specified as part of the package!'");
		//rarus FominskiyAS 24.04.2019  mantis 14191 ---  
		Сообщение.Поле = "СоставУслугПакета[" + (СтрокаДоставка.НомерСтроки-1) + "].Услуга";
		Сообщение.УстановитьДанные(ЭтотОбъект);
		Сообщение.Сообщить();
		Отказ = Истина;
	КонецЕсли;
	
	// Нельзя одновременно указывать "Доставка полная" и "Доставка льготная"	
	Льготная = СоставУслугПакета.Найти(Справочники.Scan_НоменклатураУслуг.ДоставкаЛьготная);	
	// rarus tenkam 20.12.2018 mantis 13736 +++
	Если Льготная = Неопределено Тогда
		Льготная = СоставУслугПакета.Найти(Справочники.Scan_НоменклатураУслуг.ДоставкаЛьготнаяПоСкидке);		
	КонецЕсли;	
	// rarus tenkam 20.12.2018 mantis 13736 ---
	Полная = СоставУслугПакета.Найти(Справочники.Scan_НоменклатураУслуг.ДоставкаПолная);
	Если ЗначениеЗаполнено(Льготная) И ЗначениеЗаполнено(Полная) Тогда
		Сообщение = Новый СообщениеПользователю();
		//rarus FominskiyAS 24.04.2019  mantis 14191 +++
		//Сообщение.Текст = "В одном пакете не могут быть указаны и ""Доставка полная"", и ""Доставка льготная""";
		Сообщение.Текст = НСтр("ru = 'В одном пакете не могут быть указаны и ""Доставка полная, и ""Доставка льготная""'; en = 'You can not select two types of delivery in one package.'");
		//rarus FominskiyAS 24.04.2019  mantis 14191 ---  
		Сообщение.Поле = "СоставУслугПакета[" + (Льготная.НомерСтроки-1) + "].Услуга";
		Сообщение.УстановитьДанные(ЭтотОбъект);
		Сообщение.Сообщить();
		Отказ = Истина;
	КонецЕсли;
	
	// Проверка заполнения строк
	Для Индекс = 0 по СоставУслугПакета.Количество()-1 Цикл
		СтрокаУслуга = СоставУслугПакета.Получить(Индекс);
		Если Не ЗначениеЗаполнено(СтрокаУслуга.Услуга) Тогда
			Сообщение = Новый СообщениеПользователю();
			//rarus FominskiyAS 24.04.2019  mantis 14191 +++
			//Сообщение.Текст = "В строке " + (Индекс+1) + " не заполнено значение услуги";
			Если ТекущийЯзык()="en" тогда
				Сообщение.Текст = "Please add service in line " + (Индекс+1);				
			иначе
				Сообщение.Текст = "В строке " + (Индекс+1) + " не заполнено значение услуги";	
			КонецЕсли;
			//rarus FominskiyAS 24.04.2019  mantis 14191 ---  
			Сообщение.Поле = "СоставУслугПакета[" + Индекс + "].Услуга";
			Сообщение.УстановитьДанные(ЭтотОбъект);
			Сообщение.Сообщить();
			Отказ = Истина;
		КонецЕсли;
	КонецЦикла;
	
	// Проверка на дубли
	ТЗСоставУслуг = СоставУслугПакета.Выгрузить();
	ТЗСоставУслуг.Колонки.Добавить("Количество");
	Для Каждого ТекСтрока Из ТЗСоставУслуг Цикл
		ТекСтрока.Количество = 1;
	КонецЦикла;
	ТЗСоставУслуг.Свернуть("Услуга","Количество");
	Для Каждого ТекСтрока Из ТЗСоставУслуг Цикл
		Если ТекСтрока.Количество > 1 Тогда
			НайденнаяУслуга = СоставУслугПакета.Найти(ТекСтрока.Услуга);
			Сообщение = Новый СообщениеПользователю();
			//rarus FominskiyAS 24.04.2019  mantis 14191 +++
			//Сообщение.Текст = "Услуга " + ТекСтрока.Услуга + " уже указана в составе пакета";
			Сообщение.Текст = НСтр("ru = 'Услуга '; en = 'Service '") + ТекСтрока.Услуга + НСтр("ru = '  уже указана в составе пакета '; en = ' already included into the package '");
			//rarus FominskiyAS 24.04.2019  mantis 14191 ---  
			Сообщение.Поле = "СоставУслугПакета[" + (НайденнаяУслуга.НомерСтроки-1) + "].Услуга";
			Сообщение.УстановитьДанные(ЭтотОбъект);
			Сообщение.Сообщить();
			Отказ = Истина;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	Если ИспользуетсяПоУмолчанию Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Scan_ПакетыУслугДоставки.Ссылка
		|ИЗ
		|	Справочник.Scan_ПакетыУслугДоставки КАК Scan_ПакетыУслугДоставки
		|ГДЕ
		|	Scan_ПакетыУслугДоставки.ИспользуетсяПоУмолчанию = ИСТИНА
		|	И Scan_ПакетыУслугДоставки.Ссылка <> &Ссылка";
		
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		РезультатЗапроса = Запрос.Выполнить();
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		Если ВыборкаДетальныеЗаписи.Следующий() Тогда
			СтарыйОбъект = ВыборкаДетальныеЗаписи.Ссылка.ПолучитьОбъект();
			СтарыйОбъект.ИспользуетсяПоУмолчанию = Ложь;
			СтарыйОбъект.Записать();
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры
// rarus tenkam 07.08.2017 mantis 10589 ---