
&НаКлиенте
Процедура ОбъектМетаданныхПриИзменении(Элемент)
	ОбъектМетаданныхПриИзмененииНаСервере();	
КонецПроцедуры

&НаСервере
Процедура ОбъектМетаданныхПриИзмененииНаСервере()
	СписокРеквизитов = Справочники.Scan_КонтрольныеТочкиБизнесПроцессов.СформироватьСписокРеквизитов(Объект.ОбъектМетаданных); 
	Элементы.РеквизитОбъекта.СписокВыбора.ЗагрузитьЗначения(СписокРеквизитов);	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Справочники.Scan_КонтрольныеТочкиБизнесПроцессов.ПервоначальноеЗаполнение(Объект);
	
	СписокРеквизитов = Справочники.Scan_КонтрольныеТочкиБизнесПроцессов.СформироватьСписокРеквизитов(Объект.ОбъектМетаданных); 
	Элементы.РеквизитОбъекта.СписокВыбора.ЗагрузитьЗначения(СписокРеквизитов);	
	
	СписокСправочников = Справочники.Scan_КонтрольныеТочкиБизнесПроцессов.СформироватьСписокСправочников(); 
	Элементы.ОбъектМетаданных.СписокВыбора.ЗагрузитьЗначения(СписокСправочников);
	ЭтоНоваяКТ = НЕ ЗначениеЗаполнено(Объект.Ссылка);
	Scan_СборСтатистики.Scan_ПриОткрытии("Справочники", РеквизитФормыВЗначение("Объект").Метаданные().Синоним);	
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	НайденнаяКТ = Справочники.Scan_КонтрольныеТочкиБизнесПроцессов.НайтиПоНаименованию(СокрЛП(Объект.Наименование),Истина);
	Если НЕ (НайденнаяКТ = Справочники.Scan_КонтрольныеТочкиБизнесПроцессов.ПустаяСсылка()) Тогда
		//нашли с таким же наименованием
		Если НайденнаяКТ <> Объект.Ссылка Тогда
			Отказ = Истина;
			//rarus FominskiyAS 19.04.2019  mantis 14191 +++
			//Сообщить("Контрольная точка с таким наименованием уже есть!");
			Сообщить(НСтр("ru = 'Контрольная точка с таким наименованием уже есть!'; en = 'Same checkpoint already exists!'"));
			//rarus FominskiyAS 19.04.2019  mantis 14191 ---  
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ Отказ Тогда
		НайденнаяКТ = Справочники.Scan_КонтрольныеТочкиБизнесПроцессов.НайтиПоРеквизиту("НомерКТПоБизнесПроцессу",Объект.НомерКТПоБизнесПроцессу);
		Если НайденнаяКТ <> Неопределено И НайденнаяКТ <> Объект.Ссылка Тогда
			Отказ =  (НайденнаяКТ.ОбъектМетаданных = Объект.ОбъектМетаданных);
		КонецЕсли;
			
		Если Отказ = Истина Тогда
			//rarus FominskiyAS 19.04.2019  mantis 14191 +++
			//Сообщить("Контрольная точка с таким порядковым номером уже есть!");
			Сообщить(НСтр("ru = 'Контрольная точка с таким порядковым номером уже есть!'; en = 'Same checkpoint already exists!'"));
			//rarus FominskiyAS 19.04.2019  mantis 14191 ---  
		КонецЕсли;
		
	КонецЕсли;
	
	//rarus tenkam 27.06.2016 mantis 6925 ++
	Если НЕ Отказ Тогда
		НайденнаяКТ = Справочники.Scan_КонтрольныеТочкиБизнесПроцессов.НайтиПоРеквизиту("РеквизитОбъекта",Объект.РеквизитОбъекта);
		Если НайденнаяКТ <> Неопределено И НайденнаяКТ <> Объект.Ссылка Тогда
			Отказ =  (НайденнаяКТ.ОбъектМетаданных = Объект.ОбъектМетаданных);
		КонецЕсли;
			
		Если Отказ = Истина Тогда
			//rarus FominskiyAS 19.04.2019  mantis 14191 +++
			//Сообщить("Контрольная точка с таким реквизитом уже есть!");
			Сообщить(НСтр("ru = 'Контрольная точка с таким реквизитом уже есть!'; en = 'Same checkpoint already exists!'"));
			//rarus FominskiyAS 19.04.2019  mantis 14191 ---  
		КонецЕсли;
		
	КонецЕсли;
	//rarus tenkam 27.06.2016 mantis 6925 --
	
	// Rarus tenkam 11.04.2022 mantis 18433 +++
	Если Объект.Ссылка.Пустая() Тогда
		Scan_СборСтатистики.Scan_ПередЗаписьюСправочника(РеквизитФормыВЗначение("Объект").Метаданные().Синоним, Истина, "Создание нового элемента");
	КонецЕсли;
	// Rarus tenkam 11.04.2022 mantis 18433 ---
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	//rarus tenkam 16.06.2016 mantis 6925 ++
	Если ЭтоНоваяКТ Тогда
		ЗаписатьКТВРегистр();	
	КонецЕсли;
	//rarus tenkam 16.06.2016 mantis 6925 --
КонецПроцедуры

//rarus tenkam 16.06.2016 mantis 6925 ++
&НаСервере
Процедура ЗаписатьКТВРегистр()
	
	НаборЗаписей = РегистрыСведений.Scan_КонтрольныеТочкиАРМ.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.КонтрольнаяТочка.Установить(Объект.Ссылка);
	
	НоваяЗапись = НаборЗаписей.Добавить();
	
	НоваяЗапись.КонтрольнаяТочка = Объект.Ссылка;
	НоваяЗапись.АРМОтделаЛогистики = Ложь;
	НоваяЗапись.АРМОтделаАдминистрирования = Ложь;
	НоваяЗапись.АРМОтделаПланирования = Ложь;
	НоваяЗапись.АРМДилера = Ложь; //rarus ozhnik 17366 18.03.2021 + 
	НоваяЗапись.Пользователь = ПользователиКлиентСервер.ТекущийПользователь();
	
	НаборЗаписей.Записать();
КонецПроцедуры
//rarus tenkam 16.06.2016 mantis 6925 --
