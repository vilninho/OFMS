
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Объект.Ссылка.Пустая() Тогда
		ЗаполнитьТаблицуВидовLeadTime();
	Иначе
		СообщитьОВидахLeadTime();
	КонецЕсли;
	
	ОбновитьКонтрольныеТочкиПоВидамLeadTime();
	УстановитьУсловноеОформление();
	УправлениеДиалогомНаСервере();
	Scan_СборСтатистики.Scan_ПриОткрытии("Справочники", РеквизитФормыВЗначение("Объект").Метаданные().Синоним);	

КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	ОбновитьКонтрольныеТочкиПоВидамLeadTime();
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи) // Rarus tenkam 11.04.2022 mantis 18433 +++

	Если Объект.Ссылка.Пустая() Тогда
		Scan_СборСтатистики.Scan_ПередЗаписьюСправочника(РеквизитФормыВЗначение("Объект").Метаданные().Синоним, Истина, "Создание нового элемента");
	КонецЕсли;
КонецПроцедуры 	// Rarus tenkam 11.04.2022 mantis 18433 ---
#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ВидыLeadTimeВидLeadTimeОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	НайденныеСтроки = Объект.ВидыLeadTime.НайтиСтроки(Новый Структура("ВидLeadTime", ВыбранноеЗначение));
	Если  НайденныеСтроки.Количество() > 0
		И НайденныеСтроки[0].НомерСтроки <> ТекущийЭлемент.ТекущаяСтрока + 1
		Тогда
		СтандартнаяОбработка = Ложь;
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Вид Lead Time ""%1"" уже добавлен в таблицу в строке ""%2"". Нельзя добавить два одинаковых вида Lead Time.';
		                                |en = 'The Lead Time type ""1%"" is already added in the line ""2%"". You cannot add the same Lead Time type twice.'"), 
		                                ВыбранноеЗначение, НайденныеСтроки[0].НомерСтроки);
		Сообщить(ТекстСообщения);
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОбновитьВидыLeadTime(Команда)
	
	ОбновитьВидыLeadTimeНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьНеиспользуемыеВидыLeadTime(Команда)
	
	УдалитьНеиспользуемыеВидыLeadTimeНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьПредставлениеКонтрольныхТочек(Команда)
	
	ОбновитьКонтрольныеТочкиПоВидамLeadTime();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УправлениеДиалогомНаСервере()
	
	ПравоДоступаРедактирование = ПравоДоступа("Редактирование", Метаданные.Справочники.Scan_КонтрольныеТочкиLeadTime);
	
	Элементы.ВидыLeadTimeОбновитьВидыLeadTime.Доступность              = ПравоДоступаРедактирование;
	Элементы.ВидыLeadTimeУдалитьНеиспользуемыеВидыLeadTime.Доступность = ПравоДоступаРедактирование;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуВидовLeadTime()
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	Scan_ВидыLeadTime.Ссылка КАК ВидLeadTime
	|ИЗ
	|	Справочник.Scan_ВидыLeadTime КАК Scan_ВидыLeadTime
	|ГДЕ
	|	Scan_ВидыLeadTime.Используется
	|	И НЕ Scan_ВидыLeadTime.ПометкаУдаления";
	РезультатЗапроса = Запрос.Выполнить();
	
	Объект.ВидыLeadTime.Загрузить(РезультатЗапроса.Выгрузить());
	
КонецПроцедуры

&НаСервере
Процедура СообщитьОВидахLeadTime()
	
	ИспользуемыеВидыLeadTime = ПолучитьИспользуемыеВидыLeadTime();
	Если ИспользуемыеВидыLeadTime.Количество() > 0 Тогда
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Виды Lead Time ""%1"" используются, но не указаны в данной контрольной точке. Их можно добавить по кнопке ""Обновить"".';
		                                |en = 'The Lead Time types ""1%"" are used, but not specified in this Control Point. You can add them by ""Refresh"" button.'"), 
		                                СтрСоединить(ИспользуемыеВидыLeadTime, ", "));
		Сообщить(ТекстСообщения);
	КонецЕсли;
	
	НеиспользуемыеВидыLeadTime = ПолучитьНеиспользуемыеВидыLeadTime();
	Если НеиспользуемыеВидыLeadTime.Количество() > 0 Тогда
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Виды Lead Time ""%1"" не используются, но указаны в данной контрольной точке. Их можно удалить по кнопке ""Удалить неиспользуемые"".';
		                                |en = 'The Lead Time types ""1%"" are not used, but specified in this Control Point. You can delete them by ""Delete unused"" button.'"), 
		                                СтрСоединить(НеиспользуемыеВидыLeadTime, ", "));
		Сообщить(ТекстСообщения);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	НеиспользуемыеВидыLeadTime = Новый СписокЗначений;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	Scan_ВидыLeadTime.Ссылка КАК ВидLeadTime
	|ИЗ
	|	Справочник.Scan_ВидыLeadTime КАК Scan_ВидыLeadTime
	|ГДЕ
	|	НЕ Scan_ВидыLeadTime.Используется
	|	ИЛИ Scan_ВидыLeadTime.ПометкаУдаления";
	РезультатЗапроса = Запрос.Выполнить();
	
	НеиспользуемыеВидыLeadTime.ЗагрузитьЗначения(РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("ВидLeadTime"));
	
	УсловноеОформление.Элементы.Очистить();

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ВидыLeadTime.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("Объект.ВидыLeadTime.ВидLeadTime");
	ОтборЭлемента.ВидСравнения   = ВидСравненияКомпоновкиДанных.ВСписке;
	ОтборЭлемента.ПравоеЗначение = НеиспользуемыеВидыLeadTime;
	ОтборЭлемента.Использование  = Истина;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветФона", WebЦвета.ТусклоРозовый);
	
КонецПроцедуры

&НаСервере
Функция ПолучитьИспользуемыеВидыLeadTime()
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("П1", Объект.ВидыLeadTime.Выгрузить().ВыгрузитьКолонку("ВидLeadTime"));
	Запрос.Текст = "ВЫБРАТЬ
	|	Scan_ВидыLeadTime.Ссылка КАК ВидLeadTime
	|ИЗ
	|	Справочник.Scan_ВидыLeadTime КАК Scan_ВидыLeadTime
	|ГДЕ
	|	Scan_ВидыLeadTime.Используется
	|	И НЕ Scan_ВидыLeadTime.ПометкаУдаления
	|	И НЕ Scan_ВидыLeadTime.Ссылка В(&П1)";
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("ВидLeadTime");
	
КонецФункции

&НаСервере
Функция ПолучитьНеиспользуемыеВидыLeadTime()
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("П1", Объект.ВидыLeadTime.Выгрузить().ВыгрузитьКолонку("ВидLeadTime"));
	Запрос.Текст = "ВЫБРАТЬ
	|	Scan_ВидыLeadTime.Ссылка КАК ВидLeadTime
	|ИЗ
	|	Справочник.Scan_ВидыLeadTime КАК Scan_ВидыLeadTime
	|ГДЕ
	|	(НЕ Scan_ВидыLeadTime.Используется
	|			ИЛИ Scan_ВидыLeadTime.ПометкаУдаления)
	|	И Scan_ВидыLeadTime.Ссылка В(&П1)";
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("ВидLeadTime");
	
КонецФункции

&НаСервере
Процедура ОбновитьВидыLeadTimeНаСервере()
	
	ИспользуемыеВидыLeadTime = ПолучитьИспользуемыеВидыLeadTime();
	Для Каждого ИспользуемыйВидыLeadTime Из ИспользуемыеВидыLeadTime Цикл
		НоваяСтрокаLeadTime = Объект.ВидыLeadTime.Добавить();
		НоваяСтрокаLeadTime.ВидLeadTime = ИспользуемыйВидыLeadTime;
	КонецЦикла;
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаСервере
Процедура УдалитьНеиспользуемыеВидыLeadTimeНаСервере()
	
	УдаляемыеСтрокиВидовLeadTime = Новый Массив;
	
	НеиспользуемыеВидыLeadTime = ПолучитьНеиспользуемыеВидыLeadTime();
	Для Каждого НеиспользуемыйВидыLeadTime Из НеиспользуемыеВидыLeadTime Цикл
		НайденныеСтроки = Объект.ВидыLeadTime.НайтиСтроки(Новый Структура("ВидLeadTime", НеиспользуемыйВидыLeadTime));
		
		Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл
			УдаляемыеСтрокиВидовLeadTime.Добавить(НайденнаяСтрока);
		КонецЦикла;
	КонецЦикла;
	
	Для Каждого УдаляемаяСтрока Из УдаляемыеСтрокиВидовLeadTime Цикл
		Объект.ВидыLeadTime.Удалить(УдаляемаяСтрока);
	КонецЦикла;
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьКонтрольныеТочкиПоВидамLeadTime()
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	Scan_КонтрольныеТочкиLeadTimeВидыLeadTime.Ссылка КАК КонтрольнаяТочка,
	|	Scan_КонтрольныеТочкиLeadTimeВидыLeadTime.ВидLeadTime КАК ВидLeadTime,
	|	Scan_КонтрольныеТочкиLeadTimeВидыLeadTime.Порядок КАК Порядок
	|ИЗ
	|	Справочник.Scan_КонтрольныеТочкиLeadTime.ВидыLeadTime КАК Scan_КонтрольныеТочкиLeadTimeВидыLeadTime
	|ГДЕ
	|	Scan_КонтрольныеТочкиLeadTimeВидыLeadTime.Ссылка.Используется
	|	И НЕ Scan_КонтрольныеТочкиLeadTimeВидыLeadTime.Ссылка.ПометкаУдаления
	|	И Scan_КонтрольныеТочкиLeadTimeВидыLeadTime.ВидLeadTime.Используется
	|	И НЕ Scan_КонтрольныеТочкиLeadTimeВидыLeadTime.ВидLeadTime.ПометкаУдаления
	|	И Scan_КонтрольныеТочкиLeadTimeВидыLeadTime.Порядок <> 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВидLeadTime,
	|	Порядок
	|ИТОГИ ПО
	|	ВидLeadTime";
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	КонтрольныеТочкиПоВидамLeadTime.Очистить();
	
	Макет = Справочники.Scan_КонтрольныеТочкиLeadTime.ПолучитьМакет("КонтрольныеТочкиПоВидамLeadTime");
	
	ВыборкаВидLeadTime = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "ВидLeadTime");
	Пока ВыборкаВидLeadTime.Следующий() Цикл
		
		ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
		ОбластьЗаголовок.Параметры.ВидLeadTime = ВыборкаВидLeadTime.ВидLeadTime;
		
		КонтрольныеТочкиПоВидамLeadTime.Вывести(ОбластьЗаголовок);
		
		ОбластьОтступГоризонтальный = Макет.ПолучитьОбласть("ОтступГоризонтальный");
		КонтрольныеТочкиПоВидамLeadTime.Вывести(ОбластьОтступГоризонтальный);
		
		ОбластьСтрокаОтступВертикальный = Макет.ПолучитьОбласть("Строка|ОтступВертикальный");
		КонтрольныеТочкиПоВидамLeadTime.Вывести(ОбластьСтрокаОтступВертикальный);
		
		ОбластьСтрокаОписание = Макет.ПолучитьОбласть("Строка|Описание");
		КонтрольныеТочкиПоВидамLeadTime.Присоединить(ОбластьСтрокаОписание);
		
		Выборка = ВыборкаВидLeadTime.Выбрать();
		Пока Выборка.Следующий() Цикл
			ОбластьСтрокаКонтрольнаяТочка = Макет.ПолучитьОбласть("Строка|КонтрольнаяТочка");
			ОбластьСтрокаКонтрольнаяТочка.Параметры.КонтрольнаяТочка = Выборка.КонтрольнаяТочка;
			ОбластьСтрокаКонтрольнаяТочка.Параметры.Порядок          = Выборка.Порядок;
			
			ПрисоединеннаяОбласть = КонтрольныеТочкиПоВидамLeadTime.Присоединить(ОбластьСтрокаКонтрольнаяТочка);
			Если Выборка.КонтрольнаяТочка = Объект.Ссылка Тогда
				ПрисоединеннаяОбласть.ЦветФона = WebЦвета.Роса;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры



#КонецОбласти

