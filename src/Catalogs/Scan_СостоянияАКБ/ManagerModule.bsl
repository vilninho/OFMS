// rarus tenkam 07.09.2017 mantis 9425 +++
Функция ПолучитьСостояниеПоХарактеристикам(СоответствиеХарактеристик) Экспорт
	//СоответствиеХарактеристик = новый Соответствие;
	ТаблицаСостояний = Новый ТаблицаЗначений;
	ТаблицаСостояний.Колонки.Добавить("Характеристика");
	ТаблицаСостояний.Колонки.Добавить("СостояниеХарактеристики");	
	ТаблицаСостояний.Колонки.Добавить("УровеньСостояния");
	
	Для Каждого ТекПараметр Из СоответствиеХарактеристик Цикл
		
		Если НЕ ЗначениеЗаполнено(ТекПараметр.Значение) Тогда
			Продолжить;
		КонецЕсли;
		
		Если ТекПараметр.Ключ = "Емкость" Тогда
			Продолжить;
		КонецЕсли;
		
		Если ТекПараметр.Ключ = Перечисления.Scan_ХарактеристикиАКБ.ПусковойТок Тогда
			ТекЕмкость = СоответствиеХарактеристик.Получить("Емкость");
			Если НЕ ЗначениеЗаполнено(ТекЕмкость) Тогда
				Продолжить;
			КонецЕсли;			
		Иначе
			ТекЕмкость = Неопределено;
		КонецЕсли;
		МассивВсехСостоянийХарактеристики = ПолучитьМассивСостоянийХарактеристики(ТекПараметр.Ключ, ТекЕмкость);
		СостояниеХарактеристики = ПолучитьСостояниеХарактеристики(МассивВсехСостоянийХарактеристики, ТекПараметр.Значение);		
		Если ЗначениеЗаполнено(СостояниеХарактеристики) Тогда
			НоваяСтрока = ТаблицаСостояний.Добавить();
			НоваяСтрока.Характеристика = ТекПараметр.Ключ;
			НоваяСтрока.СостояниеХарактеристики = СостояниеХарактеристики;
			НоваяСтрока.УровеньСостояния = СостояниеХарактеристики.УровеньСостояния;
		КонецЕсли;			
	КонецЦикла;
	
	Если ТаблицаСостояний.Количество() <> 0 Тогда      		
		ТаблицаСостояний.Сортировать("УровеньСостояния");
		СостояниеОбщее = ТаблицаСостояний.ВыгрузитьКолонку("СостояниеХарактеристики")[0]; 		
		Возврат СостояниеОбщее;	
	Иначе
		Возврат Справочники.Scan_СостоянияАКБ.ПустаяСсылка();
	КонецЕсли;

КонецФункции

Функция ПолучитьМассивСостоянийХарактеристики(ТекХарактеристика, Емкость = Неопределено)
	МассивСостояний = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Scan_СостоянияАКБХарактеристикиАКБ.Емкость КАК Емкость,
	|	Scan_СостоянияАКБХарактеристикиАКБ.ВидСравненияЛевый КАК ВидСравненияЛевый,
	|	Scan_СостоянияАКБХарактеристикиАКБ.ЛеваяГраница КАК ЛеваяГраница,
	|	Scan_СостоянияАКБХарактеристикиАКБ.ВидСравненияПравый КАК ВидСравненияПравый,
	|	Scan_СостоянияАКБХарактеристикиАКБ.ПраваяГраница КАК ПраваяГраница,
	|	Scan_СостоянияАКБХарактеристикиАКБ.Ссылка КАК Состояние
	|ИЗ
	|	Справочник.Scan_СостоянияАКБ.ХарактеристикиАКБ КАК Scan_СостоянияАКБХарактеристикиАКБ
	|ГДЕ
	|	Scan_СостоянияАКБХарактеристикиАКБ.Характеристика = &Характеристика
	|	//Емкость";
	
	Запрос.УстановитьПараметр("Характеристика", ТекХарактеристика);
	Если ТекХарактеристика = Перечисления.Scan_ХарактеристикиАКБ.ПусковойТок Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "//Емкость", "И Scan_СостоянияАКБХарактеристикиАКБ.Емкость = &Емкость");
		Запрос.УстановитьПараметр("Емкость", Емкость);
	КонецЕсли;
	РезультатЗапроса = Запрос.Выполнить(); 	
	ВыборкаЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаЗаписи.Следующий() Цикл
		Если НЕ ЗначениеЗаполнено(ВыборкаЗаписи.ВидСравненияЛевый) И НЕ ЗначениеЗаполнено(ВыборкаЗаписи.ЛеваяГраница) И
			НЕ ЗначениеЗаполнено(ВыборкаЗаписи.ВидСравненияПравый) И НЕ ЗначениеЗаполнено(ВыборкаЗаписи.ПраваяГраница) Тогда
			Продолжить;
		КонецЕсли;
		
		СтруктураСостояния = Новый Структура;
		СтруктураСостояния.Вставить("Состояние",ВыборкаЗаписи.Состояние);
		
		Если Не ЗначениеЗаполнено(ВыборкаЗаписи.ЛеваяГраница) Тогда
			СтруктураСостояния.Вставить("ВидСравненияЛевый", Перечисления.Scan_ВидСравнения.БольшеИлиРавно);
			СтруктураСостояния.Вставить("ЛеваяГраница",0);
		Иначе
			СтруктураСостояния.Вставить("ВидСравненияЛевый",ВыборкаЗаписи.ВидСравненияЛевый);
			СтруктураСостояния.Вставить("ЛеваяГраница",ВыборкаЗаписи.ЛеваяГраница);
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(ВыборкаЗаписи.ПраваяГраница) И ВыборкаЗаписи.ВидСравненияЛевый <> Перечисления.Scan_ВидСравнения.Равно Тогда
			СтруктураСостояния.Вставить("ВидСравненияПравый", Перечисления.Scan_ВидСравнения.МеньшеИлиРавно);
			СтруктураСостояния.Вставить("ПраваяГраница",999999999999999);
		Иначе
			СтруктураСостояния.Вставить("ВидСравненияПравый",ВыборкаЗаписи.ВидСравненияПравый);
			СтруктураСостояния.Вставить("ПраваяГраница",ВыборкаЗаписи.ПраваяГраница);
		КонецЕсли;
		
		МассивСостояний.Добавить(СтруктураСостояния);		
	КонецЦикла;
	Возврат МассивСостояний;	
	
КонецФункции

Функция ПолучитьСостояниеХарактеристики(МассивСостояний, ЗначениеХарактеристики)
	ТаблицаСостояний = Новый ТаблицаЗначений;
	ТаблицаСостояний.Колонки.Добавить("Состояние");
	ТаблицаСостояний.Колонки.Добавить("УровеньСостояния");
	
	Для Каждого ТекСостояние Из МассивСостояний Цикл
		ВсеОк = Ложь;
		//Для равенства своя проверка
		Если Не ЗначениеЗаполнено(ТекСостояние.ПраваяГраница) И ТекСостояние.ВидСравненияЛевый = Перечисления.Scan_ВидСравнения.Равно Тогда
			Если ЗначениеХарактеристики = ТекСостояние.ЛеваяГраница Тогда
				ВсеОк = Истина;
			КонецЕсли;
		Иначе
		//Общая проверка
			ПокаВсеОк = Ложь;
			Если ТекСостояние.ВидСравненияПравый = Перечисления.Scan_ВидСравнения.Меньше Тогда
				Если ЗначениеХарактеристики < ТекСостояние.ПраваяГраница Тогда
					ПокаВсеОк = Истина;
				КонецЕсли;
			ИначеЕсли ТекСостояние.ВидСравненияПравый = Перечисления.Scan_ВидСравнения.МеньшеИлиРавно Тогда
				Если ЗначениеХарактеристики <= ТекСостояние.ПраваяГраница Тогда
					ПокаВсеОк = Истина;
				КонецЕсли;
			КонецЕсли;
			Если ПокаВсеОк Тогда
				Если ТекСостояние.ВидСравненияЛевый = Перечисления.Scan_ВидСравнения.Больше Тогда
					Если ЗначениеХарактеристики > ТекСостояние.ЛеваяГраница Тогда
						ВсеОк = Истина;
					КонецЕсли;
				ИначеЕсли ТекСостояние.ВидСравненияЛевый = Перечисления.Scan_ВидСравнения.БольшеИлиРавно Тогда
					Если ЗначениеХарактеристики >= ТекСостояние.ЛеваяГраница Тогда
						ВсеОк = Истина;
					КонецЕсли;
				КонецЕсли; 
			КонецЕсли;
		КонецЕсли;
		
		Если ВсеОк Тогда
			НоваяСтрока = ТаблицаСостояний.Добавить();
			НоваяСтрока.Состояние = ТекСостояние.Состояние;
			НоваяСтрока.УровеньСостояния = ТекСостояние.Состояние.УровеньСостояния;
		КонецЕсли;		
	КонецЦикла;
	
	Если ТаблицаСостояний.Количество() <> 0 Тогда      		
		ТаблицаСостояний.Сортировать("УровеньСостояния");
		СостояниеХарактеристики = ТаблицаСостояний.ВыгрузитьКолонку("Состояние")[0]; 		
		Возврат СостояниеХарактеристики;	
	Иначе
		Возврат Справочники.Scan_СостоянияАКБ.ПустаяСсылка();
	КонецЕсли;

КонецФункции

// rarus tenkam 07.09.2017 mantis 9425 ---