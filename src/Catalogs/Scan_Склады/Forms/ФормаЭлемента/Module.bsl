
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		МассивМестХранения = РегистрыСведений.Scan_СоответствиеСкладовИМестХранения.ПолучитьМестоХранения(Объект.Ссылка);
		Для Каждого МестоХранения Из МассивМестХранения Цикл
			НовоеМесто = МестаХранения.Добавить();
			НовоеМесто.МестоХранения = МестоХранения;
		КонецЦикла;
	КонецЕсли;
	Scan_СборСтатистики.Scan_ПриОткрытии("Справочники", РеквизитФормыВЗначение("Объект").Метаданные().Синоним);	

КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	НаборЗаписей = РегистрыСведений.Scan_СоответствиеСкладовИМестХранения.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Склад.Установить(ТекущийОбъект.Ссылка);
	НаборЗаписей.Записать();
	
	Для Каждого СтрокаМестаХранения Из МестаХранения Цикл
		РегистрыСведений.Scan_СоответствиеСкладовИМестХранения.ЗаписатьСоответствие(СтрокаМестаХранения.МестоХранения, ТекущийОбъект.Ссылка);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	СообщениеОбОшибке = "";
	
	Для Каждого СтрокаМестаХранения Из МестаХранения Цикл
		Соответствует = РегистрыСведений.Scan_СоответствиеСкладовИМестХранения.ПроверитьКорректностьСоответствияПоКоду(СтрокаМестаХранения.МестоХранения, ТекущийОбъект.Ссылка, СообщениеОбОшибке);
		Если Не Соответствует Тогда
			Сообщить(СообщениеОбОшибке);
			Отказ = Не Соответствует Или Отказ;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура МестаХраненияМестоХраненияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыОтбора = Новый Структура();
	ПараметрыОтбора.Вставить("Маршрут",          Ложь);
	ПараметрыОтбора.Вставить("Недействительный", Ложь);
	
	ПараметрыФормы = Новый Структура("Отбор", ПараметрыОтбора);
	
	Результат = ОткрытьФорму("Справочник.Scan_МестаХранения.ФормаВыбора", ПараметрыФормы, Элементы.МестаХраненияМестоХранения);
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи) // Rarus tenkam 11.04.2022 mantis 18433 +++

	Если Объект.Ссылка.Пустая() Тогда
		Scan_СборСтатистики.Scan_ПередЗаписьюСправочника(РеквизитФормыВЗначение("Объект").Метаданные().Синоним, Истина, "Создание нового элемента");
	КонецЕсли;
КонецПроцедуры 	// Rarus tenkam 11.04.2022 mantis 18433 --- 
