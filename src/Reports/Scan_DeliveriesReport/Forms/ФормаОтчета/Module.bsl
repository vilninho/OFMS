//rarus pechek 25.02.2020 mantis 15409 +++
#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Элементы.Результат.ОтображениеСостояния.Видимость = ложь;
	Элементы.Результат.ОтображениеСостояния.ДополнительныйРежимОтображения = ДополнительныйРежимОтображения.НеИспользовать;
	ПараметрыВывода = УправлениеПечатью.ПодготовитьСтруктуруПараметровВывода();
	ЭтаФорма.Отчет.НаДату = ТекущаяДата();
	
	//rarus vikhle 08.05.2020 mt 15409 +++
	//Заполнение отбора по виду продукта по умолчанию
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	Scan_ВидыПродуктов.Ссылка КАК ВидПродукта
	               |ИЗ
	               |	Справочник.Scan_ВидыПродуктов КАК Scan_ВидыПродуктов
	               |ГДЕ
	               |	Scan_ВидыПродуктов.IDExternalSystem <> """"
	               |	И Scan_ВидыПродуктов.Наименование = ""Грузовые автомобили""";
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Количество() <> 0 Тогда
		Выборка.Следующий();
		Отчет.ВидПродукта = Выборка.ВидПродукта;	
	КонецЕсли;
	//rarus vikhle 08.05.2020 mt 15409 ---
	
	Scan_СборСтатистики.Scan_ПриОткрытииОтчета(РеквизитФормыВЗначение("Отчет").Метаданные().Синоним); // Rarus tenkam 11.04.2022 mantis 18433 +

КонецПроцедуры
#КонецОбласти
//rarus pechek 25.02.2020 mantis 15409 ---

//rarus pechek 25.02.2020 mantis 15409 +++
&НаСервере
Процедура КомандаСформироватьНаСервере()
	ОбъектОтчет = РеквизитФормыВЗначение("Отчет");
	ОбъектОтчет.СформироватьОтчет(Результат);
КонецПроцедуры
//rarus pechek 25.02.2020 mantis 15409 ---

//rarus pechek 25.02.2020 mantis 15409 +++
&НаКлиенте
Процедура КомандаСформировать(Команда)
	КомандаСформироватьНаСервере();
КонецПроцедуры

//rarus pechek 25.02.2020 mantis 15409 ---

//rarus vikhle 27.02.2020 mt 15409 +++
&НаКлиенте
Процедура Отправить(Команда)
	Если Результат.области.Количество() = 0 Тогда
		ТекстВопроса = НСтр("ru = 'Отчет не сформирован. Сформировать?'");
		Обработчик = Новый ОписаниеОповещения("СформироватьПередОтправкойПоПочте", ЭтотОбъект);
		ПоказатьВопрос(Обработчик, ТекстВопроса, РежимДиалогаВопрос.ДаНет, 60, КодВозвратаДиалога.Да);
	Иначе
		ПоказатьДиалогОтправкиПоПочте();
	КонецЕсли;	
КонецПроцедуры
//rarus vikhle 27.02.2020 mt 15409 ---

//rarus vikhle 27.02.2020 mt 15409 +++
&НаКлиенте
Процедура ПоказатьДиалогОтправкиПоПочте()
		
	Если Отчет.ВариантОтчета = 0 Тогда
		НаименованиеВариантаОтчета = "Отчет Deliveries общий " + Строка(Формат(Отчет.НаДату,"ДФ=dd.MM.yyyy"));
	ИначеЕсли Отчет.ВариантОтчета = 1 Тогда
		НаименованиеВариантаОтчета = "Отчет Deliveries независимый " + Строка(Формат(Отчет.НаДату,"ДФ=dd.MM.yyyy"));
	Иначе
		НаименованиеВариантаОтчета = "Отчет Deliveries собственный " + Строка(Формат(Отчет.НаДату,"ДФ=dd.MM.yyyy"));	
	КонецЕсли;
	 
	Вложение = Новый Структура;
	Вложение.Вставить("АдресВоВременномХранилище", ПоместитьВоВременноеХранилище(Результат, УникальныйИдентификатор));
	Вложение.Вставить("Представление",  НаименованиеВариантаОтчета);
	
	СписокВложений = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Вложение);
	
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.РаботаСПочтовымиСообщениями") Тогда
		ШаблонОтправки = Scan_ПраваИНастройки.Scan_Право("ШаблонПисьмаСОтчетомDeliveriesReport");
		МодульРаботаСПочтовымиСообщениямиКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("РаботаСПочтовымиСообщениямиКлиент");
		ПараметрыОтправки = МодульРаботаСПочтовымиСообщениямиКлиент.ПараметрыОтправкиПисьма();
		ПараметрыОтправки.Вставить("Кому",ПолучитьИзШаблона(ШаблонОтправки,"Получатель"));
		ПараметрыОтправки.Тема = ПолучитьИзШаблона(ШаблонОтправки,"Тема") + " " + Строка(Формат(Отчет.НаДату,"ДФ=dd.MM.yyyy"));
		ПараметрыОтправки.Вложения = СписокВложений;
		ПараметрыОтправки.Вставить("Тело",ПолучитьИзШаблона(ШаблонОтправки,"Тело"));
		МодульРаботаСПочтовымиСообщениямиКлиент.СоздатьНовоеПисьмо(ПараметрыОтправки);
	КонецЕсли;
	
КонецПроцедуры
//rarus vikhle 27.02.2020 mt 15409 ---

//rarus vikhle 27.02.2020 mt 15409 +++
&НаСервереБезКонтекста
Функция ПолучитьИзШаблона(Шаблон, Параметр)
	Если Параметр = "Тема" Тогда
		Возврат Справочники.Scan_ШаблоныПисем.ПолучитьЗаголовокПисьма(Шаблон,Неопределено);
	ИначеЕсли Параметр = "Получатель" Тогда 
		Возврат Справочники.Scan_ШаблоныПисем.ПолучитьПолучателейПисьма(Шаблон,Неопределено);
	Иначе
		Возврат Справочники.Scan_ШаблоныПисем.СформироватьТекст(Шаблон,Неопределено);
	КонецЕсли;
	
КонецФункции
//rarus vikhle 27.02.2020 mt 15409 ---

//rarus vikhle 27.02.2020 mt 15409 +++
&НаКлиенте
Процедура СформироватьПередОтправкойПоПочте (Ответ, ДополнительныеПараметры) Экспорт
	Если Ответ = КодВозвратаДиалога.Да Тогда
		КомандаСформироватьНаСервере();	
	КонецЕсли;	
КонецПроцедуры
//rarus vikhle 27.02.2020 mt 15409 ---
