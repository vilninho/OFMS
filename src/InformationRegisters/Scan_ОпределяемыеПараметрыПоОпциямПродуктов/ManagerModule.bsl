//процедура обновляет определяемые параметры
Процедура ОбновитьОпределяемыеПараметры(ИзделиеСсылка) Экспорт //rarus bonmak 08.10.2019 14177 ++
			
	ТаблицаОпций = ПолучитьПоследнююТаблицуБазовыхОпций(ИзделиеСсылка);
	
	Если ТаблицаОпций.Количество() = 0 Тогда
		Возврат;	
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТаблицаОпций.Опция КАК Опция
		|ПОМЕСТИТЬ ТабОпции
		|ИЗ
		|	&ТаблицаОпций КАК ТаблицаОпций
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Scan_ОпределяемыеПараметрыПоОпциямПродуктовСписокОпций.Ссылка КАК Ссылка,
		|	СУММА(ВЫБОР
		|			КОГДА Scan_ОпределяемыеПараметрыПоОпциямПродуктовСписокОпций.Ссылка.ТипСочетанияОпций = ЗНАЧЕНИЕ(Перечисление.Scan_ТипыСочетанияОпций.ВсеОдновременно)
		|				ТОГДА 1
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК ОбщееКолвоОдновременно
		|ПОМЕСТИТЬ ОбщееКоличествоОдновременно
		|ИЗ
		|	Справочник.Scan_ОпределяемыеПараметрыПоОпциямПродуктов.СписокОпций КАК Scan_ОпределяемыеПараметрыПоОпциямПродуктовСписокОпций
		|ГДЕ
		|	Scan_ОпределяемыеПараметрыПоОпциямПродуктовСписокОпций.Ссылка.Используется
		|
		|СГРУППИРОВАТЬ ПО
		|	Scan_ОпределяемыеПараметрыПоОпциямПродуктовСписокОпций.Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Scan_ОпределяемыеПараметрыПоОпциямПродуктовСписокОпций.Ссылка КАК Ссылка,
		|	СУММА(ВЫБОР
		|			КОГДА Scan_ОпределяемыеПараметрыПоОпциямПродуктовСписокОпций.Ссылка.ТипСочетанияОпций = ЗНАЧЕНИЕ(Перечисление.Scan_ТипыСочетанияОпций.ВсеОдновременно)
		|				ТОГДА 1
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК ОдновременноКоличество
		|ПОМЕСТИТЬ ПромежуточнаяТаблица
		|ИЗ
		|	ТабОпции КАК ТабОпции
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Scan_ОпределяемыеПараметрыПоОпциямПродуктов.СписокОпций КАК Scan_ОпределяемыеПараметрыПоОпциямПродуктовСписокОпций
		|		ПО ТабОпции.Опция = Scan_ОпределяемыеПараметрыПоОпциямПродуктовСписокОпций.Опция
		|ГДЕ
		|	Scan_ОпределяемыеПараметрыПоОпциямПродуктовСписокОпций.Ссылка.Используется
		|
		|СГРУППИРОВАТЬ ПО
		|	Scan_ОпределяемыеПараметрыПоОпциямПродуктовСписокОпций.Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПромежуточнаяТаблица.Ссылка КАК Параметр
		|ПОМЕСТИТЬ ТаблицаСНовымиПараметрами
		|ИЗ
		|	ПромежуточнаяТаблица КАК ПромежуточнаяТаблица
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОбщееКоличествоОдновременно КАК ОбщееКоличествоОдновременно
		|		ПО ПромежуточнаяТаблица.Ссылка = ОбщееКоличествоОдновременно.Ссылка
		|ГДЕ
		|	ПромежуточнаяТаблица.ОдновременноКоличество = ОбщееКоличествоОдновременно.ОбщееКолвоОдновременно
		|
		|СГРУППИРОВАТЬ ПО
		|	ПромежуточнаяТаблица.Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЕСТЬNULL(Scan_ОпределяемыеПараметрыПоОпциямПродуктовСрезПоследних.Период, &ТекДата) КАК Период,
		|	ЕСТЬNULL(Scan_ОпределяемыеПараметрыПоОпциямПродуктовСрезПоследних.Изделие, &Изделие) КАК Изделие,
		|	ЕСТЬNULL(Scan_ОпределяемыеПараметрыПоОпциямПродуктовСрезПоследних.Параметр, ТаблицаСНовымиПараметрами.Параметр) КАК Параметр,
		|	ВЫБОР
		|		КОГДА ТаблицаСНовымиПараметрами.Параметр ЕСТЬ NULL
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК Удалить
		|ИЗ
		|	РегистрСведений.Scan_ОпределяемыеПараметрыПоОпциямПродуктов.СрезПоследних(, Изделие = &Изделие) КАК Scan_ОпределяемыеПараметрыПоОпциямПродуктовСрезПоследних
		|		ПОЛНОЕ СОЕДИНЕНИЕ ТаблицаСНовымиПараметрами КАК ТаблицаСНовымиПараметрами
		|		ПО Scan_ОпределяемыеПараметрыПоОпциямПродуктовСрезПоследних.Параметр = ТаблицаСНовымиПараметрами.Параметр
		|
		|СГРУППИРОВАТЬ ПО
		|	ЕСТЬNULL(Scan_ОпределяемыеПараметрыПоОпциямПродуктовСрезПоследних.Период, &ТекДата),
		|	ЕСТЬNULL(Scan_ОпределяемыеПараметрыПоОпциямПродуктовСрезПоследних.Изделие, &Изделие),
		|	ВЫБОР
		|		КОГДА ТаблицаСНовымиПараметрами.Параметр ЕСТЬ NULL
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ,
		|	ЕСТЬNULL(Scan_ОпределяемыеПараметрыПоОпциямПродуктовСрезПоследних.Параметр, ТаблицаСНовымиПараметрами.Параметр)";
	
	ТекДата = ТекущаяДата();
	Запрос.УстановитьПараметр("Изделие", ИзделиеСсылка);
	Запрос.УстановитьПараметр("ТекДата", ТекДата);
	Запрос.УстановитьПараметр("ТаблицаОпций", ТаблицаОпций);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Запись = РегистрыСведений.Scan_ОпределяемыеПараметрыПоОпциямПродуктов.СоздатьМенеджерЗаписи();
		Запись.Изделие = ВыборкаДетальныеЗаписи.Изделие;
		Запись.Параметр = ВыборкаДетальныеЗаписи.Параметр;
		Запись.Период = ВыборкаДетальныеЗаписи.Период;
		Запись.Прочитать();
		Если ВыборкаДетальныеЗаписи.Удалить Тогда
			Запись.Удалить();
		ИначеЕсли НЕ Запись.Выбран() Тогда
			Запись.Период = ТекДата;
			Запись.Изделие = ВыборкаДетальныеЗаписи.Изделие;
			Запись.Параметр = ВыборкаДетальныеЗаписи.Параметр;
			Запись.Записать();	
		КонецЕсли;	
	КонецЦикла;
	
КонецПроцедуры //rarus bonmak 08.10.2019 14177 --

//функция получает таблицу опций последнего вида базовых опций
//обновлять определяемые параметры следует согласно имеющейся версии спецификации(если нет 1, то список опций берется из 2 и так далее)
//1 COW спецификация подтвержденная заводом
//2 COW спецификация заказанная (примечание: это 1-я версия спецификации из COW = спецификации SPORT при размещении заказа на завод)
//3 SPORT спецификация (ее получать в момент загрузки данных по КП в заявке на СОП)

Функция ПолучитьПоследнююТаблицуБазовыхОпций(ИзделиеСсылка) //rarus bonmak 08.10.2019 14177 ++	
	ТаблицаОпций = Новый ТаблицаЗначений;
	//ТаблицаОпций.Колонки.Добавить("Опция", Новый ОписаниеТипов("СправочникСсылка.Scan_ОпцииПродуктов"));

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Scan_ВерсииБазовыхСпецификаций.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.Scan_ВерсииБазовыхСпецификаций КАК Scan_ВерсииБазовыхСпецификаций
		|ГДЕ
		|	Scan_ВерсииБазовыхСпецификаций.Владелец.Изделие = &Изделие
		|	И НЕ Scan_ВерсииБазовыхСпецификаций.ПометкаУдаления
		|
		|УПОРЯДОЧИТЬ ПО
		|	Scan_ВерсииБазовыхСпецификаций.Код";
	
	Запрос.УстановитьПараметр("Изделие", ИзделиеСсылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	НужнаяСпецификация = Справочники.Scan_ВерсииБазовыхСпецификаций.ПустаяСсылка();
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		//список упорядочен по возрастанию кода
		Если ВыборкаДетальныеЗаписи.Ссылка.ВидСпецификации = Справочники.Scan_ВидыСпецификацийПродуктов.SPORTСпецификация Тогда
			НужнаяСпецификация = ВыборкаДетальныеЗаписи.Ссылка; 
		ИначеЕсли ВыборкаДетальныеЗаписи.Ссылка.ВидСпецификации = Справочники.Scan_ВидыСпецификацийПродуктов.COWСпецификацияЗаказанная Тогда
			НужнаяСпецификация = ВыборкаДетальныеЗаписи.Ссылка;
		ИначеЕсли ВыборкаДетальныеЗаписи.Ссылка.ВидСпецификации = Справочники.Scan_ВидыСпецификацийПродуктов.COWСпецификацияПодтвержденнаяЗаводом Тогда
			НужнаяСпецификация = ВыборкаДетальныеЗаписи.Ссылка;
		КонецЕсли;	
	КонецЦикла;
	Если НЕ НужнаяСпецификация.Пустая() Тогда
		ТаблицаОпций = НужнаяСпецификация.РасшифровкаОпций.Выгрузить();		
	КонецЕсли;
	
Возврат ТаблицаОпций;		
КонецФункции //rarus bonmak 08.10.2019 14177 --

