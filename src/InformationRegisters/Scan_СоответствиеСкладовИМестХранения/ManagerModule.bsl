
Функция ПолучитьСклад(МестоХраненияСсылка) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МестоХраненияСсылка", МестоХраненияСсылка);
	Запрос.Текст = "ВЫБРАТЬ
	|	Scan_СоответствиеСкладовИМестХранения.Склад КАК Склад
	|ИЗ
	|	РегистрСведений.Scan_СоответствиеСкладовИМестХранения КАК Scan_СоответствиеСкладовИМестХранения
	|ГДЕ
	|	Scan_СоответствиеСкладовИМестХранения.МестоХранения = &МестоХраненияСсылка";
	
	ВыборкаРезультат = Запрос.Выполнить().Выбрать();
	Если ВыборкаРезультат.Следующий() Тогда
		Возврат ВыборкаРезультат.Склад;
	Иначе
		Возврат Справочники.Scan_Склады.ПустаяСсылка();
	КонецЕсли;
	
КонецФункции

Функция ПолучитьМестоХранения(СкладСсылка) Экспорт
	
	МассивМестХранения = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СкладСсылка", СкладСсылка);
	Запрос.Текст = "ВЫБРАТЬ
	|	Scan_СоответствиеСкладовИМестХранения.МестоХранения КАК МестоХранения
	|ИЗ
	|	РегистрСведений.Scan_СоответствиеСкладовИМестХранения КАК Scan_СоответствиеСкладовИМестХранения
	|ГДЕ
	|	Scan_СоответствиеСкладовИМестХранения.Склад = &СкладСсылка";
	
	Результат = Запрос.Выполнить();
	
	ТабРезультат = Запрос.Выполнить().Выгрузить();
	Если ТабРезультат.Количество() <> 0 Тогда
		МассивМестХранения = ТабРезультат.ВыгрузитьКолонку("МестоХранения");
	КонецЕсли;
	
	Возврат МассивМестХранения;
	
КонецФункции

Функция ЗаписатьСоответствие(МестоХранения, Склад) Экспорт
	
	Отказ = Ложь;
	
	НаборЗаписей = РегистрыСведений.Scan_СоответствиеСкладовИМестХранения.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Склад.Установить(Склад); 
	
	Если ЗначениеЗаполнено(МестоХранения) Тогда
		НаборЗаписей.Отбор.МестоХранения.Установить(МестоХранения);
	Иначе
		Сообщить(НСтр("ru = 'Ошибка записи соответствия склада и места хранения в регистр сведений. Пустое значение места хранения'; en = 'Action failed'"));
		Возврат Истина;
	КонецЕсли;
	
	НаборЗаписей.Прочитать();
	Если НаборЗаписей.Количество() = 0 Тогда
		Если  ЗначениеЗаполнено(МестоХранения)
			И ЗначениеЗаполнено(Склад)
			Тогда
			//Если нет старого значения и оба параметра заполнены
			НоваяЗапись = НаборЗаписей.Добавить();
			НоваяЗапись.Склад         = Склад;
			НоваяЗапись.МестоХранения = МестоХранения;
			НоваяЗапись.Пользователь  = ПользователиКлиентСервер.ТекущийПользователь();
		КонецЕсли;
	Иначе
		//Если для этого кода адреса уже есть место хранения в регистре
		ТекЗапись = НаборЗаписей[0];
		Если Не ЗначениеЗаполнено(МестоХранения) Тогда
			//Нужно очистить запись
			НаборЗаписей.Удалить(ТекЗапись);
		Иначе
			ТекЗапись.Склад         = Склад;
			ТекЗапись.МестоХранения = МестоХранения;
			ТекЗапись.Пользователь  = ПользователиКлиентСервер.ТекущийПользователь();
		КонецЕсли;
	КонецЕсли;
	Попытка
		НаборЗаписей.Записать();
	Исключение
		Сообщить(НСтр("ru = 'Ошибка записи соответствия склада и места хранения в регистр сведений'; en = 'Action failed'"));
		Отказ = Истина;
	КонецПопытки;
	
	Возврат Отказ;
	
КонецФункции

Функция ПроверитьКорректностьСоответствияПоКоду(МестоХранения, Склад, СообщениеОбОшибке = "") Экспорт
	
	Если Не ЗначениеЗаполнено(Склад) Тогда
		СообщениеОбОшибке = "Не заполнен склад!";
		Возврат Ложь;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(МестоХранения) Тогда
		Возврат Истина;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МестоХранения", МестоХранения);
	Запрос.Текст = "ВЫБРАТЬ
	|	Scan_СоответствиеСкладовИМестХранения.Склад,
	|	Scan_СоответствиеСкладовИМестХранения.МестоХранения
	|ИЗ
	|	РегистрСведений.Scan_СоответствиеСкладовИМестХранения КАК Scan_СоответствиеСкладовИМестХранения
	|ГДЕ
	|	Scan_СоответствиеСкладовИМестХранения.МестоХранения = &МестоХранения";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		Если ВыборкаДетальныеЗаписи.Склад <> Склад Тогда
			СообщениеОбОшибке = СтрШаблон("Место хранения <%1> соответствует другому складу <%2>", МестоХранения, ВыборкаДетальныеЗаписи.Склад);
			Возврат Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Истина;

КонецФункции
