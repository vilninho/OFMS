//rarus tenkam 05.03.2017 mantis 6897 +++

// Функция - Получить местонахождение
//
// Параметры:
//  Изделие	 - СправочникСсылка.Scan_Изделия - 
//  НаДату	 - Дата	 - 
// 
// Возвращаемое значение:
// ТаблицаЗначений  - Изделие, МестоХранения, ДатаСобытия, Событие
//
Функция ПолучитьМестонахождение(Изделие, НаДату) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Scan_Обмен1БДИсторияМестонахожденияИзделийСрезПоследних.Изделие,
		|	Scan_Обмен1БДИсторияМестонахожденияИзделийСрезПоследних.МестоХранения,
		|	Scan_Обмен1БДИсторияМестонахожденияИзделийСрезПоследних.ДатаСобытия,
		|	Scan_Обмен1БДИсторияМестонахожденияИзделийСрезПоследних.Событие
		|ИЗ
		|	РегистрСведений.Scan_Обмен1БДИсторияМестонахожденияИзделий.СрезПоследних(&НаДату, Изделие = &Изделие) КАК Scan_Обмен1БДИсторияМестонахожденияИзделийСрезПоследних
		|
		|УПОРЯДОЧИТЬ ПО
		|	Scan_Обмен1БДИсторияМестонахожденияИзделийСрезПоследних.Период УБЫВ";
	
	Запрос.УстановитьПараметр("Изделие", Изделие);
	Запрос.УстановитьПараметр("НаДату", НаДату);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ТабРезультат = РезультатЗапроса.Выгрузить();
	Возврат ТабРезультат;
КонецФункции

// Функция - Запись истории
//
// Параметры:
//  Изделие			 - 	 - 
//  МестоХранения	 - 	 - 
//  ДатаСобытия		 - Дата	 - Дата прихода на место хранения / маршрут
//  Событие			 - ДокументСсылка.Scan_ДвижениеИзделий, ДокументСсылка.Scan_ПеремещениеИзделий, ДокументСсылка.Scan_ЗаявкаПеревозчику - 
//  GUIDСобытияВ1БД	 - Строка,36	 - 
//  Пользователь	 - 	 - 
//  НаДату			 - 	 - 
// 
// Возвращаемое значение:
//  Булево - Отказ
//
Функция ЗаписьИстории(Изделие, МестоХранения, ДатаСобытия, Событие, GUIDСобытияВ1БД, Пользователь = Неопределено , НаДату = Неопределено) Экспорт
	
	Отказ = Ложь;
	
	Если НаДату = Неопределено Тогда
		ДатаЗаписи = ТекущаяДата();
	Иначе
		ДатаЗаписи = НаДату;
	КонецЕсли; 
	
	Если Пользователь = Неопределено Тогда
		Пользователь = ПользователиКлиентСервер.ТекущийПользователь();
	КонецЕсли;
	
	ЗаписьРегСведений = РегистрыСведений.Scan_Обмен1БДИсторияМестонахожденияИзделий.СоздатьМенеджерЗаписи();
	ЗаписьРегСведений.Изделие = Изделие;
	ЗаписьРегСведений.МестоХранения = МестоХранения;
	
	ЗаписьРегСведений.ДатаСобытия = ДатаСобытия;
	
	ЗаписьРегСведений.Событие = Событие;
	
	ЗаписьРегСведений.GUIDСобытияВTLMS = Событие.УникальныйИдентификатор();
	ЗаписьРегСведений.GUIDСобытияВ1БД = GUIDСобытияВ1БД;
	ЗаписьРегСведений.ХозОперация = Событие.ХозОперация;
	ЗаписьРегСведений.Период = ДатаЗаписи;
	ЗаписьРегСведений.Пользователь = Пользователь;
	
	Попытка
		ЗаписьРегСведений.Записать();
	Исключение
		ЗаписьЖурналаРегистрации("Обмен с 1БД. Запись истории местонахождения изделия" , УровеньЖурналаРегистрации.Ошибка,, Событие, "Ошибка записи истории местонахождения изделия в регистр сведений по обмену с 1БД.");	
		//rarus FominskiyAS 24.04.2019  mantis 14191 +++
		//Сообщить(НСтр("ru = 'Ошибка записи истории местонахождения изделия в регистр сведений по обмену с 1БД'"));
		Сообщить(НСтр("ru = 'Ошибка записи истории местонахождения продукта в регистр сведений по обмену с 1БД'; en = 'Action failed'"));
		//rarus FominskiyAS 24.04.2019  mantis 14191 ---  
		Отказ = Истина;
	КонецПопытки; 

	Возврат Отказ;
КонецФункции 

// Функция - Такая запись уже есть
//
// Параметры:
//  Изделие			 - 	 - 
//  МестоХранения	 - 	 - 
//  ДатаСобытия		 - 	 - 
//  Событие			 - 	 - 
// 
// Возвращаемое значение:
//  Булево - Истина, если в истории уже есть запись с такими измерениями
//
Функция ТакаяЗаписьУжеЕсть(Изделие, МестоХранения, ДатаСобытия, Событие) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Scan_Обмен1БДИсторияМестонахожденияИзделий.Изделие,
		|	Scan_Обмен1БДИсторияМестонахожденияИзделий.Период
		|ИЗ
		|	РегистрСведений.Scan_Обмен1БДИсторияМестонахожденияИзделий КАК Scan_Обмен1БДИсторияМестонахожденияИзделий
		|ГДЕ
		|	Scan_Обмен1БДИсторияМестонахожденияИзделий.Изделие = &Изделие
		|	И Scan_Обмен1БДИсторияМестонахожденияИзделий.МестоХранения = &МестоХранения
		|	И Scan_Обмен1БДИсторияМестонахожденияИзделий.ДатаСобытия = &ДатаСобытия
		|	И Scan_Обмен1БДИсторияМестонахожденияИзделий.Событие = &Событие";
	
	Запрос.УстановитьПараметр("ДатаСобытия", ДатаСобытия);
	Запрос.УстановитьПараметр("Изделие", Изделие);
	Запрос.УстановитьПараметр("МестоХранения", МестоХранения);
	Запрос.УстановитьПараметр("Событие", Событие);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Ложь;
	Иначе
		Возврат Истина;
	КонецЕсли;
		
КонецФункции

//rarus tenkam 05.03.2017 mantis 6897 ---