
//rarus vikhle 22.04.2020 mt 15695 +++

// Функция возвращает компанию и контрагентов пользователя
//
// Параметры:
//  Пользователь - СправочникСсылка.Пользователи
//  ТолькоОсновной - Булево - при значении Истина функция вернет только основную связку компания-контрагент, при Ложь - все компании и контрагенты пользователя
//	ДилерАктивен - Булево - при переданном признаке накладывается соответствующее условие по реквизиту "ДилерАктивен" компании  
//
// Возвращаемое значение:
//	ТаблицаЗначений - таблица с колонками:
//		* Компания		- СправочникСсылка.Scan_Компании 
//		* Контрагент	- СправочникСсылка.Scan_Контрагенты
//		* Основной		- Булево
//
Функция ПолучитьКомпаниюДилераПользователя (Пользователь, ТолькоОсновной = Ложь) Экспорт
	
	// Для режима доступа Список компаний оставляем прежнюю логику
	Если Пользователь.РежимДоступаККомпаниям = Перечисления.Scan_РежимДоступаПользователяККомпаниям.СписокКомпаний Тогда //rarus kabany Дата: 08/07/2021 17897 ++ 
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	Scan_КомпанииПользователей.Компания КАК Компания,
		|	Scan_ВзаимосвязьКомпанийСКонтрагентамиСрезПоследних.Контрагент КАК Контрагент,
		|	Scan_КомпанииПользователей.Основной КАК Основной
		|ИЗ
		|	РегистрСведений.Scan_КомпанииПользователей КАК Scan_КомпанииПользователей
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Scan_ВзаимосвязьКомпанийСКонтрагентами.СрезПоследних(, ВидВзаимодействия = &ВидВзаимодействияДилер) КАК Scan_ВзаимосвязьКомпанийСКонтрагентамиСрезПоследних
		|		ПО Scan_КомпанииПользователей.Компания = Scan_ВзаимосвязьКомпанийСКонтрагентамиСрезПоследних.Компания
		|ГДЕ
		|	Scan_КомпанииПользователей.Пользователь = &Пользователь";
		Если ТолькоОсновной Тогда 
			Запрос.Текст = Запрос.Текст + " И Scan_КомпанииПользователей.Основной = Истина";
		КонецЕсли;
		
		Запрос.УстановитьПараметр("ВидВзаимодействияДилер",Scan_ПраваИНастройки.Scan_Право("ВидВзаимодействияДилер"));
		Запрос.УстановитьПараметр("Пользователь",Пользователь);
		РезультатЗапроса = Запрос.Выполнить().Выгрузить();
		Возврат РезультатЗапроса;
		//rarus kabany Дата: 08/07/2021 17897 ++	
	Иначе
		//rarus kabany Дата: 14/07/2021 17897 ++
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	Scan_КомпанииПользователей.Компания.ГруппыКомпаний КАК КомпанияГруппыКомпаний,
		|	Scan_КомпанииПользователей.Компания КАК Компания
		|ПОМЕСТИТЬ ГруппаКомпаний
		|ИЗ
		|	РегистрСведений.Scan_КомпанииПользователей КАК Scan_КомпанииПользователей
		|ГДЕ
		|	Scan_КомпанииПользователей.Пользователь = &Пользователь
		|	И Scan_КомпанииПользователей.Компания.ГруппыКомпаний <> ЗНАЧЕНИЕ(Справочник.Scan_ГруппыКомпаний.ПустаяСсылка)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Scan_Компании.Ссылка КАК Ссылка,
		|	ВЫБОР
		|		КОГДА ГруппаКомпаний.Компания = Scan_Компании.Ссылка
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК Основной
		|ПОМЕСТИТЬ Компании
		|ИЗ
		|	ГруппаКомпаний КАК ГруппаКомпаний
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Scan_Компании КАК Scan_Компании
		|		ПО (ГруппаКомпаний.КомпанияГруппыКомпаний = Scan_Компании.ГруппыКомпаний)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Компании.Ссылка КАК Компания,
		|	Scan_ВзаимосвязьКомпанийСКонтрагентамиСрезПоследних.Контрагент КАК Контрагент,
		|	Компании.Основной КАК Основной
		|ИЗ
		|	Компании КАК Компании
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Scan_ВзаимосвязьКомпанийСКонтрагентами.СрезПоследних(, ВидВзаимодействия = &ВидВзаимодействияДилер) КАК Scan_ВзаимосвязьКомпанийСКонтрагентамиСрезПоследних
		|		ПО (Компании.Ссылка = Scan_ВзаимосвязьКомпанийСКонтрагентамиСрезПоследних.Компания)
		|УПОРЯДОЧИТЬ ПО
		|	Основной УБЫВ"; // rarus kabany 15.07.2021 17897 ++ // добавлена сортировка 
		//rarus kabany Дата: 14/07/2021 17897 --
		Запрос.УстановитьПараметр("ВидВзаимодействияДилер",Scan_ПраваИНастройки.Scan_Право("ВидВзаимодействияДилер"));
		Запрос.УстановитьПараметр("Пользователь",Пользователь);
		РезультатЗапроса = Запрос.Выполнить().Выгрузить();
		Возврат РезультатЗапроса;
		//rarus kabany Дата: 08/07/2021 17897 --
	КонецЕсли;
КонецФункции

// Процедура записи в регистр сведений
// 
// Параметры:
//  Пользователь - СправочникСсылка.Пользователи
//  ТаблицаКомпаний - ТаблицаЗначений - с колонками:
//  	 * Компания - СправочникСсылка.Scan_Компании
//  	 * Основной - Булево 
Процедура ЗаписьЗначенияРегистраСведений (Пользователь, ТаблицаКомпаний) Экспорт
	НаборЗаписей = РегистрыСведений.Scan_КомпанииПользователей.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Пользователь.Установить(Пользователь);
	Для Каждого СтрокаТаблицы Из ТаблицаКомпаний Цикл
		НоваяЗапись = НаборЗаписей.Добавить();
		НоваяЗапись.Пользователь = Пользователь;
		НоваяЗапись.Компания =	СтрокаТаблицы.Компания;
		НоваяЗапись.Основной =  СтрокаТаблицы.Основной;
		НоваяЗапись.ПользовательАвтор = Пользователи.АвторизованныйПользователь();
	КонецЦикла;
	Попытка
		НаборЗаписей.Записать();
	Исключение
		Сообщить(ОписаниеОшибки());
	КонецПопытки;
КонецПроцедуры
//rarus vikhle 22.04.2020 mt 15695 ---