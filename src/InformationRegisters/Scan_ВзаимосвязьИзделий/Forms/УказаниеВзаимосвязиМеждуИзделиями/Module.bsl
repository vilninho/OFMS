// rarus tenkam 24.06.2019 mantis 14427 +++
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ЗаполнитьТаблицуЗначенией(Параметры.ИзделиеТС);  	
КонецПроцедуры

&НаКлиенте
Процедура СоставИзделияПриИзменении(Элемент)
	ТекущиеДанные = Элементы.СоставИзделия.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		ТекущиеДанные.ДатаИзмененияСвязи = Дата('00010101');
		ТекущиеДанные.Пользователь = ПользователиКлиентСервер.АвторизованныйПользователь();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуЗначенией(ИзделиеТС)
	// Получим данные из регистра 	
	СоставИзделия.Очистить();

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Scan_ВзаимосвязьИзделий.ИзделиеТС КАК ИзделиеТС,
		|	Scan_ВзаимосвязьИзделий.НадстройкиОборудованияПрицепы КАК НадстройкиОборудованияПрицепы,
		|	Scan_ВзаимосвязьИзделий.Пользователь КАК Пользователь,
		|	Scan_ВзаимосвязьИзделий.ДатаИзмененияСвязи КАК ДатаИзмененияСвязи
		|ИЗ
		|	РегистрСведений.Scan_ВзаимосвязьИзделий КАК Scan_ВзаимосвязьИзделий
		|ГДЕ
		|	(Scan_ВзаимосвязьИзделий.ИзделиеТС = &ИзделиеТС
		|			ИЛИ Scan_ВзаимосвязьИзделий.НадстройкиОборудованияПрицепы = &ИзделиеТС)";
	
	Запрос.УстановитьПараметр("ИзделиеТС", ИзделиеТС);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		НоваяСтрока = СоставИзделия.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаДетальныеЗаписи);
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Функция ПроверитьКорректностьТаблицы()
	
	Отказ = Ложь;         	
	// Нет пустых настроек
	Для Индекс = 0 по СоставИзделия.Количество()-1 Цикл
		ТекСтрока = СоставИзделия.Получить(Индекс);
		
		Если НЕ ЗначениеЗаполнено(ТекСтрока.НадстройкиОборудованияПрицепы) Тогда 
			Сообщение = Новый СообщениеПользователю();
			Сообщение.Текст = "В строке " + (Индекс+1) + " не заполнена настройка / оборудование / прицепная техника!";
			Сообщение.Поле = "СоставИзделия[" + Индекс + "].НадстройкиОборудованияПрицепы";
			Сообщение.УстановитьДанные(ЭтотОбъект);
			Сообщение.Сообщить();
			Отказ = Истина;	
		КонецЕсли;
		
		// Проверим, можно ли установить такую связь
		
	КонецЦикла;	 	
	
	// Нет дублей настроек
	Если ЕстьДубли() Тогда
		Отказ = Истина;	
		Сообщить(НСтр("ru = 'Ошибка! Есть дубли настроек / оборудования / прицепной техники.'; en = 'Check data on duplicates.'"));
	КонецЕсли;
	
	
	
	Возврат НЕ Отказ;
КонецФункции

&НаКлиенте
Процедура Сохранить(Команда)
	ВсеОк = Ложь;
	Если ПроверитьКорректностьТаблицы() Тогда
		ВсеОк = СохранитьНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция СохранитьНаСервере()
	ТекДата = ТекущаяДата();
	НаборЗаписей = РегистрыСведений.Scan_ВзаимосвязьИзделий.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ИзделиеТС.Установить(Параметры.ИзделиеТС);
	Для Каждого ТекСтрока Из СоставИзделия Цикл
		НоваяСтрока = НаборЗаписей.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекСтрока);
		Если НоваяСтрока.ДатаИзмененияСвязи = Дата('00010101') Тогда
			НоваяСтрока.ДатаИзмененияСвязи = ТекДата;
		КонецЕсли;
	КонецЦикла;
	Попытка
		НаборЗаписей.Записать();
		Сообщить(НСтр("ru = 'Данные успешно записаны.'; en = 'Data successfully written.'"));
		Возврат Истина;
	Исключение
		Сообщить(НСтр("ru = 'Не удалось записать данные.'; en = 'Error'"));	
		Возврат Ложь;
	КонецПопытки;
	
КонецФункции

Функция ЕстьДубли()
	//ТЗКопия = Новый ТаблицаЗначений;
	ТЗКопия = СоставИзделия.Выгрузить();
	ТЗКопия.Колонки.Добавить("Количество");
	Для Каждого ТекСтрока Из ТЗКопия Цикл
		ТекСтрока.Количество = 1;
	КонецЦикла;
	ТЗКопия.Свернуть("ИзделиеТС,НадстройкиОборудованияПрицепы", "Количество");
	Для Каждого ТекСтрока Из ТЗКопия Цикл
		Если ТекСтрока.Количество > 1 Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Ложь;
КонецФункции

&НаКлиенте
Процедура СоставИзделияИзделиеТСОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	Если ПолучитьГруппуИзделийOFMS(ВыбранноеЗначение) <> ПредопределенноеЗначение("Перечисление.Scan_ГруппыИзделийOFMS.ТранспортноеСредство") Тогда
		Сообщить(НСтр("ru = 'Необходимо указать продукт с группой продуктов OFMS - Транспортное средство!';en='Choose product with the correct OFMS group (Chassis)!'"));
		СтандартнаяОбработка = Ложь;
		Возврат;                                
	КонецЕсли; 
КонецПроцедуры

&НаКлиенте
Процедура СоставИзделияНадстройкиОборудованияПрицепыОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	ГруппаИзделийOFMS = ПолучитьГруппуИзделийOFMS(ВыбранноеЗначение);
	Если ГруппаИзделийOFMS = ПредопределенноеЗначение("Перечисление.Scan_ГруппыИзделийOFMS.ТранспортноеСредство") 
		ИЛИ ГруппаИзделийOFMS = Неопределено 
		ИЛИ ГруппаИзделийOFMS = ПредопределенноеЗначение("Перечисление.Scan_ГруппыИзделийOFMS.ПустаяСсылка") Тогда
		Сообщить(НСтр("ru = 'Необходимо указать продукт с группой продуктов OFMS отличной от Транспортное средство!';en='Choose product with the correct OFMS group (Superstructure/Equipment/Trailers)!'"));
		СтандартнаяОбработка = Ложь;
		Возврат;                                
	КонецЕсли;	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьГруппуИзделийOFMS(ИзделиеСсылка)
	Возврат ИзделиеСсылка.ГруппаИзделийOFMS;	
КонецФункции

// rarus tenkam 24.06.2019 mantis 14427 ---


