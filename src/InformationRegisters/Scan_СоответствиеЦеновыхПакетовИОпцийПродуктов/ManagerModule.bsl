
Функция ПолучитьЦеновойПакет(ОпцияПродуктаСсылка) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ОпцияПродуктаСсылка", ОпцияПродуктаСсылка);
	Запрос.Текст = "ВЫБРАТЬ
	|	Scan_СоответствиеЦеновыхПакетовИОпцийПродуктов.ЦеновойПакет КАК ЦеновойПакет
	|ИЗ
	|	РегистрСведений.Scan_СоответствиеЦеновыхПакетовИОпцийПродуктов КАК Scan_СоответствиеЦеновыхПакетовИОпцийПродуктов
	|ГДЕ
	|	Scan_СоответствиеЦеновыхПакетовИОпцийПродуктов.ОпцияПродукта = &ОпцияПродуктаСсылка";
	
	ВыборкаРезультат = Запрос.Выполнить().Выбрать();
	Если ВыборкаРезультат.Следующий() Тогда
		Возврат ВыборкаРезультат.ЦеновойПакет;
	Иначе
		Возврат Справочники.Scan_ЦеновыеПакеты.ПустаяСсылка();
	КонецЕсли;
	
КонецФункции

Функция ПолучитьОпцииПродуктов(ЦеновойПакетСсылка) Экспорт
	
	МассивОпцийПродуктов = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ЦеновойПакетСсылка", ЦеновойПакетСсылка);
	Запрос.Текст = "ВЫБРАТЬ
	|	Scan_СоответствиеЦеновыхПакетовИОпцийПродуктов.ОпцияПродукта КАК ОпцияПродукта
	|ИЗ
	|	РегистрСведений.Scan_СоответствиеЦеновыхПакетовИОпцийПродуктов КАК Scan_СоответствиеЦеновыхПакетовИОпцийПродуктов
	|ГДЕ
	|	Scan_СоответствиеЦеновыхПакетовИОпцийПродуктов.ЦеновойПакет = &ЦеновойПакетСсылка";
	
	Результат = Запрос.Выполнить();
	
	ТабРезультат = Запрос.Выполнить().Выгрузить();
	Если ТабРезультат.Количество() <> 0 Тогда
		МассивОпцийПродуктов = ТабРезультат.ВыгрузитьКолонку("ОпцияПродукта");
	КонецЕсли;
	
	Возврат МассивОпцийПродуктов;
	
КонецФункции

Функция ЗаписатьСоответствие(ОпцияПродукта, ЦеновойПакет) Экспорт
	
	Отказ = Ложь;
	
	НаборЗаписей = РегистрыСведений.Scan_СоответствиеЦеновыхПакетовИОпцийПродуктов.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ЦеновойПакет.Установить(ЦеновойПакет);
	
	Если ЗначениеЗаполнено(ОпцияПродукта) Тогда
		НаборЗаписей.Отбор.ОпцияПродукта.Установить(ОпцияПродукта);
	Иначе
		Сообщить(НСтр("ru = 'Ошибка записи соответствия ценового пакета и опции продукта в регистр сведений. Пустое значение опции продукта'; en = 'Action failed'"));
		Возврат Истина;
	КонецЕсли;
	
	НаборЗаписей.Прочитать();
	Если НаборЗаписей.Количество() = 0 Тогда
		Если  ЗначениеЗаполнено(ОпцияПродукта)
			И ЗначениеЗаполнено(ЦеновойПакет)
			Тогда
			//Если нет старого значения и оба параметра заполнены
			НоваяЗапись = НаборЗаписей.Добавить();
			НоваяЗапись.ЦеновойПакет  = ЦеновойПакет;
			НоваяЗапись.ОпцияПродукта = ОпцияПродукта;
			НоваяЗапись.Пользователь  = ПользователиКлиентСервер.ТекущийПользователь();
		КонецЕсли;
	Иначе
		ТекЗапись = НаборЗаписей[0];
		Если Не ЗначениеЗаполнено(ОпцияПродукта) Тогда
			//Нужно очистить запись
			НаборЗаписей.Удалить(ТекЗапись);
		Иначе
			ТекЗапись.ЦеновойПакет  = ЦеновойПакет;
			ТекЗапись.ОпцияПродукта = ОпцияПродукта;
			ТекЗапись.Пользователь  = ПользователиКлиентСервер.ТекущийПользователь();
		КонецЕсли;
	КонецЕсли;
	Попытка
		НаборЗаписей.Записать();
	Исключение
		Сообщить(НСтр("ru = 'Ошибка записи соответствия ценового пакета и опции продукта в регистр сведений'; en = 'Action failed'"));
		Отказ = Истина;
	КонецПопытки;
	
	Возврат Отказ;
	
КонецФункции

Функция ЗаписатьНаборСоответствий(ОпцииПродуктов, ЦеновойПакет) Экспорт
	
	Отказ = Ложь;
	
	НаборЗаписей = РегистрыСведений.Scan_СоответствиеЦеновыхПакетовИОпцийПродуктов.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ЦеновойПакет.Установить(ЦеновойПакет);
	НаборЗаписей.Прочитать();
	НаборЗаписей.Очистить();
	
	ТекущийПользователь = ПользователиКлиентСервер.ТекущийПользователь();
	
	Для Каждого ОпцияПродукта Из ОпцииПродуктов Цикл
		НоваяЗапись = НаборЗаписей.Добавить();
		НоваяЗапись.ЦеновойПакет  = ЦеновойПакет;
		НоваяЗапись.ОпцияПродукта = ОпцияПродукта;
		НоваяЗапись.Пользователь  = ТекущийПользователь;
	КонецЦикла;
	
	Попытка
		НаборЗаписей.Записать();
	Исключение
		Сообщить(НСтр("ru = 'Ошибка записи соответствия ценового пакета и опции продукта в регистр сведений'; en = 'Action failed'"));
		Отказ = Истина;
	КонецПопытки;
	
	Возврат Отказ;
	
КонецФункции

Функция ПроверитьКорректностьСоответствияПоКоду(ОпцияПродукта, ЦеновойПакет, СообщениеОбОшибке = "") Экспорт
	
	Если Не ЗначениеЗаполнено(ЦеновойПакет) Тогда
		СообщениеОбОшибке = "Не заполнен ценовой пакет!";
		Возврат Ложь;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ОпцияПродукта) Тогда
		Возврат Истина;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ОпцияПродукта", ОпцияПродукта);
	Запрос.Текст = "ВЫБРАТЬ
	|	Scan_СоответствиеЦеновыхПакетовИОпцийПродуктов.ЦеновойПакет,
	|	Scan_СоответствиеЦеновыхПакетовИОпцийПродуктов.ОпцияПродукта
	|ИЗ
	|	РегистрСведений.Scan_СоответствиеЦеновыхПакетовИОпцийПродуктов КАК Scan_СоответствиеЦеновыхПакетовИОпцийПродуктов
	|ГДЕ
	|	Scan_СоответствиеЦеновыхПакетовИОпцийПродуктов.ОпцияПродукта = &ОпцияПродукта";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		Если ВыборкаДетальныеЗаписи.ЦеновойПакет <> ЦеновойПакет Тогда
			СообщениеОбОшибке = СтрШаблон("Опция продукта <%1> соответствует другому ценовому пакету <%2>", ОпцияПродукта, ВыборкаДетальныеЗаписи.ЦеновойПакет);
			Возврат Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Истина;

КонецФункции
