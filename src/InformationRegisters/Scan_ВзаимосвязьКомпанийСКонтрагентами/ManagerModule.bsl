Функция ПолучитьКонтрагентовПоКомпании(КомпанияСсылка) Экспорт //rarus bonmak 05.12.2019 14456 ++
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	Scan_ВзаимосвязьКомпанийСКонтрагентами.Период КАК Период,
	               |	Scan_ВзаимосвязьКомпанийСКонтрагентами.Компания КАК Компания,
	               |	Scan_ВзаимосвязьКомпанийСКонтрагентами.Контрагент КАК Контрагент,
	               |	Scan_ВзаимосвязьКомпанийСКонтрагентами.ВидВзаимодействия КАК ВидВзаимодействия,
	               |	Scan_ВзаимосвязьКомпанийСКонтрагентами.Пользователь КАК Пользователь,
	               |	Scan_ВзаимосвязьКомпанийСКонтрагентами.ДокументИзмененияВ1БД КАК ДокументИзмененияВ1БД,
	               |	Scan_ВзаимосвязьКомпанийСКонтрагентами.РежимУстановкиВидаВзаимодействия КАК РежимУстановкиВидаВзаимодействия
	               |ИЗ
	               |	РегистрСведений.Scan_ВзаимосвязьКомпанийСКонтрагентами КАК Scan_ВзаимосвязьКомпанийСКонтрагентами
	               |ГДЕ
	               |	Scan_ВзаимосвязьКомпанийСКонтрагентами.Компания = &Компания";
	Запрос.УстановитьПараметр("Компания", КомпанияСсылка);
	Выгрузка = Запрос.Выполнить().Выгрузить();
	Возврат Выгрузка;    
КонецФункции //rarus bonmak 05.12.2019 14456 --

// Функция записи параметров в регистр сведений
Функция ДобавитьЗаписьВзаимосвязьКомпанийСКонтрагентами(ПериодЗаписи, КомпанияСсылка, КонтрагентСсылка, ВидВзаимодействияСсылка, РежимУстановкиВидаВзаимодействияСсылка) Экспорт //rarus bonmak 05.12.2019 14456 ++	
	Отказ = Ложь;
	НаборЗаписей = РегистрыСведений.Scan_ВзаимосвязьКомпанийСКонтрагентами.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Компания.Установить(КомпанияСсылка);
	НаборЗаписей.Отбор.ВидВзаимодействия.Установить(ВидВзаимодействияСсылка);
	Если ВидВзаимодействияСсылка <> Scan_ПраваИНастройки.Scan_Право("ВидВзаимодействияДилер") Тогда
		НаборЗаписей.Отбор.Контрагент.Установить(КонтрагентСсылка);
	КонецЕсли;
	НаборЗаписей.Прочитать();
	Если НаборЗаписей.Количество() = 0 Тогда
		СтрокаЗаписи = НаборЗаписей.Добавить();
		СтрокаЗаписи.Компания = КомпанияСсылка;
		СтрокаЗаписи.ВидВзаимодействия = ВидВзаимодействияСсылка;
	Иначе
		СтрокаЗаписи = НаборЗаписей[0];
	КонецЕсли;
	
	СтрокаЗаписи.Период = ПериодЗаписи;
	СтрокаЗаписи.Контрагент = КонтрагентСсылка;
	СтрокаЗаписи.РежимУстановкиВидаВзаимодействия = РежимУстановкиВидаВзаимодействияСсылка;
	СтрокаЗаписи.Пользователь = ПользователиКлиентСервер.ТекущийПользователь();
	Попытка
		НаборЗаписей.Записать(); 
	Исключение
		Сообщить(НСтр("ru = 'Ошибка записи взаимосвязи компаний с контрагентами в регистр сведений'; en = 'Error writing related companies with counterparties in the data register'"));
		Отказ = Истина;	
	КонецПопытки;	
	Возврат Отказ;		
КонецФункции //rarus bonmak 05.12.2019 14456 --

//rarus vikhle 21.04.2020 mt 15695 +++

//Функция возвращает дилера-контрагента компании
Функция ПолучитьДилераПоКомпании (КомпанияСсылка) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
	               |	Scan_ВзаимосвязьКомпанийСКонтрагентамиСрезПоследних.Контрагент КАК Контрагент
	               |ИЗ
	               |	РегистрСведений.Scan_ВзаимосвязьКомпанийСКонтрагентами.СрезПоследних(
	               |			,
	               |			ВидВзаимодействия = &ВидВзаимодействияДилер
	               |				И Компания = &Компания) КАК Scan_ВзаимосвязьКомпанийСКонтрагентамиСрезПоследних
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Scan_ВзаимосвязьКомпанийСКонтрагентамиСрезПоследних.Период УБЫВ";
	Запрос.УстановитьПараметр("Компания", КомпанияСсылка);
	ВидВзаимодействияДилер = Scan_ПраваИНастройки.Scan_Право("ВидВзаимодействияДилер");
	Запрос.УстановитьПараметр("ВидВзаимодействияДилер",ВидВзаимодействияДилер);
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	Возврат Выборка.Контрагент;  	
КонецФункции
//rarus vikhle 21.04.2020 mt 15695 ---


Функция ЕстьСвязьКомпанияКонтрагент(КомпанияСсылка, КонтрагентСсылка) Экспорт //rarus bonmak 15.04.2020 14456 ++
	СвязьЕсть = Ложь;
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Scan_ВзаимосвязьКомпанийСКонтрагентамиСрезПоследних.Компания КАК Компания,
	|	Scan_ВзаимосвязьКомпанийСКонтрагентамиСрезПоследних.Контрагент КАК Контрагент
	|ИЗ
	|	РегистрСведений.Scan_ВзаимосвязьКомпанийСКонтрагентами.СрезПоследних(
	|			,
	|			Контрагент = &Контрагент
	|				И Компания = &Компания) КАК Scan_ВзаимосвязьКомпанийСКонтрагентамиСрезПоследних";
	
	Запрос.УстановитьПараметр("Компания", КомпанияСсылка);
	Запрос.УстановитьПараметр("Контрагент", КонтрагентСсылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		СвязьЕсть = Истина;	
	КонецЕсли;
	
	Возврат СвязьЕсть;
КонецФункции //rarus bonmak 15.04.2020 14456 --

Функция УдалитьЗаписьВзаимосвязьКомпанийСКонтрагентами(ЗаписьУдаления) Экспорт //rarus bonmak 15.04.2020 14456 ++
	МенеждерЗаписей = РегистрыСведений.Scan_ВзаимосвязьКомпанийСКонтрагентами.СоздатьМенеджерЗаписи();
	ЗаполнитьЗначенияСвойств(МенеждерЗаписей, ЗаписьУдаления);
	МенеждерЗаписей.Прочитать();
	Попытка
		МенеждерЗаписей.Удалить(); 
	Исключение	
	КонецПопытки;	
КонецФункции //rarus bonmak 15.04.2020 14456 --