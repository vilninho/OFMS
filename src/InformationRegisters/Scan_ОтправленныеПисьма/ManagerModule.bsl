//rarus tenkam 12.12.2017 mantis 11858 +++

Функция ЕстьОтправленноеПисьмо(ДокументОтправки) Экспорт
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ
	|	Scan_ОтправленныеПисьма.ДатаОтправки КАК ДатаОтправки,
	|	Scan_ОтправленныеПисьма.Кому КАК Кому,
	|	Scan_ОтправленныеПисьма.ТемаПисьма КАК ТемаПисьма
	|ИЗ
	|	РегистрСведений.Scan_ОтправленныеПисьма КАК Scan_ОтправленныеПисьма
	|ГДЕ
	|	Scan_ОтправленныеПисьма.ДокументОтправки = &ДокументОтправки";
	Запрос.УстановитьПараметр("ДокументОтправки", ДокументОтправки);
	РезультатВыборка = Запрос.Выполнить().Выбрать();
	
	Если РезультатВыборка.Следующий() Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции
//rarus tenkam 12.12.2017 mantis 11858 ---

// rarus agar 03.06.2021 16927 ++
Процедура ЗаписатьОтправкуПисьма(ПараметрыПисьма) Экспорт
	
	// rarus agar 08.06.2021 16927 ++
	ТемаПисьма = "";
	Если ПараметрыПисьма.Свойство("Тема") Тогда
		ТемаПисьма = ПараметрыПисьма.Тема;
	КонецЕсли;
	// rarus agar 08.06.2021 16927 --
	
	ОтветныйАдрес="";
	Если  ПараметрыПисьма.Свойство("АдресОтвета")
		И ПараметрыПисьма.АдресОтвета <> Неопределено 
		Тогда
		Для Каждого Адрес Из ПараметрыПисьма.АдресОтвета Цикл
			Если ОтветныйАдрес = "" Тогда
				ОтветныйАдрес = Строка(Адрес.Адрес);
			Иначе
				ОтветныйАдрес = ОтветныйАдрес+ ", "+ Строка(Адрес.Адрес);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	ТелоПисьма = "";
	Если  ПараметрыПисьма.Свойство("Тело") 
		И ТипЗнч(ПараметрыПисьма.Тело) = Тип("ФорматированныйДокумент") 
		Тогда
		ТелоПисьма = ПараметрыПисьма.Тело.ПолучитьТекст();
	Иначе
		ТелоПисьма = ПараметрыПисьма.Тело;
	КонецЕсли;
	
	ПолучателиПисьма = "";
	Если ПараметрыПисьма.Свойство("Кому") Тогда
		Для Каждого ЭлементКому Из ПараметрыПисьма.Кому Цикл
			Если ПолучателиПисьма = "" Тогда
				ПолучателиПисьма = Строка(ЭлементКому.Адрес);
			Иначе
				ПолучателиПисьма = ПолучателиПисьма+ ", "+ Строка(ЭлементКому.Адрес);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	ПолучателиКопии = "";
	Если ПараметрыПисьма.Свойство("Копии") Тогда
		Для Каждого ЭлементКопии Из ПараметрыПисьма.Копии Цикл
			Если ПолучателиКопии = "" Тогда
				ПолучателиКопии = Строка(ЭлементКопии.Адрес);
			Иначе	
				ПолучателиКопии = ПолучателиКопии+ ", "+ Строка(ЭлементКопии.Адрес);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	НоваяЗапись = РегистрыСведений.Scan_ОтправленныеПисьма.СоздатьМенеджерЗаписи();
	НоваяЗапись.ТемаПисьма       = ТемаПисьма; // rarus agar 08.06.2021 16927 +-
	// rarus agar 28.06.2021 АПК ++
	//НоваяЗапись.ДатаОтправки     = ТекущаяДата();
	НоваяЗапись.ДатаОтправки     = ТекущаяДатаСеанса();
	// rarus agar 28.06.2021 АПК --
	// rarus agar 15.02.2022 17929 ++
	//НоваяЗапись.Владелец         = ПараметрыПисьма.Владелец;
	//НоваяЗапись.Пользователь     = ПараметрыПисьма.Пользователь;
	//НоваяЗапись.Автоматически    = ПараметрыПисьма.Автоматически;
	//НоваяЗапись.ДокументОтправки = ПараметрыПисьма.ДокументОтправки;
	//НоваяЗапись.Отправитель      = ПараметрыПисьма.РеквизитыОтправителя.АдресЭлектроннойПочты;
	Если ПараметрыПисьма.Свойство("Владелец") Тогда
		НоваяЗапись.Владелец         = ПараметрыПисьма.Владелец;
	КонецЕсли;
	Если ПараметрыПисьма.Свойство("Пользователь") Тогда
		НоваяЗапись.Пользователь     = ПараметрыПисьма.Пользователь;
	КонецЕсли;
	Если ПараметрыПисьма.Свойство("Автоматически") Тогда
		НоваяЗапись.Автоматически    = ПараметрыПисьма.Автоматически;
	КонецЕсли;
	Если ПараметрыПисьма.Свойство("ДокументОтправки") Тогда
		НоваяЗапись.ДокументОтправки = ПараметрыПисьма.ДокументОтправки;
	КонецЕсли;
	Если  ПараметрыПисьма.Свойство("РеквизитыОтправителя") 
		И ПараметрыПисьма.РеквизитыОтправителя.Свойство("АдресЭлектроннойПочты")
		Тогда
		НоваяЗапись.Отправитель      = ПараметрыПисьма.РеквизитыОтправителя.АдресЭлектроннойПочты;
	КонецЕсли;
	// rarus agar 15.02.2022 17929 --
	НоваяЗапись.АдресОтвета      = ОтветныйАдрес;
	НоваяЗапись.ТелоПисьма       = ТелоПисьма;
	НоваяЗапись.Кому             = ПолучателиПисьма;
	НоваяЗапись.Копии            = ПолучателиКопии;
	
	// Rarus tenkam 06.07.2022 mantis 18726 АПК +++
	//ТекстВЖР = "Кому: " + НоваяЗапись.Кому + Символы.ПС +
	//		   "Дата отправки: " + НоваяЗапись.ДатаОтправки + Символы.ПС + 
	//		   "Документ отправки: " + НоваяЗапись.ДокументОтправки + Символы.ПС +
	//		   "Тема: " + НоваяЗапись.ТемаПисьма; 	
	
	ТекстВЖР = СтрШаблон(НСтр("ru = 'Кому:  %1'"), НоваяЗапись.Кому) + Символы.ПС +
		СтрШаблон(НСтр("ru = 'Копии:  %1'"), НоваяЗапись.Копии) + Символы.ПС +
		СтрШаблон(НСтр("ru = 'Дата отправки:  %1'"), НоваяЗапись.ДатаОтправки) + Символы.ПС +
		СтрШаблон(НСтр("ru = 'Документ отправки:  %1'"), НоваяЗапись.ДокументОтправки) + Символы.ПС +
		СтрШаблон(НСтр("ru = 'Тема:  %1'"), НоваяЗапись.ТемаПисьма);
	// Rarus tenkam 06.07.2022 mantis 18726 АПК --- 	
  
	Попытка
		НоваяЗапись.Записать();
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Запись отправленного сообщения в регистр'",ОбщегоНазначения.КодОсновногоЯзыка()), УровеньЖурналаРегистрации.Примечание, Метаданные.РегистрыСведений.Scan_ОтправленныеПисьма , ,ТекстВЖР,); // Rarus tenkam 06.07.2022 mantis 18726 АПК + (НСтр)
	Исключение
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Запись отправленного сообщения в регистр'",ОбщегоНазначения.КодОсновногоЯзыка()), УровеньЖурналаРегистрации.Ошибка, Метаданные.РегистрыСведений.Scan_ОтправленныеПисьма, ,ТекстВЖР,); // Rarus tenkam 06.07.2022 mantis 18726 АПК + (НСтр)
	КонецПопытки;
	
КонецПроцедуры
// rarus agar 03.06.2021 16927 --