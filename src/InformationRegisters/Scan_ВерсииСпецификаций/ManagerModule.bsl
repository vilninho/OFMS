// Функция записи параметров в регистр сведений
Функция ЗаписьЗначенияРегистраСведения(СтруктураРеквизитов) Экспорт //rarus bonmak 20.03.2020 14177 ++
	Отказ = Ложь;
	Записывать = Ложь;	
	СтруктураОтбора = ПреобразоватьСтруктуруРеквизитов(СтруктураРеквизитов);
	
	Если СтруктураОтбора = Неопределено Тогда
		Отказ = Истина;
		Возврат Отказ;
	КонецЕсли;
	ТекДата = ТекущаяДата();
	НаборЗаписей = РегистрыСведений.Scan_ВерсииСпецификаций.СоздатьНаборЗаписей();	
	НаборЗаписей.Отбор.Спецификация.Установить(СтруктураОтбора.Спецификация);
	НаборЗаписей.Отбор.ВидСпецификации.Установить(СтруктураОтбора.ВидСпецификации);
	НаборЗаписей.Отбор.Версия.Установить(СтруктураОтбора.Версия);
	НаборЗаписей.Прочитать();
	
	Если НаборЗаписей.Количество() = 1 Тогда
		//проверим отличия в опциях
		СтрокаНабора = НаборЗаписей[0]; 
		Если СтруктураОтбора.Опции <> СтрокаНабора.Опции Тогда  
			СтрокаНабора.Опции = СтруктураОтбора.Опции;
			СтрокаНабора.ДатаВерсииСпецификации = СтруктураОтбора.ДатаВерсииСпецификации;
			Записывать = Истина;			
		КонецЕсли;		
	ИначеЕсли НаборЗаписей.Количество() = 0 Тогда
		СтрокаНабора = НаборЗаписей.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаНабора, СтруктураОтбора);
		СтрокаНабора.Период = ТекДата;
		Записывать = Истина;		
	КонецЕсли;		
	Если Записывать Тогда
		Попытка
			НаборЗаписей.Записать();
		Исключение	
			Отказ = Истина;	
		КонецПопытки;
	КонецЕсли;	
	Возврат Отказ;
КонецФункции //rarus bonmak 20.03.2020 14177 --

Функция ПреобразоватьСтруктуруРеквизитов(СтруктураРеквизитов) //rarus bonmak 20.03.2020 14177 ++
	
	//rarus ozhnik 15888 14.07.2020 + 
	Если СтруктураРеквизитов.Свойство("Спецификация") Тогда
		НайденныйЭлемент = СтруктураРеквизитов.Спецификация;	
	Иначе //rarus ozhnik 15888 14.07.2020 -
		НайденныйЭлемент = Справочники.Scan_ВерсииБазовыхСпецификаций.НайтиПоРеквизиту("IDExternalSystem", СтруктураРеквизитов.IDExternalSystem);
		Если НайденныйЭлемент.Пустая() Тогда
			Возврат Неопределено;
		КонецЕсли;
		
	КонецЕсли; //rarus ozhnik 15888 14.07.2020 +-
	
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("Спецификация", НайденныйЭлемент);
	СтруктураОтбора.Вставить("ВидСпецификации",СтруктураРеквизитов.ВидСпецификации);
	СтруктураОтбора.Вставить("Версия",СтруктураРеквизитов.ВерсияСпецификации);
	СтруктураОтбора.Вставить("Опции",СтруктураРеквизитов.Опции);
	СтруктураОтбора.Вставить("ДатаВерсииСпецификации",СтруктураРеквизитов.ДатаВерсииСпецификации);
	СтруктураОтбора.Вставить("ПродуктКП", НайденныйЭлемент.Владелец.ПродуктКП);
	СтруктураОтбора.Вставить("НомерКПSPORT",НайденныйЭлемент.Владелец.НомерКПSPORT);
	
	Возврат СтруктураОтбора;
КонецФункции //rarus bonmak 20.03.2020 14177 --

