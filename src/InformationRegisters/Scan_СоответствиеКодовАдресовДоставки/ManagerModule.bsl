//rarus tenkam 27.07.2017 mantis 10271 +++

Функция ПолучитьКодАдресаДоставки(МестоХраненияСсылка) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	Scan_СоответствиеКодовАдресовДоставки.КодАдресаДоставки
	               |ИЗ
	               |	РегистрСведений.Scan_СоответствиеКодовАдресовДоставки КАК Scan_СоответствиеКодовАдресовДоставки
	               |ГДЕ
	               |	Scan_СоответствиеКодовАдресовДоставки.МестоХранения = &МестоХраненияСсылка";
	Запрос.УстановитьПараметр("МестоХраненияСсылка", МестоХраненияСсылка);
	ВыборкаРезультат = Запрос.Выполнить().Выбрать();
	Если ВыборкаРезультат.Следующий() Тогда
		Возврат ВыборкаРезультат.КодАдресаДоставки;
	Иначе
		Возврат Справочники.Scan_КодыАдресовДоставки.ПустаяСсылка();
	КонецЕсли;    
КонецФункции

Функция ПолучитьМестоХранения(КодАдресаДоставкиСсылка) Экспорт 	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	Scan_СоответствиеКодовАдресовДоставки.МестоХранения
	               |ИЗ
	               |	РегистрСведений.Scan_СоответствиеКодовАдресовДоставки КАК Scan_СоответствиеКодовАдресовДоставки
	               |ГДЕ
	               |	Scan_СоответствиеКодовАдресовДоставки.КодАдресаДоставки = &КодАдресаДоставкиСсылка";
	Запрос.УстановитьПараметр("КодАдресаДоставкиСсылка", КодАдресаДоставкиСсылка);
	Результат = Запрос.Выполнить();
	Если КодАдресаДоставкиСсылка = Справочники.Scan_КодыАдресовДоставки.PORTSTP Тогда
		// получим массив мест хранения
		МассивМестХранения = Новый Массив;
		ТабРезультат = Запрос.Выполнить().Выгрузить();
		Если ТабРезультат.Количество() <> 0 Тогда
			МассивМестХранения = ТабРезультат.ВыгрузитьКолонку("МестоХранения");
		КонецЕсли;
		Возврат МассивМестХранения;
	Иначе
		ВыборкаРезультат = Запрос.Выполнить().Выбрать();
		Если ВыборкаРезультат.Следующий() Тогда
			Возврат ВыборкаРезультат.МестоХранения;
		Иначе
			Возврат Справочники.Scan_МестаХранения.ПустаяСсылка();
		КонецЕсли; 
	КонецЕсли; 
КонецФункции

Функция ЗаписатьСоответствие(МестоХранения, КодАдресаДоставки) Экспорт
	
	Отказ = Ложь;
	
	НаборЗаписей = РегистрыСведений.Scan_СоответствиеКодовАдресовДоставки.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.КодАдресаДоставки.Установить(КодАдресаДоставки); 
	Если КодАдресаДоставки = Справочники.Scan_КодыАдресовДоставки.PORTSTP Тогда
		Если ЗначениеЗаполнено(МестоХранения) Тогда
			НаборЗаписей.Отбор.МестоХранения.Установить(МестоХранения);
		Иначе
			//rarus FominskiyAS 24.04.2019  mantis 14191 +++
			//Сообщить(НСтр("ru = 'Ошибка записи соответствия кода адреса доставки и места хранения в регистр сведений. Пустое значение места хранения'"));
			Сообщить(НСтр("ru = 'Ошибка записи соответствия кода адреса доставки и места хранения в регистр сведений. Пустое значение места хранения'; en = 'Action failed'"));
			//rarus FominskiyAS 24.04.2019  mantis 14191 ---  
			Возврат Истина;
		КонецЕсли;
	КонецЕсли;
	НаборЗаписей.Прочитать();
	Если НаборЗаписей.Количество() = 0 Тогда
		Если ЗначениеЗаполнено(МестоХранения) И ЗначениеЗаполнено(КодАдресаДоставки) Тогда
			//Если нет старого значения и оба параметра заполнены
			НоваяЗапись = НаборЗаписей.Добавить(); 
			НоваяЗапись.МестоХранения = МестоХранения; 
			НоваяЗапись.КодАдресаДоставки = КодАдресаДоставки; 
		    НоваяЗапись.Пользователь = ПользователиКлиентСервер.ТекущийПользователь();
		КонецЕсли;
	Иначе
		//Если для этого кода адреса уже есть место хранения в регистре
		ТекЗапись = НаборЗаписей[0];
		Если НЕ ЗначениеЗаполнено(МестоХранения) Тогда
			//Нужно очистить запись
			НаборЗаписей.Удалить(ТекЗапись);
		Иначе 
			ТекЗапись.МестоХранения = МестоХранения; 
			ТекЗапись.КодАдресаДоставки = КодАдресаДоставки; 
		    ТекЗапись.Пользователь = ПользователиКлиентСервер.ТекущийПользователь();	
		КонецЕсли;
	КонецЕсли;
	Попытка
		НаборЗаписей.Записать(); 
	Исключение
		//rarus FominskiyAS 24.04.2019  mantis 14191 +++
		//Сообщить(НСтр("ru = 'Ошибка записи соответствия кода адреса доставки и места хранения в регистр сведений'"));
		Сообщить(НСтр("ru = 'Ошибка записи соответствия кода адреса доставки и места хранения в регистр сведений'; en = 'Action failed'"));
		//rarus FominskiyAS 24.04.2019  mantis 14191 ---  
		Отказ = Истина;	
	КонецПопытки;
	
	Возврат Отказ;
		
КонецФункции 

Функция ПроверитьКорректностьСоответствияПоМестуХранения(МестоХранения, КодАдресаДоставки, СообщениеОбОшибке = "") Экспорт
	Если НЕ ЗначениеЗаполнено(МестоХранения) Тогда
		СообщениеОбОшибке = "Не заполнено место хранения!";
		Возврат Ложь;
	ИначеЕсли КодАдресаДоставки = Справочники.Scan_КодыАдресовДоставки.PORTSTP Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(КодАдресаДоставки) Тогда
		Возврат Истина;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Scan_СоответствиеКодовАдресовДоставки.КодАдресаДоставки,
	|	Scan_СоответствиеКодовАдресовДоставки.МестоХранения
	|ИЗ
	|	РегистрСведений.Scan_СоответствиеКодовАдресовДоставки КАК Scan_СоответствиеКодовАдресовДоставки
	|ГДЕ
	|	Scan_СоответствиеКодовАдресовДоставки.КодАдресаДоставки = &КодАдресаДоставки";
	
	Запрос.УстановитьПараметр("КодАдресаДоставки", КодАдресаДоставки);
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		Если ВыборкаДетальныеЗаписи.МестоХранения <> МестоХранения Тогда
			СообщениеОбОшибке = "Выбранные код адреса доставки соответствует другому месту хранения " + ВыборкаДетальныеЗаписи.МестоХранения;
			Возврат Ложь;
		КонецЕсли;
	КонецЕсли;
	Возврат Истина;

КонецФункции

Функция ПроверитьКорректностьСоответствияПоКоду(МестоХранения, КодАдресаДоставки, СообщениеОбОшибке = "") Экспорт
	Если НЕ ЗначениеЗаполнено(КодАдресаДоставки) Тогда
		СообщениеОбОшибке = "Не заполнен код адреса доставки!";
		Возврат Ложь;
	//ИначеЕсли КодАдресаДоставки = Справочники.Scan_КодыАдресовДоставки.PORTSTP Тогда
	//	Возврат Истина;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(МестоХранения) Тогда
		Возврат Истина;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Scan_СоответствиеКодовАдресовДоставки.КодАдресаДоставки,
	|	Scan_СоответствиеКодовАдресовДоставки.МестоХранения
	|ИЗ
	|	РегистрСведений.Scan_СоответствиеКодовАдресовДоставки КАК Scan_СоответствиеКодовАдресовДоставки
	|ГДЕ
	|	Scan_СоответствиеКодовАдресовДоставки.МестоХранения = &МестоХранения";
	
	Запрос.УстановитьПараметр("МестоХранения", МестоХранения);
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		Если ВыборкаДетальныеЗаписи.КодАдресаДоставки <> КодАдресаДоставки Тогда
			СообщениеОбОшибке = "Выбранное место хранения соответствует другому коду адреса доставки " + ВыборкаДетальныеЗаписи.КодАдресаДоставки;
			Возврат Ложь;
		КонецЕсли;
	КонецЕсли;
	Возврат Истина;

КонецФункции


//rarus tenkam 27.07.2017 mantis 10271 ---
