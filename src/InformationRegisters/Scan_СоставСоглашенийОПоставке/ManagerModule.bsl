&НаСервере
Функция ПолучитьСписокИзделий(СоглашениеОПоставке) экспорт
	
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Scan_СоставСоглашенийОПоставкеСрезПоследних.Изделие КАК Продукт,
		|	Scan_СоставСоглашенийОПоставкеСрезПоследних.СоглашениеОПоставке КАК СоглашениеОПоставке
		|ИЗ
		|	РегистрСведений.Scan_СоставСоглашенийОПоставке.СрезПоследних КАК Scan_СоставСоглашенийОПоставкеСрезПоследних
		|ГДЕ
		|	Scan_СоставСоглашенийОПоставкеСрезПоследних.СоглашениеОПоставке = &СоглашениеОПоставке";
	
	Запрос.УстановитьПараметр("СоглашениеОПоставке", СоглашениеОПоставке);
	
	СоставСоглашения = Запрос.Выполнить().Выгрузить(); 	
	
	возврат СоставСоглашения;
	
КонецФункции

&НаСервере
Функция ПолучитьСоглашениеОПоставке(Изделие) экспорт
	
	СоглашениеДляВозврата = неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Scan_СоставСоглашенийОПоставкеСрезПоследних.Изделие КАК Продукт,
		|	Scan_СоставСоглашенийОПоставкеСрезПоследних.СоглашениеОПоставке КАК СоглашениеОПоставке
		|ИЗ
		|	РегистрСведений.Scan_СоставСоглашенийОПоставке.СрезПоследних КАК Scan_СоставСоглашенийОПоставкеСрезПоследних
		|ГДЕ
		|	Scan_СоставСоглашенийОПоставкеСрезПоследних.Изделие = &Изделие";
	
	Запрос.УстановитьПараметр("Изделие", Изделие);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		СоглашениеДляВозврата = Выборка.СоглашениеОПоставке;	
	КонецЕсли; 
	
	возврат СоглашениеДляВозврата;
	
КонецФункции

&НаСервере
Процедура ЗаписатьСоставСоглашения(ТекущийСоставСоглашения, СоглашениеОПоставке, Отказ, ПривязанныеЗаказы = Неопределено) Экспорт //rarus vikhle 14.12.2020 mt 16977  добавил параметр отказ //rarus vikhle 03.09.2021 mt 17933 + ПривязанныеЗаказы
	//rarus vikhle 30.06.2020 mt 15888 +++
	НачатьТранзакцию();
	БлокировкаДанных = Новый БлокировкаДанных;
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.Scan_СоставСоглашенийОПоставке");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = ТекущийСоставСоглашения;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Изделие","Продукт");
	БлокировкаДанных.Заблокировать();
	//rarus vikhle 30.06.2020 mt 15888 ---
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТЗ.Продукт КАК ТекущийПродукт
		|ПОМЕСТИТЬ ВТ_ТЗ
		|ИЗ
		|	&ТаблицаЗаписей КАК ТЗ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Scan_СоставСоглашенийОПоставкеСрезПоследних.Изделие КАК Изделие
		|ПОМЕСТИТЬ ВТ_ПрошлыеИзделия
		|ИЗ
		|	РегистрСведений.Scan_СоставСоглашенийОПоставке.СрезПоследних КАК Scan_СоставСоглашенийОПоставкеСрезПоследних
		|ГДЕ
		|	Scan_СоставСоглашенийОПоставкеСрезПоследних.СоглашениеОПоставке = &СоглашениеОПоставке
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Scan_СоставСоглашенийОПоставкеСрезПоследних.Изделие КАК Изделие,
		//rarus vikhle 17.12.2020 mt 16181 +++
		|	Scan_СоставСоглашенийОПоставкеСрезПоследних.СоглашениеОПоставке.НомерСоглашенияОПоставке КАК НомерЗаявки
		//rarus vikhle 17.12.2020 mt 16181 ---
		|ПОМЕСТИТЬ ЗанятыеИзделия
		|ИЗ
		|	РегистрСведений.Scan_СоставСоглашенийОПоставке.СрезПоследних(
		|			,
		|			Изделие В
		|				(ВЫБРАТЬ
		|					ВТ_ТЗ.ТекущийПродукт
		|				ИЗ
		|					ВТ_ТЗ КАК ВТ_ТЗ)) КАК Scan_СоставСоглашенийОПоставкеСрезПоследних
		|ГДЕ
		|	НЕ Scan_СоставСоглашенийОПоставкеСрезПоследних.СоглашениеОПоставке В (&СоглашениеОПоставке)
		|	И Scan_СоставСоглашенийОПоставкеСрезПоследних.СоглашениеОПоставке <> ЗНАЧЕНИЕ(Справочник.Scan_СоглашенияОПоставке.ПустаяСсылка)
		|	И НЕ Scan_СоставСоглашенийОПоставкеСрезПоследних.СоглашениеОПоставке.Статус В (&Статусы)
		//rarus vikhle 13.08.2021 mt 17834 +++
		|	И ВЫБОР
		|			КОГДА НЕ &НаОснованииЗаявкиSRU
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ НЕ Scan_СоставСоглашенийОПоставкеСрезПоследних.СоглашениеОПоставке.ТипСоглашенияОПоставке = ЗНАЧЕНИЕ(Перечисление.Scan_ТипыСоглашенийОПоставке.ЗаявкаНаСОПSRU)
		|		КОНЕЦ
		//rarus vikhle 13.08.2021 mt 17834 ---
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗанятыеИзделия.Изделие КАК Изделие,
		//rarus vikhle 17.12.2020 mt 16181 +++
		|	ЗанятыеИзделия.НомерЗаявки КАК НомерЗаявки
		//rarus vikhle 17.12.2020 mt 16181 +++
		|ИЗ
		|	ЗанятыеИзделия КАК ЗанятыеИзделия
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ПрошлыеИзделия.Изделие КАК ПрошлыйПродукт,
		|	ВТ_ТЗ.ТекущийПродукт КАК ТекущийПродукт,
		//rarus vikhle 03.09.2021 mt 17933 +++
		|	ВТ_ТЗ.ТекущийПродукт.ЗаказНаЗавод КАК ЗаказНаЗавод
		//rarus vikhle 03.09.2021 mt 17933 ---
		|ИЗ
		|	ВТ_ТЗ КАК ВТ_ТЗ
		|		ПОЛНОЕ СОЕДИНЕНИЕ ВТ_ПрошлыеИзделия КАК ВТ_ПрошлыеИзделия
		|		ПО ВТ_ТЗ.ТекущийПродукт = ВТ_ПрошлыеИзделия.Изделие
		|ГДЕ
		|	НЕ ВТ_ПрошлыеИзделия.Изделие В
		|				(ВЫБРАТЬ
		|					ЗанятыеИзделия.Изделие
		|				ИЗ
		|					ЗанятыеИзделия КАК ЗанятыеИзделия)";
	//rarus vikhle 18.08.2020 mt 16181 +-
	
	//rarus vikhle 10.11.2020 mt 16181 +++
	МассивСтатусов = Новый Массив;
	МассивСтатусов.Добавить(Справочники.Scan_СтатусыСоглашенийОПоставкеИСпециальныхУсловий.Отказ);
	МассивСтатусов.Добавить(Справочники.Scan_СтатусыСоглашенийОПоставкеИСпециальныхУсловий.СОП_Отменен);
	МассивСтатусов.Добавить(Справочники.Scan_СтатусыСоглашенийОПоставкеИСпециальныхУсловий.СОП_Расторгнут);
	МассивСтатусов.Добавить(Справочники.Scan_СтатусыСоглашенийОПоставкеИСпециальныхУсловий.НеактуальноЕстьДС);
	МассивСтатусов.Добавить(Справочники.Scan_СтатусыСоглашенийОПоставкеИСпециальныхУсловий.ПроектЗаявкиНаСОП);
	//rarus vikhle 10.11.2020 mt 16181 ---
	
	Запрос.УстановитьПараметр("ТаблицаЗаписей", ТекущийСоставСоглашения);
	Запрос.УстановитьПараметр("СоглашениеОпоставке", СоглашениеОпоставке);
	//Запрос.УстановитьПараметр("ПрошлаяВерсияСоглашения", СоглашениеОпоставке.Основание); //rarus vikhle 10.11.2020 mt 16181	
	Запрос.УстановитьПараметр("Статусы", МассивСтатусов); //rarus vikhle 10.11.2020 mt 16181
	
	//rarus vikhle 13.08.2021 mt 17834 +++
	// Для заявок на СОП на основании заявки на СОП SRU не проверяем вхождение продуктов в заявки на СОП SRU
	Если ТипЗнч(СоглашениеОПоставке.Основание) = Тип("СправочникСсылка.Scan_СоглашенияОПоставке") 
		И Не СоглашениеОПоставке.Основание.Пустая() Тогда
		ЗаявкаНаОснованииЗаявкиSRU = СоглашениеОПоставке.Основание.ТипСоглашенияОПоставке = Перечисления.Scan_ТипыСоглашенийОПоставке.ЗаявкаНаСОПSRU;
	Иначе	
		ЗаявкаНаОснованииЗаявкиSRU = Ложь;	
	КонецЕсли;	
	Запрос.УстановитьПараметр("НаОснованииЗаявкиSRU", ЗаявкаНаОснованииЗаявкиSRU);
	//rarus vikhle 13.08.2021 mt 17834 ---
	
	МассивРезультатовЗапроса = Запрос.ВыполнитьПакет();
	ЗанятыеИзделия	=	МассивРезультатовЗапроса[3].Выгрузить();
	
	Для Каждого СтрокаИзделия Из ЗанятыеИзделия Цикл
		//ВывестиСообщениеПол(НСтр("ru = 'Продукт %1 уже указан в другой заявке!'"),,,,,СтрокаИзделия.Изделие);
		ВывестиСообщениеПол(НСтр("ru = 'Продукт ""%1"" уже указан в заявке № %2'"),,,,,СтрокаИзделия.Изделие,СтрокаИзделия.НомерЗаявки);//rarus vikhle 17.12.2020 mt 16181
		Отказ = Истина;//rarus vikhle 14.12.2020 mt 16977
	КонецЦикла;
		
	ТЧ 	=	МассивРезультатовЗапроса[4].Выгрузить(); 
	//rarus vikhle 30.06.2020 mt 15888 ---
		
	// Необходимо обработать 2 варианта
	// 1. Добавлено новое изделие в соглашение о поставке.
	// 2. Удалено изделие из соглашения о поставке.
	// 3. Изделие было и осталось.
	
	Для каждого СтрокаПродукта Из ТЧ Цикл
		// Вариант 1
		Если СтрокаПродукта.ПрошлыйПродукт = Null Тогда
			// Изделие добавили
			НоваяЗапись = РегистрыСведений.Scan_СоставСоглашенийОПоставке.СоздатьМенеджерЗаписи();
			НоваяЗапись.Изделие = СтрокаПродукта.ТекущийПродукт;
			НоваяЗапись.Период = ТекущаяДатаСеанса();
			НоваяЗапись.СоглашениеОПоставке = СоглашениеОПоставке;
			НоваяЗапись.Записать();
			
			// Добавим в массив заказов для дальнейшей проверки модуле объекта на необходимость добавления записи по заказам в регистр "Scan_ОчередьDD2DDSИнформера"
			Если ТипЗнч(ПривязанныеЗаказы) = Тип("Массив") Тогда //rarus vikhle 08.09.2021 mt 17933
				ПривязанныеЗаказы.Добавить(СтрокаПродукта.ЗаказНаЗавод); //rarus vikhle 01.09.2021 mt 17933 +++
			КонецЕсли;	
			
		КонецЕсли;
		
		// Вариант 2
		Если СтрокаПродукта.ТекущийПродукт = Null Тогда
			// Изделие удалили
			НоваяЗапись = РегистрыСведений.Scan_СоставСоглашенийОПоставке.СоздатьМенеджерЗаписи();
			НоваяЗапись.Изделие = СтрокаПродукта.ПрошлыйПродукт;
			НоваяЗапись.Период = ТекущаяДатаСеанса();
			НоваяЗапись.СоглашениеОПоставке = Справочники.Scan_СоглашенияОПоставке.ПустаяСсылка();
			НоваяЗапись.Записать();
		КонецЕсли;
	КонецЦикла; 
	ЗафиксироватьТранзакцию();//rarus vikhle 30.06.2020 mt 15888	
КонецПроцедуры