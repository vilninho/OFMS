#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

// Добавляет запись в регистр сведений
//
// Параметры:
//  Продукт				 - СправочникСсылка.Scan_Изделия	- значение измерения "Продукт"
//  СебестоимостьСНДС	 - Число	 - значение ресурса "СебестоимостьСНДС"
//  Период				 - Дата	 - Период записи
//  Пользователь		 - СправочникСсылка.Пользователи	 - значение реквизита "Пользователь"
//
Процедура ДобавитьЗаписьПоПродукту(Продукт, СебестоимостьСНДС, Период = Неопределено, Пользователь = Неопределено) Экспорт //rarus vikhle 19.07.2021 m 17933 +++
	
	Если НЕ ЗначениеЗаполнено(Продукт) Тогда
		Возврат;
	КонецЕсли;
	
	Если Период = Неопределено Тогда
		Период = ТекущаяДатаСеанса();
	КонецЕсли;	
	
	Если Пользователь = Неопределено Тогда
		Пользователь = Пользователи.ТекущийПользователь();
	КонецЕсли;	
	
	// rarus vikhle 17.01.2022 m 18269 +++
	
	//Чтение старого значения регистра
	СтруктураОтбора   = Новый Структура("Продукт", Продукт);
	СтруктураСведений = РегистрыСведений.Scan_ПлановаяСебестоимостьПродуктов.ПолучитьПоследнее(Период, СтруктураОтбора);
	ЗначениеСтарое    = СтруктураСведений.СебестоимостьСНДС;
	Записывать        = Ложь;
	
	//Введено значение, а старое отсутствует
	Если ЗначениеЗаполнено(СебестоимостьСНДС) И (ЗначениеСтарое = 0) Тогда 
		Записывать = Истина; 
	КонецЕсли; 
	
	//Значение стерто, а старое значение было введено
	Если НЕ ЗначениеЗаполнено(СебестоимостьСНДС) И (ЗначениеСтарое <> 0) Тогда 
		Записывать = Истина; 
	КонецЕсли; 
	
	//Введено значение и было введено старое
	Если ЗначениеЗаполнено(СебестоимостьСНДС) И (ЗначениеСтарое <> 0) Тогда
		//Значение изменилось
		Если СебестоимостьСНДС <> ЗначениеСтарое Тогда 
			Записывать = Истина; 
		КонецЕсли; 
	КонецЕсли;
	// rarus vikhle 17.02.2021 m 18269 ---
	
	Если Записывать Тогда
		МенеджерЗаписи = РегистрыСведений.Scan_ПлановаяСебестоимостьПродуктов.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Период				= Период;
		МенеджерЗаписи.Продукт				= Продукт;
		МенеджерЗаписи.СебестоимостьСНДС	= СебестоимостьСНДС;
		МенеджерЗаписи.Пользователь			= Пользователь;
		МенеджерЗаписи.Записать();
	КонецЕсли;	
		
КонецПроцедуры

// Удалить все записи по продукту
//
// Параметры:
//  Продукт	 - СправочникСсылка.Scan_Изделия	 - 
//
Процедура УдалитьЗаписьПоПродукту(Продукт) Экспорт // rarus vikhle 21.12.2021 m 18269 +++
	
	Если НЕ ЗначениеЗаполнено(Продукт) Тогда
		Возврат;
	КонецЕсли;
	
	НаборЗаписей = РегистрыСведений.Scan_ПлановаяСебестоимостьПродуктов.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Продукт.Установить(Продукт);
	
	НаборЗаписей.Прочитать();
	Если НаборЗаписей.Выбран() Тогда
		НаборЗаписей.Очистить();
		НаборЗаписей.Записать();
	КонецЕсли;
	
КонецПроцедуры // rarus vikhle 21.12.2021 m 18269 ---

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли	