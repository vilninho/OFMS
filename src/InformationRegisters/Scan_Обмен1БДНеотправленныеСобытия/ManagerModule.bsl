//rarus tenkam 17.08.2017 mantis 10824 +++
Функция ЗаписатьСобытиеВРегистр(СтруктураПараметров, ТекстЗапроса, ТекстОтвета = "") Экспорт
	Если НЕ СтруктураПараметров.Свойство("ДокументСсылка") Тогда
		Возврат Ложь;
	КонецЕсли;

	Если НЕ (СтруктураПараметров.Свойство("ВидСобытия") И СтруктураПараметров.Свойство("ДатаСобытия") И СтруктураПараметров.Свойство("GuidСобытияВВнешнейСистеме") И СтруктураПараметров.Свойство("ТСGUID")) Тогда
		ЗаписьЖурналаРегистрации("Обмен с 1БД. Запись неотправленного события" , УровеньЖурналаРегистрации.Ошибка,, СтруктураПараметров.ДокументСсылка, "Ошибка записи события в регистр неотправленных событий.");	
		Возврат Ложь;
	КонецЕсли;
	
	ВсеОк = Истина;
	Если СтруктураПараметров.Свойство("ПользовательСсылка") И ЗначениеЗаполнено(СтруктураПараметров.ПользовательСсылка) Тогда
		Пользователь = СтруктураПараметров.ПользовательСсылка; 	
	ИначеЕсли СтруктураПараметров.Свойство("Пользователь") И ЗначениеЗаполнено(СтруктураПараметров.Пользователь) Тогда
		Пользователь = Справочники.Пользователи.НайтиПоНаименованию(СтруктураПараметров.Пользователь,Истина);
		Если НЕ ЗначениеЗаполнено(Пользователь) Тогда
			Пользователь = ПользователиКлиентСервер.ТекущийПользователь();
		КонецЕсли;
	Иначе
		Пользователь = ПользователиКлиентСервер.ТекущийПользователь();
	КонецЕсли;
	
	ЗаписьРегСведений = РегистрыСведений.Scan_Обмен1БДНеотправленныеСобытия.СоздатьМенеджерЗаписи();
	Если СтруктураПараметров.Свойство("Изделие") Тогда
		Изделие = СтруктураПараметров.Изделие;
	Иначе
		Изделие = Справочники.Scan_Изделия.НайтиПоРеквизиту("IDExternalSystem", СтруктураПараметров.ТСGUID); 
		Если НЕ ЗначениеЗаполнено(Изделие) Тогда
			ЗаписьЖурналаРегистрации("Обмен с 1БД. Запись неотправленного события" , УровеньЖурналаРегистрации.Ошибка,, СтруктураПараметров.ДокументСсылка, "Ошибка записи события в регистр неотправленных событий.");	
			Возврат Ложь;
		КонецЕсли;
	КонецЕсли;
	ЗаписьРегСведений.Изделие = Изделие;
	ЗаписьРегСведений.Пользователь = Пользователь;
	ЗаписьРегСведений.ВидСобытия = СтруктураПараметров.ВидСобытия;
	ЗаписьРегСведений.ДатаСобытия = СтруктураПараметров.ДатаСобытия;
	ЗаписьРегСведений.GuidСобытияВВнешнейСистеме = СтруктураПараметров.GuidСобытияВВнешнейСистеме;
	// rarus tenkam 18.03.2019 mantis 13629 +++
	ЗаписьРегСведений.ДатаИзменения = ТекущаяДата();
	// rarus tenkam 18.03.2019 mantis 13629 ---
	
	ЗаписьРегСведений.ТекстЗапроса = ТекстЗапроса;
	ЗаписьРегСведений.ТекстОтвета = ТекстОтвета;
		
	Если СтруктураПараметров.Свойство("ТекстСобытия") Тогда
		ЗаписьРегСведений.ТекстСобытия = СтруктураПараметров.ТекстСобытия;
	КонецЕсли;
	Если СтруктураПараметров.Свойство("ПричинаСобытия") Тогда
		ЗаписьРегСведений.ПричинаСобытия = СтруктураПараметров.ПричинаСобытия;
	КонецЕсли;
	
	//rarus tenkam 27.09.2017 mantis 10831 +++
	Если СтруктураПараметров.Свойство("GuidСобытия") Тогда
		ЗаписьРегСведений.GuidСобытия1БД = СтруктураПараметров.GuidСобытия;
	КонецЕсли;		
	//rarus tenkam 27.09.2017 mantis 10831 ---   	
	
	Попытка
		ЗаписьРегСведений.Записать();
	Исключение
		ЗаписьЖурналаРегистрации("Обмен с 1БД. Запись неотправленного события" , УровеньЖурналаРегистрации.Ошибка,, СтруктураПараметров.ДокументСсылка, "Ошибка записи события в регистр неотправленных событий.");	

		//rarus FominskiyAS 24.04.2019  mantis 14191 +++
		//Сообщить(НСтр("ru = 'Ошибка записи события в регистр неотправленных событий.'"));
		Сообщить(НСтр("ru = 'Ошибка записи события в регистр неотправленных событий.'; en = 'Action failed'"));
		//rarus FominskiyAS 24.04.2019  mantis 14191 ---  
		ВсеОк = Ложь;
	КонецПопытки; 

	Возврат ВсеОк;

КонецФункции

Функция УдалитьСобытие(СтруктураПараметров) Экспорт
	НаборЗаписей = РегистрыСведений.Scan_Обмен1БДНеотправленныеСобытия.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ВидСобытия.Установить(СтруктураПараметров.ВидСобытия);
	НаборЗаписей.Отбор.ДатаСобытия.Установить(СтруктураПараметров.ДатаСобытия);
	НаборЗаписей.Отбор.GuidСобытияВВнешнейСистеме.Установить(СтруктураПараметров.GuidСобытияВВнешнейСистеме);
	
	Если СтруктураПараметров.Свойство("Изделие") Тогда
		Изделие = СтруктураПараметров.Изделие;
	Иначе
		Изделие = Справочники.Scan_Изделия.НайтиПоРеквизиту("IDExternalSystem", СтруктураПараметров.ТСGUID); 
		Если НЕ ЗначениеЗаполнено(Изделие) Тогда
			ЗаписьЖурналаРегистрации("Регистр сведений. Неотправленные события в 1БД (scania)", УровеньЖурналаРегистрации.Ошибка,
			,, "Не удалось удалить событие по причине некорректности данных");
			Возврат Ложь;
		КонецЕсли;
	КонецЕсли;
	
	НаборЗаписей.Отбор.Изделие.Установить(Изделие);
	
	Попытка
		НаборЗаписей.Записать();
	Исключение
		ЗаписьЖурналаРегистрации("Регистр сведений. Неотправленные события в 1БД (scania)", УровеньЖурналаРегистрации.Ошибка,
		,, СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Не удалось удалить событие по причине: %1'"), ПодробноеПредставлениеОшибки(ИнформацияОбОшибке())));
	КонецПопытки;
	Возврат Истина;
КонецФункции
//rarus tenkam 17.08.2017 mantis 10824 ---