// rarus tenkam 07.08.2020 mantis 16181 +++
Функция ПолучитьЦенуПродукта(Продукт, СоставляющаяЦены, СНДС = Истина) Экспорт
	СтруктураОтбора = Новый Структура("Продукт", Продукт);
	СтруктураОтбора.Вставить("СоставляющаяЦены", СоставляющаяЦены);
	
	Полученныеданные = РегистрыСведений.Scan_ПлановыеЗначенияСоставляющихЦеныПродукта.Получить(ТекущаяДата(), СтруктураОтбора);
	
	Если НЕ ЗначениеЗаполнено(Полученныеданные) ИЛИ Полученныеданные.ЦенаСНДС = Неопределено Тогда
		Возврат 0;
	КонецЕсли;
	
	Если СНДС Тогда
		Возврат Полученныеданные.ЦенаСНДС;
	Иначе
		Возврат Полученныеданные.ЦенаБезНДС;
	КонецЕсли;
КонецФункции

Функция ПолучитьЦеныПродуктов(МассивПродуктов, СоставляющаяЦены) Экспорт
	СоответствиеПродуктЦена = Новый Соответствие;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Scan_ПлановыеЗначенияСоставляющихЦеныПродуктаСрезПоследних.Продукт КАК Продукт,
		|	Scan_ПлановыеЗначенияСоставляющихЦеныПродуктаСрезПоследних.ЦенаСНДС КАК ЦенаСНДС
		|ИЗ
		|	РегистрСведений.Scan_ПлановыеЗначенияСоставляющихЦеныПродукта.СрезПоследних(
		|			,
		|			Продукт В (&МассивПродуктов)
		|				И СоставляющаяЦены = &СоставляющаяЦены) КАК Scan_ПлановыеЗначенияСоставляющихЦеныПродуктаСрезПоследних";
	
	Запрос.УстановитьПараметр("МассивПродуктов", МассивПродуктов);
	Запрос.УстановитьПараметр("СоставляющаяЦены", СоставляющаяЦены);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		СоответствиеПродуктЦена.Вставить(ВыборкаДетальныеЗаписи.Продукт, ВыборкаДетальныеЗаписи.ЦенаСНДС); 
	КонецЦикла;
	
	Возврат СоответствиеПродуктЦена;	
	
КонецФункции

Функция ПолучитьЗначенияВсехСоставляющихПродукта(Продукт, СНДС = Истина) Экспорт
	СоответствиеСоставляющих = Новый Соответствие;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Scan_СоставляющиеРасчетаЦеныПродуктов.Ссылка КАК СоставляющаяЦены
		|ПОМЕСТИТЬ СоставляющиеЦеныПродуктаПлан
		|ИЗ
		|	Справочник.Scan_СоставляющиеРасчетаЦеныПродуктов КАК Scan_СоставляющиеРасчетаЦеныПродуктов
		|ГДЕ
		|	(Scan_СоставляющиеРасчетаЦеныПродуктов.Родитель = ЗНАЧЕНИЕ(Справочник.Scan_СоставляющиеРасчетаЦеныПродуктов.СкидкиНаценки)
		|			ИЛИ Scan_СоставляющиеРасчетаЦеныПродуктов.Ссылка = ЗНАЧЕНИЕ(Справочник.Scan_СоставляющиеРасчетаЦеныПродуктов.ЦенаДилера)
		|			ИЛИ Scan_СоставляющиеРасчетаЦеныПродуктов.Ссылка = ЗНАЧЕНИЕ(Справочник.Scan_СоставляющиеРасчетаЦеныПродуктов.ЦенаКлиента))
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	СоставляющаяЦены
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СоставляющиеЦеныПродуктаПлан.СоставляющаяЦены КАК СоставляющаяЦены,
		|	ЕСТЬNULL(Scan_ПлановыеЗначенияСоставляющихЦеныПродуктаСрезПоследних.ЦенаСНДС, 0) КАК ЦенаСНДС,
		|	ЕСТЬNULL(Scan_ПлановыеЗначенияСоставляющихЦеныПродуктаСрезПоследних.ЦенаБезНДС, 0) КАК ЦенаБезНДС,
		|	СоставляющиеЦеныПродуктаПлан.СоставляющаяЦены.ИмяПредопределенныхДанных КАК СоставляющаяЦеныИмяПредопределенныхДанных
		|ИЗ
		|	СоставляющиеЦеныПродуктаПлан КАК СоставляющиеЦеныПродуктаПлан
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Scan_ПлановыеЗначенияСоставляющихЦеныПродукта.СрезПоследних(, Продукт = &Продукт) КАК Scan_ПлановыеЗначенияСоставляющихЦеныПродуктаСрезПоследних
		|		ПО СоставляющиеЦеныПродуктаПлан.СоставляющаяЦены = Scan_ПлановыеЗначенияСоставляющихЦеныПродуктаСрезПоследних.СоставляющаяЦены";
	
	Запрос.УстановитьПараметр("Продукт", Продукт);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Если СНДС Тогда
			СоответствиеСоставляющих.Вставить(ВыборкаДетальныеЗаписи.СоставляющаяЦеныИмяПредопределенныхДанных, ВыборкаДетальныеЗаписи.ЦенаСНДС); 
		Иначе
			СоответствиеСоставляющих.Вставить(ВыборкаДетальныеЗаписи.СоставляющаяЦеныИмяПредопределенныхДанных, ВыборкаДетальныеЗаписи.ЦенаБезНДС); 
		КонецЕсли;

	КонецЦикла;
	
	Возврат СоответствиеСоставляющих;	
	
КонецФункции
// rarus tenkam 07.08.2020 manti 16181 ---