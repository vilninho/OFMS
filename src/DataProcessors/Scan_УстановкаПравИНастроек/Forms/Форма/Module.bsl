////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ (ПЕРЕНЕСЕНЫ ИЗ МОДУЛЯ ОБРАБОТКИ)
//rarus sergei 17.08.2016 mantis 7110 ++
&НаКлиенте
Перем МассивИзделий;
//rarus sergei 17.08.2016 mantis 7110 --

// Эта функция осуществляет поиск строки
// Если строка найдена, возвращает найденною строку дерева
// Если строка не найдена, возвращает неопределено
&НаСервере
Функция НайтиОтмеченноеЗначениеВДеревеЗначений(ПравоНастройка)
	ТекДеревоПрав = РеквизитФормыВЗначение("ДеревоПрав");
	
	СтрокаПоиска = ТекДеревоПрав.Строки.Найти(ПравоНастройка, "ПравоНастройка", Истина);
	Если СтрокаПоиска = Неопределено Тогда
		Рез = Неопределено;
	Иначе
		Рез = СтрокаПоиска.Значение;
	КонецЕсли;
	
	Возврат Рез;
КонецФункции
	
//выгружает текущие настройки во внешний файл
&НаКлиенте
Процедура СохранитьНастройкиВФайл() Экспорт
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	Диалог.МножественныйВыбор = Ложь;
	Диалог.ПолноеИмяФайла = "ПраваИНастройки" + ТипОбъектаНастройки;
	Диалог.Расширение = "xml";
	Диалог.Фильтр = "Файлы XML (*.xml)|*.xml";
	Диалог.Показать(Новый ОписаниеОповещения("СохранитьНастройкиВФайлЗавершение", ЭтотОбъект, Новый Структура("Диалог", Диалог))); 	                                       
КонецПроцедуры

&НаКлиенте
Процедура СохранитьНастройкиВФайлЗавершение(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
    
    Диалог = ДополнительныеПараметры.Диалог;
    
    
    Если (ВыбранныеФайлы <> Неопределено) Тогда
        СохранитьНастройкиВФайлСервер(Диалог.ПолноеИмяФайла);
    Иначе
        Возврат; 					
    КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура СохранитьНастройкиВФайлСервер(ПолноеИмяФайла) Экспорт
	ФайлВыгрузки = Новый ЗаписьXML();
	ФайлВыгрузки.ОткрытьФайл(ПолноеИмяФайла);
	ФайлВыгрузки.ЗаписатьОбъявлениеXML();
	ФайлВыгрузки.ЗаписатьНачалоЭлемента("ОбъектНастройки");
	ФайлВыгрузки.ЗаписатьАтрибут("ТипОбъектаНастройки", ЗначениеВСтрокуВнутр(ТипОбъектаНастройки));
	
	//выборка и выгрузка прав и настроек пользователя
	ФайлВыгрузки.ЗаписатьНачалоЭлемента("ПраваИНастройки");
	Запрос = Новый Запрос();
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ПраваИНастройки.Объект КАК Объект,
	|	ПраваИНастройки.ПравоНастройка КАК ПравоНастройка,
	|	ПраваИНастройки.Значение КАК Значение
	|ИЗ
	|	РегистрСведений.Scan_ПраваИНастройки КАК ПраваИНастройки
	|ГДЕ
	|	ПраваИНастройки.Объект = &Объект
	|";
	Запрос.УстановитьПараметр("Объект", ОбъектНастройкиПрав);
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	Для каждого СтрокаТЗ Из РезультатЗапроса Цикл
		ФайлВыгрузки.ЗаписатьНачалоЭлемента("ЗаписьРегистра"); 
		ФайлВыгрузки.ЗаписатьАтрибут("ПравоНастройка", ЗначениеВСтрокуВнутр(СтрокаТЗ.ПравоНастройка));
		ФайлВыгрузки.ЗаписатьАтрибут("Значение", ЗначениеВСтрокуВнутр(СтрокаТЗ.Значение));
		ФайлВыгрузки.ЗаписатьКонецЭлемента();
	КонецЦикла; 
	ФайлВыгрузки.ЗаписатьКонецЭлемента();
	
	//если объект настройки пользователь, то так же выгрузим права доступа к документам и справочникам
	Если ТипОбъектаНастройки = Перечисления.Scan_НазначениеПравИНастроек.Пользователь Тогда
		
		//выборка и выгрузка прав доступа к документам
		ФайлВыгрузки.ЗаписатьНачалоЭлемента("ДоступКДокументам");
		Запрос = Новый Запрос();
		Запрос.Текст = "
		|ВЫБРАТЬ
		|	ДоступКДокументам.Пользователь КАК Пользователь,
		|	ДоступКДокументам.Право КАК Право,
		|	ДоступКДокументам.Объект КАК Объект,
		|	ДоступКДокументам.ДоступЕсть КАК ДоступЕсть
		|ИЗ
		|	РегистрСведений.Scan_ДоступКДокументам КАК ДоступКДокументам
		|ГДЕ
		|	ДоступКДокументам.Пользователь = &Пользователь
		|";
		Запрос.УстановитьПараметр("Пользователь", ОбъектНастройкиПрав);
		РезультатЗапроса = Запрос.Выполнить().Выгрузить();
		Для каждого СтрокаТЗ Из РезультатЗапроса Цикл
			ФайлВыгрузки.ЗаписатьНачалоЭлемента("ЗаписьРегистра"); 
			ФайлВыгрузки.ЗаписатьАтрибут("Право", ЗначениеВСтрокуВнутр(СтрокаТЗ.Право));
			ФайлВыгрузки.ЗаписатьАтрибут("Объект", ЗначениеВСтрокуВнутр(СтрокаТЗ.Объект));				
			ФайлВыгрузки.ЗаписатьАтрибут("ДоступЕсть", ЗначениеВСтрокуВнутр(СтрокаТЗ.ДоступЕсть));
			ФайлВыгрузки.ЗаписатьКонецЭлемента();
		КонецЦикла; 
		ФайлВыгрузки.ЗаписатьКонецЭлемента();
		
		//выборка и выгрузка прав доступа к справочникам
		ФайлВыгрузки.ЗаписатьНачалоЭлемента("ДоступКСправочникам");
		Запрос = Новый Запрос();
		Запрос.Текст = "
		|ВЫБРАТЬ
		|	ДоступКСправочникам.Пользователь КАК Пользователь,
		|	ДоступКСправочникам.Право КАК Право,
		|	ДоступКСправочникам.Объект КАК Объект,
		|	ДоступКСправочникам.ДоступЕсть КАК ДоступЕсть
		|ИЗ
		|	РегистрСведений.Scan_ДоступКСправочникам КАК ДоступКСправочникам
		|ГДЕ
		|	ДоступКСправочникам.Пользователь = &Пользователь
		|";
		Запрос.УстановитьПараметр("Пользователь", ОбъектНастройкиПрав);
		РезультатЗапроса = Запрос.Выполнить().Выгрузить();
		Для каждого СтрокаТЗ Из РезультатЗапроса Цикл
			ФайлВыгрузки.ЗаписатьНачалоЭлемента("ЗаписьРегистра"); 
			ФайлВыгрузки.ЗаписатьАтрибут("Право", ЗначениеВСтрокуВнутр(СтрокаТЗ.Право));
			ФайлВыгрузки.ЗаписатьАтрибут("Объект", ЗначениеВСтрокуВнутр(СтрокаТЗ.Объект));				
			ФайлВыгрузки.ЗаписатьАтрибут("ДоступЕсть", ЗначениеВСтрокуВнутр(СтрокаТЗ.ДоступЕсть));
			ФайлВыгрузки.ЗаписатьКонецЭлемента();
		КонецЦикла; 
		ФайлВыгрузки.ЗаписатьКонецЭлемента();
		
	КонецЕсли; 	
	ФайлВыгрузки.ЗаписатьКонецЭлемента();		
	ФайлВыгрузки.Закрыть();
КонецПроцедуры
	
//загружает настройки из внешнего файла
&НаКлиенте
Процедура ЗагрузитьНастройкиИзФайла(Знач Оповещение) Экспорт
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	Диалог.МножественныйВыбор = Ложь;
	Диалог.Фильтр = "Файлы XML (*.xml)|*.xml";
	
	Диалог.Показать(Новый ОписаниеОповещения("ЗагрузитьНастройкиИзФайлаЗавершение", ЭтотОбъект, Новый Структура("Диалог, Оповещение", Диалог, Оповещение)));

КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьНастройкиИзФайлаЗавершение(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
    
    Диалог = ДополнительныеПараметры.Диалог;
    Оповещение = ДополнительныеПараметры.Оповещение;
    
    
    Если (ВыбранныеФайлы <> Неопределено) Тогда
        ЗагрузитьНастройкиИзФайлаСервер(Диалог.ПолноеИмяФайла);
    Иначе
        //rarus FominskiyAS 24.04.2019  mantis 14191 +++
		//Сообщить("Не указано имя файла загрузки!", СтатусСообщения.ОченьВажное);
		Сообщить(НСтр("ru = 'Не указано имя файла загрузки!'; en = 'Please enter File name for downloading!'"), СтатусСообщения.ОченьВажное);
		//rarus FominskiyAS 24.04.2019  mantis 14191 ---  
		Если Не Оповещение = Неопределено Тогда 
			ВыполнитьОбработкуОповещения(Оповещение);
		КонецЕсли;
        Возврат;
    КонецЕсли;
	Если Не Оповещение = Неопределено Тогда 
		ВыполнитьОбработкуОповещения(Оповещение);
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ЗагрузитьНастройкиИзФайлаСервер(ПолноеИмяФайла) Экспорт
	ФайлЗагрузки = Новый ЧтениеXML();
	ФайлЗагрузки.ОткрытьФайл(ПолноеИмяФайла);
	ФайлЗагрузки.Прочитать();
	
	Если ЗначениеИзСтрокиВнутр(ФайлЗагрузки.ПолучитьАтрибут("ТипОбъектаНастройки")) <> ТипОбъектаНастройки Тогда 
		//rarus FominskiyAS 24.04.2019  mantis 14191 +++
		//Сообщить("Тип объекта настройки отличается от загружаемого!");
		Сообщить(НСтр("ru = 'Тип объекта настройки отличается от загружаемого!'; en = 'Action failed!'"));
		//rarus FominskiyAS 24.04.2019  mantis 14191 ---   
		Возврат;
	КонецЕсли;
	
	НастройкиДляПользователя = ТипОбъектаНастройки = Перечисления.Scan_НазначениеПравИНастроек.Пользователь;
	
	//временная таблица для хранеия восстановленных значений регистра "ПраваИНастройки"
	ТаблицаПравИНастроек = Новый ТаблицаЗначений;
	ТаблицаПравИНастроек.Колонки.Добавить("ПравоНастройка");
	ТаблицаПравИНастроек.Колонки.Добавить("Значение");
	
	//если объект настройки пользователь, то так же загрузим права доступа к документам и справочникам
	Если НастройкиДляПользователя Тогда 
		//временная таблица для хранеия восстановленных значений регистра "ДоступКДокументам"
		ТаблицаДоступаКДокументам = Новый ТаблицаЗначений;
		ТаблицаДоступаКДокументам.Колонки.Добавить("Право");
		ТаблицаДоступаКДокументам.Колонки.Добавить("Объект");
		ТаблицаДоступаКДокументам.Колонки.Добавить("ДоступЕсть");
		
		//временная таблица для хранеия восстановленных значений регистра "ДоступКСправочникам"
		ТаблицаДоступаКСправочникам = Новый ТаблицаЗначений;
		ТаблицаДоступаКСправочникам.Колонки.Добавить("Право");
		ТаблицаДоступаКСправочникам.Колонки.Добавить("Объект");
		ТаблицаДоступаКСправочникам.Колонки.Добавить("ДоступЕсть");
	КонецЕсли;
	
	Пока ФайлЗагрузки.Прочитать() Цикл
		
		Если (ФайлЗагрузки.ТипУзла = ТипУзлаXML.НачалоЭлемента) И ФайлЗагрузки.Имя = "ПраваИНастройки" Тогда
			ЗагрузкаПравИНастроек = Истина;
			Продолжить;
		КонецЕсли; 
		
		Если (ФайлЗагрузки.ТипУзла = ТипУзлаXML.КонецЭлемента) И ФайлЗагрузки.Имя = "ПраваИНастройки" Тогда
			ЗагрузкаПравИНастроек = Ложь;
			Продолжить;
		КонецЕсли; 
		
		Если НастройкиДляПользователя Тогда
			Если (ФайлЗагрузки.ТипУзла = ТипУзлаXML.НачалоЭлемента) И ФайлЗагрузки.Имя = "ДоступКДокументам" Тогда
				ЗагрузкаДоступаКДокументам = Истина;
				Продолжить;
			КонецЕсли; 
			
			Если (ФайлЗагрузки.ТипУзла = ТипУзлаXML.КонецЭлемента) И ФайлЗагрузки.Имя = "ДоступКДокументам" Тогда
				ЗагрузкаДоступаКДокументам = Ложь;
				Продолжить;
			КонецЕсли; 
			
			Если (ФайлЗагрузки.ТипУзла = ТипУзлаXML.НачалоЭлемента) И ФайлЗагрузки.Имя = "ДоступКСправочникам" Тогда
				ЗагрузкаДоступаКСправочникам = Истина;
				Продолжить;
			КонецЕсли; 
			
			Если (ФайлЗагрузки.ТипУзла = ТипУзлаXML.КонецЭлемента) И ФайлЗагрузки.Имя = "ДоступКСправочникам" Тогда
				ЗагрузкаДоступаКСправочникам = Ложь;
				Продолжить;
			КонецЕсли; 
		КонецЕсли; 
		
		
		Если ФайлЗагрузки.ТипУзла = ТипУзлаXML.КонецЭлемента Тогда Продолжить; КонецЕсли; 
		
		Если ЗагрузкаПравИНастроек Тогда
			НоваяСтрока = ТаблицаПравИНастроек.Добавить();
			НоваяСтрока.ПравоНастройка = ЗначениеИзСтрокиВнутр(ФайлЗагрузки.ПолучитьАтрибут("ПравоНастройка"));
			НоваяСтрока.Значение = ЗначениеИзСтрокиВнутр(ФайлЗагрузки.ПолучитьАтрибут("Значение"));
			Продолжить;
		КонецЕсли;
		
		Если НастройкиДляПользователя Тогда
			Если ЗагрузкаДоступаКДокументам Тогда
				НоваяСтрока = ТаблицаДоступаКДокументам.Добавить();
				НоваяСтрока.Право = ЗначениеИзСтрокиВнутр(ФайлЗагрузки.ПолучитьАтрибут("Право"));
				НоваяСтрока.Объект = ЗначениеИзСтрокиВнутр(ФайлЗагрузки.ПолучитьАтрибут("Объект"));
				НоваяСтрока.ДоступЕсть = ЗначениеИзСтрокиВнутр(ФайлЗагрузки.ПолучитьАтрибут("ДоступЕсть"));
				Продолжить;
			КонецЕсли; 
			
			Если ЗагрузкаДоступаКСправочникам Тогда
				НоваяСтрока = ТаблицаДоступаКСправочникам.Добавить();
				НоваяСтрока.Право = ЗначениеИзСтрокиВнутр(ФайлЗагрузки.ПолучитьАтрибут("Право"));
				НоваяСтрока.Объект = ЗначениеИзСтрокиВнутр(ФайлЗагрузки.ПолучитьАтрибут("Объект"));
				НоваяСтрока.ДоступЕсть = ЗначениеИзСтрокиВнутр(ФайлЗагрузки.ПолучитьАтрибут("ДоступЕсть"));
				Продолжить;
			КонецЕсли; 
		КонецЕсли; 
	КонецЦикла;
	
	//запишем все данные в регистр "ПраваИНастройки"
	НаборЗаписей = РегистрыСведений.Scan_ПраваИНастройки.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Объект.Установить(ОбъектНастройкиПрав);
	Для каждого СтрокаТЗ Из ТаблицаПравИНастроек Цикл
		НоваяЗапись = НаборЗаписей.Добавить();
		НоваяЗапись.Объект = ОбъектНастройкиПрав;
		НоваяЗапись.ПравоНастройка = СтрокаТЗ.ПравоНастройка;
		НоваяЗапись.Значение = СтрокаТЗ.Значение;
	КонецЦикла;  	
	НаборЗаписей.Записать();
	
	Если НастройкиДляПользователя Тогда	
		//запишем все данные в регистр "ДоступКДокументам"
		НаборЗаписей = РегистрыСведений.Scan_ДоступКДокументам.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Пользователь.Установить(ОбъектНастройкиПрав);
		Для каждого СтрокаТЗ Из ТаблицаДоступаКДокументам Цикл
			НоваяЗапись = НаборЗаписей.Добавить();
			НоваяЗапись.Пользователь = ОбъектНастройкиПрав;
			НоваяЗапись.Право = СтрокаТЗ.Право;		
			НоваяЗапись.Объект = СтрокаТЗ.Объект;		
			НоваяЗапись.ДоступЕсть = СтрокаТЗ.ДоступЕсть;
		КонецЦикла;  	
		НаборЗаписей.Записать();
		
		//запишем все данные в регистр "ДоступКСправочникам"
		НаборЗаписей = РегистрыСведений.Scan_ДоступКСправочникам.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Пользователь.Установить(ОбъектНастройкиПрав);
		Для каждого СтрокаТЗ Из ТаблицаДоступаКСправочникам Цикл
			НоваяЗапись = НаборЗаписей.Добавить();
			НоваяЗапись.Пользователь = ОбъектНастройкиПрав;
			НоваяЗапись.Право = СтрокаТЗ.Право;		
			НоваяЗапись.Объект = СтрокаТЗ.Объект;		
			НоваяЗапись.ДоступЕсть = СтрокаТЗ.ДоступЕсть;
		КонецЦикла;  	
		НаборЗаписей.Записать();
	КонецЕсли;
КонецПроцедуры

// Заполняет реквизит "СтруктураОписаний" из макета "НастройкиПоУмолчанию"
// В дальнейшем эта структура может использоваться для вывода описаний в форме
&НаСервере
Процедура ПолучитьСтруктуруОписаний() Экспорт
	Макет = ПланыВидовХарактеристик.Scan_ПраваИНастройки.ПолучитьМакет("НастройкиПоУмолчанию");	
	Для Номер = 1 По 10000 Цикл	// Заведомо больше, чем есть в макете
		Код = СокрЛП(Макет.ПолучитьОбласть("R" + Строка(Номер) + "C3").ТекущаяОбласть.Текст);
		Описание=СокрЛП(Макет.ПолучитьОбласть("R" + Строка(Номер) + "C8").ТекущаяОбласть.Текст);
		Если Код = "" Тогда Прервать;
		ИначеЕсли Описание = "" Тогда Продолжить;
		КонецЕсли;
		СтруктураОписаний.Вставить("_" + Код, Описание);
	КонецЦикла;
КонецПроцедуры

//Сохранение прав объекта в регистр сведений.
// Функция осуществляет рекурсивный обход дерева прав с поиском и сохранением изменений.
// Если ОбъектНастройкиПрав соответствует текущему пользователю,
// его подразделению или организации, а так же компании в целом, производится
// обновление кэша прав (только для клиента).
&НаКлиенте
Функция ЗаписатьПраваИНастройки()
	флИзмененыПраваНастройки = Ложь;
	Рез = ЗаписатьПраваИНастройкиСервер(,, флИзмененыПраваНастройки);
	
	// Если все корректно записалось, то для корневого вызова проверим
	// необходимость обновления собственных прав (только на клиенте)
	Если Рез И флИзмененыПраваНастройки Тогда
		ОбновитьПовторноИспользуемыеЗначения();
		
		Если ТипЗнч(ОбъектНастройкиПрав) = Тип("СправочникСсылка.Пользователи") И ОбъектНастройкиПрав <> ТекущийПользователь Тогда
		//rarus FominskiyAS 24.04.2019  mantis 14191 +++
		//Сообщить("Права пользователя """ + ОбъектНастройкиПрав + """ изменены! Изменения вступят в силу при перезапуске системы пользователем.", СтатусСообщения.Внимание);
		Сообщить(НСтр("ru = 'Права пользователя ""'; en = 'User ""'") + ОбъектНастройкиПрав + НСтр("ru = '"" изменены! Изменения вступят в силу при перезапуске системы пользователем.'; en = '"" rights changed! Please restart the system.'"), СтатусСообщения.Внимание);
		//rarus FominskiyAS 24.04.2019  mantis 14191 ---  	
		КонецЕсли;
	КонецЕсли;
	
	СнятьПризнакМодификацииСтрок(ДеревоПрав);
	
	Возврат рез;
КонецФункции // ЗаписатьПраваИНастройки

&НаКлиенте
Процедура СнятьПризнакМодификацииСтрок(ВеткаДерева)
	Для Каждого ТекСтрока Из ВеткаДерева.ПолучитьЭлементы() Цикл
		Если ТекСтрока.Изменено Тогда
			ТекСтрока.Изменено = Ложь;
		КонецЕсли;
		СнятьПризнакМодификацииСтрок(ТекСтрока);
	КонецЦикла;
КонецПроцедуры

&НаСервере
Функция ЗаписатьПраваИНастройкиСервер(ВеткаДерева = Неопределено, ТекДеревоПрав = Неопределено, флИзмененыПраваИНастройки = Неопределено)
	// При неопределенном входном параметре берем корень дерева
	Если ВеткаДерева = Неопределено Тогда
		ВеткаДерева = РеквизитФормыВЗначение("ДеревоПрав");
	КонецЕсли;
	
	Если ТекДеревоПрав = Неопределено Тогда
		ТекДеревоПрав = РеквизитФормыВЗначение("ДеревоПрав");
	КонецЕсли;
	
	//// проверим заполненность способа распределения затрат
	//нсУчетЗатратЗначение = НайтиОтмеченноеЗначениеВДеревеЗначений(ПланыВидовХарактеристик.уатПраваИНастройки.ВестиУчетЗатрат);
	//Если нсУчетЗатратЗначение <> Неопределено И нсУчетЗатратЗначение Тогда
	//	нсРаспределениеЗатратЗначение = НайтиОтмеченноеЗначениеВДеревеЗначений(ПланыВидовХарактеристик.уатПраваИНастройки.СпособРаспределенияЗатратМеждуТС);
	//	Если нсРаспределениеЗатратЗначение <> Неопределено И НЕ ЗначениеЗаполнено(нсРаспределениеЗатратЗначение) Тогда
	//		Сообщить("Необходимо указать способ распределения затрат между ТС!");
	//		Если ТипЗнч(ВеткаДерева) = Тип("ДеревоЗначений") Тогда
	//			ВеткаДерева = Неопределено;
	//			ТекДеревоПрав = Неопределено;
	//		КонецЕсли;
	//		Возврат Ложь;
	//	КонецЕсли;			
	//КонецЕсли;
	//
	//// проверим правильность выбора основной АЗС типа склад
	//нсАЗСЗначение = НайтиОтмеченноеЗначениеВДеревеЗначений(ПланыВидовХарактеристик.уатПраваИНастройки.ОсновнаяАЗС);	
	//Если ЗначениеЗаполнено(нсАЗСЗначение) Тогда
	//	Если ТипЗнч(нсАЗСЗначение.Контрагент_Склад) <> Тип("СправочникСсылка.Склады") Тогда
	//		нсВидЗаправкиЗначение = НайтиОтмеченноеЗначениеВДеревеЗначений(ПланыВидовХарактеристик.уатПраваИНастройки.ОсновнойВидЗаправки);
	//		Если нсВидЗаправкиЗначение <> Неопределено И (нсВидЗаправкиЗначение = Перечисления.уатВидыДвиженияГСМ.ЗаправкаСклад
	//			ИЛИ нсВидЗаправкиЗначение = Перечисления.уатВидыДвиженияГСМ.ЗаправкаТалоны
	//			ИЛИ нсВидЗаправкиЗначение = Перечисления.уатВидыДвиженияГСМ.ЗаправкаПластиковаяКартаСклад) Тогда
	//			
	//			Сообщить("Должна быть выбрана основная АЗС - склад!");
	//			Если ТипЗнч(ВеткаДерева) = Тип("ДеревоЗначений") Тогда
	//				ВеткаДерева = Неопределено;
	//				ТекДеревоПрав = Неопределено;
	//			КонецЕсли;
	//			Возврат Ложь;
	//		КонецЕсли;			
	//	ИначеЕсли ТипЗнч(нсАЗСЗначение.Контрагент_Склад) <> Тип("СправочникСсылка.Контрагенты") Тогда
	//		нсВидЗаправкиЗначение = НайтиОтмеченноеЗначениеВДеревеЗначений(ПланыВидовХарактеристик.уатПраваИНастройки.ОсновнойВидЗаправки);
	//		Если нсВидЗаправкиЗначение <> Неопределено И (нсВидЗаправкиЗначение = Перечисления.уатВидыДвиженияГСМ.ЗаправкаПоставщик
	//			ИЛИ нсВидЗаправкиЗначение = Перечисления.уатВидыДвиженияГСМ.ЗаправкаПластиковаяКарта
	//			ИЛИ нсВидЗаправкиЗначение = Перечисления.уатВидыДвиженияГСМ.ЗаправкаНаличные) Тогда
	//			
	//			Сообщить("Должна быть выбрана основная АЗС - контрагент!");
	//			Если ТипЗнч(ВеткаДерева) = Тип("ДеревоЗначений") Тогда
	//				ВеткаДерева = Неопределено;
	//				ТекДеревоПрав = Неопределено;
	//			КонецЕсли;
	//			Возврат Ложь;
	//		КонецЕсли;						
	//	КонецЕсли;
	//КонецЕсли; 
	//
	//// проверим что настройка СкладДляПеремещенияГСМ это не группа справочника
	//нсСкладЗначение = НайтиОтмеченноеЗначениеВДеревеЗначений(ПланыВидовХарактеристик.уатПраваИНастройки.СкладДляПеремещенияГСМ);	
	//Если ЗначениеЗаполнено(нсСкладЗначение) И нсСкладЗначение.ЭтоГруппа Тогда
	//	Сообщить("В настройке ""ПАРАМЕТРЫ СИСТЕМЫ / ГСМ / Склад для перемещения ГСМ"" должен быть указан элемент справочника!");
	//	Если ТипЗнч(ВеткаДерева) = Тип("ДеревоЗначений") Тогда
	//		ВеткаДерева = Неопределено;
	//		ТекДеревоПрав = Неопределено;
	//	КонецЕсли;
	//	Возврат Ложь;
	//КонецЕсли;
	
	// каждую ветку будем обходить в попытке, по причине возможного отказа записи
	Попытка
		Для каждого Строка Из ВеткаДерева.Строки Цикл
			Если Строка.Группа Тогда
				Если НЕ ЗаписатьПраваИНастройкиСервер(Строка, ТекДеревоПрав, флИзмененыПраваИНастройки) Тогда
					Возврат Ложь;
				КонецЕсли;
			ИначеЕсли Строка.Изменено Тогда
				Рег=РегистрыСведений.Scan_ПраваИНастройки.СоздатьМенеджерЗаписи();
				Если ТипЗнч (ОбъектНастройкиПрав) = Тип("СправочникСсылка.Пользователи") Тогда
					Рег.Объект = ОбъектНастройкиПрав;
				Иначе
					Рег.Объект = ОбъектНастройкиПрав;
				КонецЕсли;
				Рег.ПравоНастройка = Строка.ПравоНастройка;
				Рег.Значение = Строка.Значение;
				Рег.Пользователь = ПользователиКлиентСервер.АвторизованныйПользователь();		// rarus tenkam 13.05.2019 mantis 14226 +
				Рег.Записать();
				Строка.Изменено = Ложь;
				флИзмененыПраваИНастройки = Истина;
				//Если Строка.Код = "30004" Тогда
				//	ИзмененаНастройкаСрокУстраненияНедостатков = Истина; 				
				//КонецЕсли;
				
				// rarus tenkam 30.08.2018 mantis 13474 +++   				
				Рег2=РегистрыСведений.Scan_ИсторияИзмененияПравИНастроек.СоздатьМенеджерЗаписи();
				Рег2.Период = ТекущаяДата();
				Рег2.Объект = ОбъектНастройкиПрав;
				Рег2.ПравоНастройка = Строка.ПравоНастройка;
				Рег2.Значение = Строка.Значение;
				Рег2.Пользователь = ПользователиКлиентСервер.ТекущийПользователь();
				Рег2.Записать();				
				// rarus tenkam 30.08.2018 mantis 13474 ---
				
			КонецЕсли; 
		КонецЦикла; 
		
		// В случае исключения вернем Ложь - запись не удалась
	Исключение
		Если ТипЗнч(ВеткаДерева) = Тип("ДеревоЗначений") Тогда
			ВеткаДерева = Неопределено;
			ТекДеревоПрав = Неопределено;
		КонецЕсли;
		
		Возврат Ложь;
	КонецПопытки;
	
	Если ТипЗнч(ВеткаДерева) = Тип("ДеревоЗначений") Тогда  // необходимо ввиду рекурсивности вызова
		//Сохранение настройки доступности документов и справочников в регистр сведений.
		Попытка
			Отбор = Новый Структура;
			Отбор.Вставить("Пользователь", ОбъектНастройкиПрав);
			
			ТабДоступностиСправочников_ = РеквизитФормыВЗначение("ТабДоступностиСправочников");
			
			ТекСтроки = ТабДоступностиСправочников_.НайтиСтроки(Отбор);
			Для Каждого ТекСтрока из ТекСтроки Цикл    
				Если ТекСтрока.Изменено Тогда
					Рег = РегистрыСведений.Scan_ДоступКСправочникам.СоздатьМенеджерЗаписи();
					Рег.Пользователь = ОбъектНастройкиПрав;
					Рег.Право = ТекСтрока.Право;
					Рег.Объект = ТекСтрока.Объект;
					Рег.ДоступЕсть = ТекСтрока.Доступность;
					Рег.Записать();
					ТекСтрока.Изменено = Ложь;	
					флИзмененыПраваИНастройки = Истина;
				КонецЕсли;
			КонецЦикла;
			ЗначениеВРеквизитФормы(ТабДоступностиСправочников_, "ТабДоступностиСправочников");
			
			ТабДоступностиДокументов_ = РеквизитФормыВЗначение("ТабДоступностиДокументов");
			
			ТекСтроки = ТабДоступностиДокументов_.НайтиСтроки(Отбор);
			Для Каждого ТекСтрока из ТекСтроки Цикл
				Если ТекСтрока.Изменено Тогда
					Рег = РегистрыСведений.Scan_ДоступКДокументам.СоздатьМенеджерЗаписи();
					Рег.Пользователь = ОбъектНастройкиПрав;
					Рег.Право = ТекСтрока.Право;
					Рег.Объект = ТекСтрока.Объект;
					Рег.ДоступЕсть = ТекСтрока.Доступность;
					Рег.Записать();
					ТекСтрока.Изменено = Ложь;	
					флИзмененыПраваИНастройки = Истина;
				КонецЕсли;
			КонецЦикла;
			
			ЗначениеВРеквизитФормы(ТабДоступностиДокументов_, "ТабДоступностиДокументов");
		Исключение Возврат Ложь;
		КонецПопытки;
		
		ВеткаДерева = Неопределено;
		ТекДеревоПрав = Неопределено;
	КонецЕсли;
	
	Возврат Истина;
КонецФункции

//Получение иерархической структуры ПВХ Права и настройки в результат запроса
&НаСервере
Процедура ПолучитьИерархиюПрав() Экспорт
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ПланВидовХарактеристикПраваИНастройки.Ссылка КАК Ссылка,
	|	ПланВидовХарактеристикПраваИНастройки.Код КАК Код,
	|	ПланВидовХарактеристикПраваИНастройки.Наименование КАК Наименование,
	|	ПланВидовХарактеристикПраваИНастройки.ЭтоГруппа КАК ЭтоГруппа,
	|	ПланВидовХарактеристикПраваИНастройки.ТипЗначения КАК ТипЗначения,
	|	ПланВидовХарактеристикПраваИНастройки.Назначение КАК Назначение,
	|	ПланВидовХарактеристикПраваИНастройки.ЭтоНастройка КАК ЭтоНастройка,
	|	ПланВидовХарактеристикПраваИНастройки.Родитель КАК Родитель,
	|	ПланВидовХарактеристикПраваИНастройки.ЗначениеПоУмолчанию КАК ЗначениеПоУмолчанию,
	|	ПланВидовХарактеристикПраваИНастройки.НастройкаОрганизации КАК НастройкаОрганизации,
	|	ПланВидовХарактеристикПраваИНастройки.НастройкаПодразделения КАК НастройкаПодразделения,
	|	ПланВидовХарактеристикПраваИНастройки.НастройкаПользователя КАК НастройкаПользователя,
	// rarus tenkam 05.06.2018 mantis 13137 +++
	|	ВЫБОР
	|		КОГДА ПланВидовХарактеристикПраваИНастройки.Код = ""72001""
	|				ИЛИ ПланВидовХарактеристикПраваИНастройки.Код = ""72002""
	|				ИЛИ ПланВидовХарактеристикПраваИНастройки.Код = ""72003""
	|				ИЛИ ПланВидовХарактеристикПраваИНастройки.Код = ""72004""
	|				ИЛИ ПланВидовХарактеристикПраваИНастройки.Код = ""72008""
	|				ИЛИ ПланВидовХарактеристикПраваИНастройки.Код = ""72009""
	|				ИЛИ ПланВидовХарактеристикПраваИНастройки.Код = ""72013""
	|				ИЛИ ПланВидовХарактеристикПраваИНастройки.Код = ""72014""
	|				ИЛИ ПланВидовХарактеристикПраваИНастройки.Код = ""72017""
	|				ИЛИ ПланВидовХарактеристикПраваИНастройки.Код = ""72018""
	|				ИЛИ ПланВидовХарактеристикПраваИНастройки.Код = ""72020""
	|				ИЛИ ПланВидовХарактеристикПраваИНастройки.Код = ""72021""
	|				ИЛИ ПланВидовХарактеристикПраваИНастройки.Код = ""72026""
	|				ИЛИ ПланВидовХарактеристикПраваИНастройки.Код = ""72027""
	|			ТОГДА ЛОЖЬ
	|		КОГДА ПланВидовХарактеристикПраваИНастройки.Код = ""72100""
	|				ИЛИ ПланВидовХарактеристикПраваИНастройки.Код = ""72101""
	|				ИЛИ ПланВидовХарактеристикПраваИНастройки.Код = ""72102""
	|				ИЛИ ПланВидовХарактеристикПраваИНастройки.Код = ""72103""
	|				ИЛИ ПланВидовХарактеристикПраваИНастройки.Код = ""72104""
	|				ИЛИ ПланВидовХарактеристикПраваИНастройки.Код = ""72105""
	|				ИЛИ ПланВидовХарактеристикПраваИНастройки.Код = ""72106""
	|				ИЛИ ПланВидовХарактеристикПраваИНастройки.Код = ""72107""
	|				ИЛИ ПланВидовХарактеристикПраваИНастройки.Код = ""72108""
	|				ИЛИ ПланВидовХарактеристикПраваИНастройки.Код = ""72109""
	|				ИЛИ ПланВидовХарактеристикПраваИНастройки.Код = ""72110""
	|				ИЛИ ПланВидовХарактеристикПраваИНастройки.Код = ""72111""
	|				ИЛИ ПланВидовХарактеристикПраваИНастройки.Код = ""72112""
	|				ИЛИ ПланВидовХарактеристикПраваИНастройки.Код = ""72113""
	//rarus bonmak 16.01.2020 вне задачи ++
	|				ИЛИ ПланВидовХарактеристикПраваИНастройки.Код = ""72114""
	|				ИЛИ ПланВидовХарактеристикПраваИНастройки.Код = ""72115""
	|				ИЛИ ПланВидовХарактеристикПраваИНастройки.Код = ""72116""
	|				ИЛИ ПланВидовХарактеристикПраваИНастройки.Код = ""72117""
	|				ИЛИ ПланВидовХарактеристикПраваИНастройки.Код = ""72118""
	|				ИЛИ ПланВидовХарактеристикПраваИНастройки.Код = ""72119""
	//rarus bonmak 16.01.2020 вне задачи --
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК НастройкаМеханизмаШаблонов
	// rarus tenkam 05.06.2018 mantis 13137 ---
	|ИЗ
	|	ПланВидовХарактеристик.Scan_ПраваИНастройки КАК ПланВидовХарактеристикПраваИНастройки
	|ГДЕ
	|	НЕ ПланВидовХарактеристикПраваИНастройки.ПометкаУдаления
	|
	|УПОРЯДОЧИТЬ ПО
	|	Код ИЕРАРХИЯ";
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Рез = Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
	
	////удалим из дерева права доступа для несуществующих спр. и док.
	//ПраваДоступаСправочников = Рез.Строки.Найти(ПланыВидовХарактеристик.уатПраваИНастройки.ПраваДоступаСправочников, "Ссылка", Истина);
	//Если ПраваДоступаСправочников <> Неопределено Тогда
	//	мсвСтрокДляУдаления = Новый Массив;
	//	Для Каждого ТекСтрока Из ПраваДоступаСправочников.Строки Цикл
	//		Если Найти(ТекСтрока.Наименование, "Право доступа") <> 0 Тогда
	//			Если Метаданные.Справочники.Найти(СокрЛП(Сред(ТекСтрока.Наименование, 15))) = Неопределено Тогда
	//				мсвСтрокДляУдаления.Добавить(ТекСтрока);
	//			КонецЕсли;
	//		КонецЕсли;
	//	КонецЦикла;
	//	Для Каждого ТекСтрокаУдаления Из мсвСтрокДляУдаления Цикл
	//		ПраваДоступаСправочников.Строки.Удалить(ТекСтрокаУдаления);
	//	КонецЦикла;
	//КонецЕсли;
	//ПраваДоступаДокументов = Рез.Строки.Найти(ПланыВидовХарактеристик.уатПраваИНастройки.ПраваДоступаДокументов, "Ссылка", Истина);
	//Если ПраваДоступаДокументов <> Неопределено Тогда
	//	мсвСтрокДляУдаления = Новый Массив;
	//	Для Каждого ТекСтрока Из ПраваДоступаДокументов.Строки Цикл
	//		Если Найти(ТекСтрока.Наименование, "Право доступа") <> 0 Тогда
	//			Если Метаданные.Документы.Найти(СокрЛП(Сред(ТекСтрока.Наименование, 15))) = Неопределено Тогда
	//				мсвСтрокДляУдаления.Добавить(ТекСтрока);
	//			КонецЕсли;
	//		КонецЕсли;
	//	КонецЦикла;
	//	Для Каждого ТекСтрокаУдаления Из мсвСтрокДляУдаления Цикл
	//		ПраваДоступаДокументов.Строки.Удалить(ТекСтрокаУдаления);
	//	КонецЦикла;
	//КонецЕсли;
	
	ЗначениеВРеквизитФормы(Рез, "ЗапросПравИНастроек");
КонецПроцедуры

//Получение настройки доступности справочников из результат запроса
&НаСервере
Процедура ПолучитьДоступностьСправочников() Экспорт
	Текст = "ВЫБРАТЬ
	|	ДоступКСправочникам.Пользователь,
	|	ДоступКСправочникам.Объект,
	|	ДоступКСправочникам.Право,
	|	ДоступКСправочникам.ДоступЕсть КАК Доступность,
	|	ЛОЖЬ КАК Изменено
	|ИЗ
	|	РегистрСведений.Scan_ДоступКСправочникам КАК ДоступКСправочникам";
	Запрос = Новый Запрос;
	Запрос.Текст = Текст;
	Рез = Запрос.Выполнить().Выгрузить();
	
	ЗначениеВРеквизитФормы(Рез, "ТабДоступностиСправочников");
КонецПроцедуры

//Получение настройки доступности документов из результат запроса
&НаСервере
Процедура ПолучитьДоступностьДокументов() Экспорт
	Текст = "ВЫБРАТЬ
	|	ДоступКДокументам.Пользователь,
	|	ДоступКДокументам.Объект,
	|	ДоступКДокументам.Право,
	|	ДоступКДокументам.ДоступЕсть КАК Доступность,
	|	ВЫБОР
	|		КОГДА ДоступКДокументам.Объект ССЫЛКА Справочник.Пользователи
	|			ТОГДА ""Пользователи""
	|	КОНЕЦ КАК ВидОбъекта,
	|	ЛОЖЬ КАК Изменено
	|ИЗ
	|	РегистрСведений.Scan_ДоступКДокументам КАК ДоступКДокументам";
	Запрос = Новый Запрос;
	Запрос.Текст = Текст;
	Рез = Запрос.Выполнить().Выгрузить();
	
	ЗначениеВРеквизитФормы(Рез, "ТабДоступностиДокументов");
КонецПроцедуры

//Формирование иерархии
&НаСервере
Процедура ПостроениеВеткиДереваПрав(ВыборкаРодитель, СтрокиРодитель, Уровень = 0, СтруктураТекущихПрав) Экспорт
	
	ИспользованиеШаблонов = Scan_ПраваИНастройки.Scan_Право("ИспользоватьМеханизмШаблоновДляОтправкиПисем");	//rarus tenkam 05.06.2018 mantis 13137 +
	
	Для Каждого Выборка Из ВыборкаРодитель.Строки Цикл
		// Проверим, надо ли выводить эту строчку выборки
		Если Выборка.Уровень() <> Уровень 
			ИЛИ НЕ Выборка.ЭтоГруппа И ((Выборка.ЭтоНастройка И НЕ ОтображатьНастройки)
			ИЛИ (НЕ Выборка.ЭтоНастройка И НЕ ОтображатьПрава)
			// rarus tenkam 05.06.2018 mantis 13137 +++
			ИЛИ (ЗначениеЗаполнено(Выборка.НастройкаМеханизмаШаблонов) И НЕ Выборка.НастройкаМеханизмаШаблонов И ИспользованиеШаблонов)
			ИЛИ (ЗначениеЗаполнено(Выборка.НастройкаМеханизмаШаблонов) И Выборка.НастройкаМеханизмаШаблонов И НЕ ИспользованиеШаблонов)
			// rarus tenkam 05.06.2018 mantis 13137 ---
			//ИЛИ (НЕ ЭтоОбъединеннаяКонфигурация И Выборка.Ссылка = ПланыВидовХарактеристик.уатПраваИНастройки.ДокументыТК)
			//ИЛИ (НЕ ЭтоОбъединеннаяКонфигурация И Выборка.Родитель = ПланыВидовХарактеристик.уатПраваИНастройки.ДокументыТК)
			//ИЛИ (НЕ ЭтоОбъединеннаяКонфигурацияМодульЛогистики И Выборка.Ссылка = ПланыВидовХарактеристик.уатПраваИНастройки.РазрешитьРаботуСУЭ)
			//ИЛИ (НЕ ЭтоОбъединеннаяКонфигурация И Выборка.Ссылка = ПланыВидовХарактеристик.уатПраваИНастройки.РазрешитьРаботуСУАТ)
			ИЛИ (ЭтоОбъединеннаяКонфигурация И Выборка.Ссылка = ПланыВидовХарактеристик.Scan_ПраваИНастройки.РазрешитьРаботуСПрограммой)
			//ИЛИ (ТипОбъектаНастройки = Перечисления.уатНазначениеПравИНастроек.Компания      И НЕ Выборка.НастройкаКомпании)
			ИЛИ (ТипОбъектаНастройки = Перечисления.Scan_НазначениеПравИНастроек.Организация   И НЕ Выборка.НастройкаОрганизации)
			ИЛИ (ТипОбъектаНастройки = Перечисления.Scan_НазначениеПравИНастроек.Подразделение И НЕ Выборка.НастройкаПодразделения)
			ИЛИ (ТипОбъектаНастройки = Перечисления.Scan_НазначениеПравИНастроек.Пользователь  И НЕ Выборка.НастройкаПользователя)) Тогда
			Продолжить;
		КонецЕсли;
		
		//rarus tenkam 12.09.2016 (убрала оформление системы) ++
		//// Удалим неиспользуемые настройки оформления системы
		//Если Выборка.Ссылка = ПланыВидовХарактеристик.Scan_ПраваИНастройки.ОформлениеСистемы 
		//	ИЛИ Выборка.Родитель = ПланыВидовХарактеристик.Scan_ПраваИНастройки.ОформлениеСистемы Тогда
		//	Продолжить;
		//КонецЕсли;
		//rarus tenkam 12.09.2016 --

		// Добавим строку и заполним её параметры
		Строка = СтрокиРодитель.Добавить();
		Строка.ПравоНастройка = Выборка.Ссылка;
		Строка.Код = Выборка.Код;
		Строка.Назначение = Выборка.Назначение;
		Строка.ЭтоНастройка = Выборка.ЭтоНастройка;
		Строка.Группа = Выборка.ЭтоГруппа;
		Строка.Изменено = Ложь;
		Строка.ПоУмолчанию = Выборка.ЗначениеПоУмолчанию;
		//Строка.НастройкаКомпании = Выборка.НастройкаКомпании;
		Строка.НастройкаОрганизации = Выборка.НастройкаОрганизации;
		Строка.НастройкаПодразделения = Выборка.НастройкаПодразделения;
		Строка.НастройкаПользователя = Выборка.НастройкаПользователя;
		
		// Проверим, если это группа, пойдем на следующий уровень рекурсией
		Если Строка.Группа Тогда
			Строка.КартинкаЭлемента = 6;	// Картинка открытой папки из ккМаркеры
			ПостроениеВеткиДереваПрав(Выборка, Строка.Строки, 1 + Выборка.Уровень(), СтруктураТекущихПрав);
			// Для группы ни значения ни типа не бывает
		Иначе
			// Сначала проверим число типов, так как поддерживается строго только один
			МассивТипов = Выборка.ТипЗначения.Типы();
			Если МассивТипов.Количество() = 1 Тогда
				// Определимся с типом значения
				Строка.Тип = МассивТипов[0]; // Тут только один, составные не поддерживаем
				Если Строка.Тип = Тип("Булево") Тогда
					Строка.ЗначениеБулево = Scan_ПраваИНастройки.Scan_Право(Строка.ПравоНастройка, СтруктураТекущихПрав);
					Строка.Значение = Строка.ЗначениеБулево;
				Иначе
					Строка.ЗначениеБулево = 2;
					Строка.Значение = Scan_ПраваИНастройки.Scan_Право(Строка.ПравоНастройка, СтруктураТекущихПрав);
				КонецЕсли; 
			//Иначе // Умудрились создать элемент с несколькими типами или вовсе без них
			//	// Проверка на составной тип сотрудников
			//	Если Строка.ПравоНастройка = ПланыВидовХарактеристик.уатПраваИНастройки.ОсновнойДиспетчер 
			//		ИЛИ Строка.ПравоНастройка = ПланыВидовХарактеристик.уатПраваИНастройки.ОсновнойВрач
			//		ИЛИ Строка.ПравоНастройка = ПланыВидовХарактеристик.уатПраваИНастройки.ОсновнойМеханик Тогда
			//		Строка.Тип = Тип("СправочникСсылка.Сотрудники");
			//		Строка.ЗначениеБулево = уатПраваИНастройки.уатПраво(Строка.ПравоНастройка, СтруктураТекущихПрав);
			//		Строка.Значение = Строка.ЗначениеБулево;
			//	Иначе
			//		Строка.ЗначениеБулево = 2;
			//		Строка.Значение = "Ошибка в заданных типах - должен быть один тип!";
			//	КонецЕсли;
			КонецЕсли;
			// Теперь определим доступность и картинку
			//Если (ТипОбъектаНастройки = Перечисления.уатНазначениеПравИНастроек.Компания И  НЕ Строка.НастройкаКомпании) 
				//ИЛИ (ТипОбъектаНастройки = Перечисления.уатНазначениеПравИНастроек.Организация И  НЕ Строка.НастройкаОрганизации) 
				Если (ТипОбъектаНастройки = Перечисления.Scan_НазначениеПравИНастроек.Организация И  НЕ Строка.НастройкаОрганизации)
				ИЛИ (ТипОбъектаНастройки = Перечисления.Scan_НазначениеПравИНастроек.Подразделение И  НЕ Строка.НастройкаПодразделения) 
				ИЛИ (ТипОбъектаНастройки = Перечисления.Scan_НазначениеПравИНастроек.Пользователь  И  НЕ Строка.НастройкаПользователя) Тогда
				
				Строка.КартинкаЭлемента = 21;	// Картинка серого маркера из ккМаркеры
			ИначеЕсли Строка.ЭтоНастройка Тогда
				Строка.КартинкаЭлемента = 20;	// Картинка зеленого маркера из ккМаркеры
			Иначе
				Строка.КартинкаЭлемента = 18;	// Картинка красного маркера из ккМаркеры
			КонецЕсли; 
		КонецЕсли;
		
		Если Строка.Группа И Строка.Строки.Количество() = 0 Тогда
			СтрокиРодитель.Удалить(Строка);
			Продолжить;
		КонецЕсли;
		
		Строка.ТипБулево = (Строка.Тип = Тип("Булево"));
		//Строка.ТипВремя = (Выборка.Ссылка = ПланыВидовХарактеристик.Scan_ПраваИНастройки.ВремяФактическогоУходаИзделий);	//rarus tenkam 05.10.2016 mantis 7183 +  	//rarus tenkam 14.11.2016 mantis 7688 + (новое требование - уберем настройку)
		//	ИЛИ Выборка.Ссылка = ПланыВидовХарактеристик.уатПраваИНастройки.ВремяВыездаПЛ
		//	ИЛИ Выборка.Ссылка = ПланыВидовХарактеристик.уатПраваИНастройки.ВремяДоОбеда
		//	ИЛИ Выборка.Ссылка = ПланыВидовХарактеристик.уатПраваИНастройки.ПродолжительностьОбеда
		//	ИЛИ Выборка.Ссылка = ПланыВидовХарактеристик.уатПраваИНастройки.ПродолжительностьРабочегоДня);
		//
		Если (Строка.Значение = Перечисления.Scan_ВидыПравДляДокументов.РедактированиеПоПользователям
			ИЛИ Строка.Значение = Перечисления.Scan_ВидыПравДляСправочников.РедактированиеПоГруппам) Тогда
			
			Строка.ПравоДоступаРедактированиеПоПользователям = Истина;
		КонецЕсли;
		
		//Если (ТипОбъектаНастройки = ПредопределенноеЗначение("Перечисление.уатНазначениеПравИНастроек.Компания") И НЕ Строка.НастройкаКомпании = NULL И НЕ Строка.НастройкаКомпании)
			Если (ТипОбъектаНастройки = ПредопределенноеЗначение("Перечисление.Scan_НазначениеПравИНастроек.Организация") И Scan_ОбщегоНазначения.Scan_ЗначениеНеЗаполнено(ОбъектНастройкиПрав) И НЕ Строка.НастройкаОрганизации = NULL И Строка.НастройкаОрганизации)
			ИЛИ (ТипОбъектаНастройки = ПредопределенноеЗначение("Перечисление.Scan_НазначениеПравИНастроек.Подразделение") И Scan_ОбщегоНазначения.Scan_ЗначениеНеЗаполнено(ОбъектНастройкиПрав) И НЕ Строка.НастройкаПодразделения = NULL И Строка.НастройкаПодразделения)
			ИЛИ (ТипОбъектаНастройки = ПредопределенноеЗначение("Перечисление.Scan_НазначениеПравИНастроек.Пользователь") И Scan_ОбщегоНазначения.Scan_ЗначениеНеЗаполнено(ОбъектНастройкиПрав) И НЕ Строка.НастройкаПользователя = NULL И Строка.НастройкаПользователя)
			Тогда
			
			Строка.ТолькоПросмотр = Истина;
			
		ИначеЕсли Строка.ПравоНастройка = ПредопределенноеЗначение("ПланВидовХарактеристик.Scan_ПраваИНастройки.РазрешитьИзменениеЛичныхНастроек") И ТекущийПользователь = ОбъектНастройкиПрав Тогда
			Строка.ТолькоПросмотр = Истина;
		// rarus tenkam 30.05.2019 mantis 14224 +++
		// rarus tenkam 06.11.2020 mantis 16673 +++
		//ИначеЕсли Строка.ПравоНастройка = ПредопределенноеЗначение("ПланВидовХарактеристик.Scan_ПраваИНастройки.КоличествоДнейДляРасчетаLDD") 
		//		И НЕ Scan_ПраваИНастройки.Scan_Право("РазрешатьИзменениеКоличестваДнейДляLDD") Тогда
		//	Строка.ТолькоПросмотр = Истина;		
		// rarus tenkam 06.11.2020 mantis 16673 ---
		ИначеЕсли Строка.ПравоНастройка = ПредопределенноеЗначение("ПланВидовХарактеристик.Scan_ПраваИНастройки.КоличествоДнейДляРасчетаDD") 
				И НЕ Scan_ПраваИНастройки.Scan_Право("РазрешатьИзменениеКоличестваДнейДляРасчетаДатыDD") Тогда
			Строка.ТолькоПросмотр = Истина;		
		ИначеЕсли Строка.ПравоНастройка = ПредопределенноеЗначение("ПланВидовХарактеристик.Scan_ПраваИНастройки.КоличествоДнейДляРасчетаDDКузовостроительВРФ") 
				И НЕ Scan_ПраваИНастройки.Scan_Право("РазрешатьИзменениеКоличестваДнейДляРасчетаДатыDDКузовостроительВРФ") Тогда
			Строка.ТолькоПросмотр = Истина;		
		// rarus tenkam 30.05.2019 mantis 14224 ---	
		КонецЕсли;
		
	КонецЦикла; 
КонецПроцедуры

// Процедура формирует структуру прав объекта в виде дерева
//
&НаКлиенте
Процедура ФормированиеДереваПравОбъекта() Экспорт
	Состояние("Формирование дерева прав и настроек ... ");
	ФормированиеДереваПравОбъектаСервер();
	РазвернутьДеревоПравПоУмолчанию(ДеревоПрав);
	ПроставитьПризнакЗначениеРавноДефолтному(ДеревоПрав);
КонецПроцедуры

&НаКлиенте
Процедура РазвернутьДеревоПравПоУмолчанию(ВеткаДерева)
	Для Каждого ТекСтрока Из ВеткаДерева.ПолучитьЭлементы() Цикл
		Если НЕ ТекСтрока.Группа Тогда
			//ИЛИ ТекСтрока.ПравоНастройка = ПредопределенноеЗначение("ПланВидовХарактеристик.уатПраваИНастройки.ПраваДоступаСправочников")
			//ИЛИ ТекСтрока.ПравоНастройка = ПредопределенноеЗначение("ПланВидовХарактеристик.уатПраваИНастройки.ПраваДоступаДокументов") Тогда
			
			Продолжить;
		КонецЕсли;
		
		Элементы.ДеревоПрав.Развернуть(ТекСтрока.ПолучитьИдентификатор());
		РазвернутьДеревоПравПоУмолчанию(ТекСтрока);
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура ФормированиеДереваПравОбъектаСервер() Экспорт
	// Получим соответствие прав и настроек выбранного объекта в реквизит СтруктураТекущихПрав
	СтруктураТекущихПрав = Scan_ПраваИНастройки.Scan_ПолучитьПраваИНастройкиПользователя(ОбъектНастройкиПрав);
	
	// Все запросы к БД сделаны, далее просто отобразим данные в соответствии с фильтрами
	ДеревоПравСервер = РеквизитФормыВЗначение("ДеревоПрав");
	
	ДеревоПравСервер.Строки.Очистить();
	Если НЕ (ОтображатьПрава ИЛИ ОтображатьНастройки) Тогда
		// Хотя бы один флаг должен быть включен.
		ОтображатьНастройки = Истина;
	КонецЕсли;
	
	ЗапросПравИНастроекСервер = РеквизитФормыВЗначение("ЗапросПравИНастроек");
	ПостроениеВеткиДереваПрав(ЗапросПравИНастроекСервер, ДеревоПравСервер.Строки, 0, СтруктураТекущихПрав);
	
	ЗначениеВРеквизитФормы(ДеревоПравСервер, "ДеревоПрав");
КонецПроцедуры

&НаКлиенте
Процедура ПроставитьПризнакЗначениеРавноДефолтному(ВеткаДерева);
	Для Каждого Текстрока Из ВеткаДерева.ПолучитьЭлементы() Цикл
		флРавно = (Текстрока.Значение = Текстрока.ПоУмолчанию);
		Если ТекСтрока.ЗначениеРавноДефолтному <> флРавно Тогда
			ТекСтрока.ЗначениеРавноДефолтному = флРавно;
		КонецЕсли;
		ПроставитьПризнакЗначениеРавноДефолтному(Текстрока);
	КонецЦикла;
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПравоДоступауатВыгрузкаРегламентированныхОтчетов()
	Возврат ПланыВидовХарактеристик.Scan_ПраваИНастройки.НайтиПоНаименованию("Право доступа уатВыгрузкаРегламентированныхОтчетов");
КонецФункции

&НаСервереБезКонтекста
Функция ПравоДоступауатРегламентированныйОтчет()
	Возврат ПланыВидовХарактеристик.Scan_ПраваИНастройки.НайтиПоНаименованию("Право доступа уатРегламентированныйОтчет");
КонецФункции


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

// Рекурсивная функция для сворачивания групп включая вложенные
&НаКлиенте
Процедура СвернутьСтрокиДерева(ТекСтрокаДерева)
	Для каждого ТекСтрока из ТекСтрокаДерева.ПолучитьЭлементы() Цикл
		Элементы.ДеревоПрав.Свернуть(ТекСтрока.ПолучитьИдентификатор())
	КонецЦикла;
КонецПроцедуры

// Удаляет все записи из регистра ДоступКСправочникам или ДоступКДокументам
// и из таблиц ТабДоступностиСправочников или ТабДоступностиДокументов
// по реквизиту ОбъектНастройкиПрав и текущему значению ПравоНастройка  
&НаКлиенте
Процедура УдалитьТекущиеДоступности()
	ТекущаяСтрока = ДеревоПрав.НайтиПоИдентификатору(Элементы.ДеревоПрав.ТекущаяСтрока);
	УдалитьТекущиеДоступностиСервер(ТекущаяСтрока.ПравоНастройка, ТекущаяСтрока.Значение);
КонецПроцедуры

&НаСервере
Процедура УдалитьТекущиеДоступностиСервер(ПравоНастройка, ЗначениеПрава)
	Отбор = Новый Структура;
	Отбор.Вставить("Пользователь", ОбъектНастройкиПрав);
	Отбор.Вставить("Право", ПравоНастройка);
	
	Если ТипЗнч(ЗначениеПрава) = Тип("ПеречислениеСсылка.Scan_ВидыПравДляСправочников") Тогда	
		СтрокиПоиска = ТабДоступностиСправочников.НайтиСтроки(Отбор);
		Для Каждого ТекСтрока из СтрокиПоиска Цикл
			ТабДоступностиСправочников.Удалить(ТекСтрока);
		КонецЦикла;
		Рег = РегистрыСведений.Scan_ДоступКСправочникам.СоздатьНаборЗаписей();
		
	ИначеЕсли ТипЗнч(ЗначениеПрава) = Тип("ПеречислениеСсылка.Scan_ВидыПравДляДокументов") Тогда	
		СтрокиПоиска = ТабДоступностиДокументов.НайтиСтроки(Отбор);
		Для Каждого ТекСтрока из СтрокиПоиска Цикл
			ТабДоступностиДокументов.Удалить(ТекСтрока);
		КонецЦикла;
		Рег = РегистрыСведений.Scan_ДоступКДокументам.СоздатьНаборЗаписей();
		
	КонецЕсли;	
	
	Рег.Отбор.Пользователь.Значение = ОбъектНастройкиПрав;
	Рег.Отбор.Пользователь.Использование = Истина;
	Рег.Отбор.Право.Значение = ПравоНастройка;
	Рег.Отбор.Право.Использование = Истина;
	//Рег.Прочитать();
	//Рег.Очистить();
	Рег.Записать();
КонецПроцедуры

// Устанавливает или убирает значение Истина в колонке доступность для всех
// строк таблицы ДоступностиОбъектов
&НаКлиенте
Процедура УстановкаФлаговДоступностиОбъектов(ЗначениеДоступности)
	Для Каждого Стр из ТаблицаДоступностиОбъектов Цикл
		Если Стр.Доступность <> ЗначениеДоступности Тогда
			Стр.Доступность = ЗначениеДоступности;
			Стр.Изменено = Истина;
		КонецЕсли;
	КонецЦикла;
	
	Модифицированность = Истина;
КонецПроцедуры	

// при активных страничках "Справочники" или "Документы" панели1 
// будут переписаны отображаемые записи в ТабДоступностиСправочников или 
// ТабДоступностиДокументов
&НаКлиенте
Процедура ПерезаписатьСтраницы()
	Если ВидОбъектаНастройкиПрав = "Справочники" Тогда
		ПереписатьТабДоступностиСправочников();
	ИначеЕсли ВидОбъектаНастройкиПрав ="Документы" Тогда
		ПереписатьТабДоступностиДокументов();
	КонецЕсли;
	
КонецПроцедуры

//При смене страничек изменяет доступности элементов формы
&НаКлиенте
Процедура ДоступностьНастроекИПараметров(Доступность)
	Доступность = ?(ДостПрава, Доступность, Ложь);
	Элементы.ТипОбъектаНастройки.Доступность = Доступность;
	Элементы.ОтображатьНастройки.Доступность = Доступность;
	Элементы.ОтображатьПрава.Доступность = Доступность;
КонецПроцедуры

//Заполняет строки таблицы доступности справочников из таблицы ТабДоступностиСправочников
&НаКлиенте
Процедура ЗаполнитьТаблицуДоступностейОбъектов(ВыбраннаяСтрока)
	
	ТаблицаДоступностиОбъектов.Очистить();	
	Отбор = Новый Структура;
	Отбор.Вставить("Пользователь", ОбъектНастройкиПрав);
	Отбор.Вставить("Право", ВыбраннаяСтрока.ПравоНастройка);
	Если ВидОбъектаНастройкиПрав = "Справочники" Тогда
		СтрокиПоиска = ТабДоступностиСправочников.НайтиСтроки(Отбор);
		
		Для Каждого ТекСтрока из СтрокиПоиска Цикл
			НоваяСтрока = ТаблицаДоступностиОбъектов.Добавить();
			НоваяСтрока.Пользователь = ТекСтрока.Пользователь;
			НоваяСтрока.Право = ТекСтрока.Право;
			НоваяСтрока.Изменено = ТекСтрока.Изменено;
			НоваяСтрока.Объект = ТекСтрока.Объект;
			НоваяСтрока.Доступность = ТекСтрока.Доступность;
			НоваяСтрока.Инд = ТабДоступностиСправочников.Индекс(ТекСтрока);
		КонецЦикла;
		
	ИначеЕсли ВидОбъектаНастройкиПрав = "Документы" Тогда
		Если ВыбраннаяСтрока.Значение = ПредопределенноеЗначение("Перечисление.Scan_ВидыПравДляДокументов.РедактированиеПоПользователям") Тогда
			Отбор.Вставить("ВидОбъекта", "Пользователи");
		КонецЕсли;
		
		СтрокиПоиска = ТабДоступностиДокументов.НайтиСтроки(Отбор);
		Для Каждого ТекСтрока из СтрокиПоиска Цикл
			НоваяСтрока = ТаблицаДоступностиОбъектов.Добавить();
			НоваяСтрока.Объект = ТекСтрока.Объект;
			НоваяСтрока.Право = ТекСтрока.Право;
			НоваяСтрока.Пользователь = ТекСтрока.Пользователь;
			НоваяСтрока.Изменено = ТекСтрока.Изменено;
			НоваяСтрока.Доступность = ТекСтрока.Доступность;
			НоваяСтрока.Инд = ТабДоступностиДокументов.Индекс(ТекСтрока);
		КонецЦикла;	
	КонецЕсли;
КонецПроцедуры

//Заполняет строки таблицы ТабДоступностиСправочников из таблицы доступности справочников
&НаКлиенте
Процедура ПереписатьТабДоступностиСправочников()
	КоличествоСтрок = ТабДоступностиСправочников.Количество();
	Для каждого ТекСтрока Из ТаблицаДоступностиОбъектов Цикл
		Если не ТекСтрока.Изменено Тогда
			Продолжить;
		КонецЕсли;
		//проверка переписываемой строки имеется ли она в ТабДоступностиСправочников
		Если ТекСтрока.Инд < КоличествоСтрок Тогда
			ТабДоступностиСправочников[ТекСтрока.Инд].Пользователь  =ТекСтрока.Пользователь;
			ТабДоступностиСправочников[ТекСтрока.Инд].Право = ТекСтрока.Право;
			ТабДоступностиСправочников[ТекСтрока.Инд].Объект = ТекСтрока.Объект;
			ТабДоступностиСправочников[ТекСтрока.Инд].Доступность = ТекСтрока.Доступность;
			ТабДоступностиСправочников[ТекСтрока.Инд].Изменено = Истина;
			ТекСтрока.Изменено=Ложь;
		Иначе
			НоваяСтрока = ТабДоступностиСправочников.Добавить();
			НоваяСтрока.Пользователь = ТекСтрока.Пользователь;
			НоваяСтрока.Право = ТекСтрока.Право;
			НоваяСтрока.Объект = ТекСтрока.Объект;
			НоваяСтрока.Доступность = ТекСтрока.Доступность;
			НоваяСтрока.Изменено = Истина;
			ТекСтрока.Изменено = Ложь;
		КонецЕсли;              
	КонецЦикла; 
КонецПроцедуры

//Заполняет строки таблицы ТабДоступностиДокументов из таблицы доступности документов
&НаКлиенте
Процедура ПереписатьТабДоступностиДокументов()
	КоличествоСтрок = ТабДоступностиДокументов.Количество();
	
	Для Каждого ТекСтрока Из ТаблицаДоступностиОбъектов Цикл
		Если Не ТекСтрока.Изменено Тогда
			Продолжить;
		КонецЕсли;
		
		Если ТекСтрока.Инд < КоличествоСтрок Тогда
			ТабДоступностиДокументов[ТекСтрока.Инд].Пользователь = ТекСтрока.Пользователь;
			ТабДоступностиДокументов[ТекСтрока.Инд].Право = ТекСтрока.Право;
			ТабДоступностиДокументов[ТекСтрока.Инд].Объект = ТекСтрока.Объект;
			ТабДоступностиДокументов[ТекСтрока.Инд].Изменено = Истина;
			ТабДоступностиДокументов[ТекСтрока.Инд].Доступность = ТекСтрока.Доступность;
			Если ТипЗнч(ТекСтрока.Объект) = Тип("СправочникСсылка.Пользователи") Тогда
				ТабДоступностиДокументов[ТекСтрока.Инд].ВидОбъекта = "Пользователи";
			КонецЕсли;                                              
			ТекСтрока.Изменено = Ложь;
		Иначе
			НоваяСтрока = ТабДоступностиДокументов.Добавить();
			НоваяСтрока.Пользователь=ТекСтрока.Пользователь;
			НоваяСтрока.Право = ТекСтрока.Право;
			НоваяСтрока.Объект = ТекСтрока.Объект;
			НоваяСтрока.Изменено = Истина;
			НоваяСтрока.Доступность = ТекСтрока.Доступность;
			Если ТипЗнч(ТекСтрока.Объект) = Тип("СправочникСсылка.Пользователи") Тогда
				НоваяСтрока.ВидОбъекта = "Пользователи";
			КонецЕсли;
			ТекСтрока.Изменено = Ложь;
		КонецЕсли;
	КонецЦикла; 
КонецПроцедуры

//Запрос пользователя о сохранении изменений
&НаКлиенте
Процедура ЗапросСохраненияИзменений(Знач Оповещение)
	//При изменении выбранного объекта надо произвести запись внесенных изменений
	Если Модифицированность Тогда
		Результат = Неопределено;

		ПоказатьВопрос(Новый ОписаниеОповещения("ЗапросСохраненияИзмененийЗавершение", ЭтотОбъект, Новый Структура("Оповещение", Оповещение)), "Данные были изменены. Произвести запись?", РежимДиалогаВопрос.ДаНетОтмена);
        Возврат; 
	КонецЕсли; 
	ЗапросСохраненияИзмененийФрагмент(Оповещение);
КонецПроцедуры

&НаКлиенте
Процедура ЗапросСохраненияИзмененийЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
    
    Оповещение = ДополнительныеПараметры.Оповещение;
    
    
    Результат = РезультатВопроса;
    Если Результат = КодВозвратаДиалога.Да Тогда
        ПерезаписатьСтраницы();
        Если НЕ ЗаписатьПраваИНастройки() Тогда
            ВыполнитьОбработкуОповещения(Оповещение, Ложь);
            Возврат;
        КонецЕсли;
        ЗаполнитьТаблицуДоступностейОбъектов(ДеревоПрав.НайтиПоИдентификатору(Элементы.ДеревоПрав.ТекущаяСтрока));
        Модифицированность = Ложь;
    ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
        Модифицированность = Ложь;
    Иначе ВыполнитьОбработкуОповещения(Оповещение, Ложь);
        Возврат;
    КонецЕсли; 
    
    ЗапросСохраненияИзмененийФрагмент(Оповещение);

КонецПроцедуры

&НаКлиенте
Процедура ЗапросСохраненияИзмененийФрагмент(Знач Оповещение)
    
    ВыполнитьОбработкуОповещения(Оповещение, Истина);
    Возврат;

КонецПроцедуры

// Устанавливает значение по умолчанию для выбранной строки дерева прав
&НаКлиенте
Процедура УстановитьПравоНастройкуПоУмолчанию(ТекущаяСтрока)
	Если ТекущаяСтрока.Группа Тогда
		Для Каждого Строка Из ТекущаяСтрока.ПолучитьЭлементы() Цикл
			УстановитьПравоНастройкуПоУмолчанию(Строка);
		КонецЦикла;
	КонецЕсли;
	Если ТекущаяСтрока.ТолькоПросмотр ИЛИ ТекущаяСтрока.Группа Тогда
		Возврат;
	КонецЕсли;
	Если  (Не ТекущаяСтрока.Группа) И (Не ТекущаяСтрока.Значение = ТекущаяСтрока.ПоУмолчанию) Тогда
		Разрешено = (ТекущаяСтрока.ЭтоНастройка И ДостНастройки) ИЛИ (НЕ ТекущаяСтрока.ЭтоНастройка И ДостПрава);
		Если Разрешено Тогда
			ТекущаяСтрока.Значение = ТекущаяСтрока.ПоУмолчанию;
			Если ТекущаяСтрока.Тип = Тип("Булево") Тогда
				ТекущаяСтрока.ЗначениеБулево = ТекущаяСтрока.Значение;
			КонецЕсли;
			ТекущаяСтрока.Изменено = Истина;
			Модифицированность = Истина;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

// Установка настроек по умолчанию для выбранного объекта
&НаКлиенте
Процедура УстановитьЗначенияПоУмолчанию(ВеткаДерева)
	Для Каждого Стр Из ВеткаДерева.ПолучитьЭлементы() Цикл
		Если Стр.Группа Тогда
			УстановитьЗначенияПоУмолчанию(Стр);
		Иначе
			Если Стр.Значение <> Стр.ПоУмолчанию Тогда
				Стр.Значение = Стр.ПоУмолчанию;
				Если Стр.Тип = Тип("Булево") Тогда
					Стр.ЗначениеБулево = Стр.Значение;
				КонецЕсли;
				Стр.Изменено = Истина;
				Модифицированность = Истина;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Функция ПолучитьСтрокуДереваПравПоНастройке(ПравоНастройка, СтрокаДерева = Неопределено)
	Если СтрокаДерева = Неопределено Тогда
		СтрокаДерева = ДеревоПрав;
	КонецЕсли;
	
	Рез = Неопределено;
	
	Для Каждого ТекСтрока Из СтрокаДерева.ПолучитьЭлементы() Цикл
		Если ТекСтрока.ПравоНастройка = ПравоНастройка Тогда
			Рез = ТекСтрока.ПолучитьИдентификатор();
			Прервать;
		КонецЕсли;
		
		Рез = ПолучитьСтрокуДереваПравПоНастройке(ПравоНастройка, ТекСтрока);
	КонецЦикла;
	
	Возврат Рез;
КонецФункции
	
	
////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура УстановитьТекущееПравоПоУмолчанию(Команда)
	ТекущаяСтрока = Элементы.ДеревоПрав.ТекущиеДанные;
	Если ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Если ТекущаяСтрока.Группа Тогда
		Ответ = Неопределено;

		ПоказатьВопрос(Новый ОписаниеОповещения("УстановитьТекущееПравоПоУмолчаниюЗавершение", ЭтотОбъект, Новый Структура("ТекущаяСтрока", ТекущаяСтрока)), "Установить значения по умолчанию в группе " + ТекущаяСтрока.ПравоНастройка, РежимДиалогаВопрос.ДаНет);
        Возврат;
	КонецЕсли;
	
	УстановитьТекущееПравоПоУмолчаниюФрагмент(ТекущаяСтрока);
КонецПроцедуры

&НаКлиенте
Процедура УстановитьТекущееПравоПоУмолчаниюЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
    
    ТекущаяСтрока = ДополнительныеПараметры.ТекущаяСтрока;
    
    
    Ответ = РезультатВопроса;
    Если Ответ = КодВозвратаДиалога.Нет Тогда
        Возврат;
    КонецЕсли;
    
    УстановитьТекущееПравоПоУмолчаниюФрагмент(ТекущаяСтрока);

КонецПроцедуры

&НаКлиенте
Процедура УстановитьТекущееПравоПоУмолчаниюФрагмент(Знач ТекущаяСтрока)
    
    УстановитьПравоНастройкуПоУмолчанию(ТекущаяСтрока);
    ПроставитьПризнакЗначениеРавноДефолтному(ДеревоПрав);

КонецПроцедуры

&НаКлиенте
Процедура СохранитьИЗакрыть(Команда)
	ПерезаписатьСтраницы();
	Рез = ЗаписатьПраваИНастройки();
	//rarus sergei 17.08.2016 mantis 7110 ++
	Если ИзмененаНастройкаСрокУстраненияНедостатков Тогда
		МассивИзделий = Новый Массив;
		ЗаполнитьМассивИзделий(МассивИзделий);
		Если МассивИзделий.Количество() <> 0 Тогда
			Режим = РежимДиалогаВопрос.ДаНет;
			Ответ = Вопрос(НСтр("ru = 'Изменена настройка ""Cрок устранения недостатков"". Пересчитать крайнюю дату устранения недостатков в изделиях?'"), Режим, 0);
			Если Ответ = КодВозвратаДиалога.Да Тогда
				ПересчитатьКрайнююДатуУстраненияНедостатков(МассивИзделий)
			КонецЕсли;
		КонецЕсли; 
	КонецЕсли;
	ИзмененаНастройкаСрокУстраненияНедостатков = Ложь;
	//rarus sergei 17.08.2016 mantis 7110 --
	Если Рез Тогда
		Модифицированность = Ложь;
		Закрыть();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура Сохранить(Команда)
	ПерезаписатьСтраницы();
	Если НЕ ЗаписатьПраваИНастройки() Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьТаблицуДоступностейОбъектов(ДеревоПрав.НайтиПоИдентификатору(Элементы.ДеревоПрав.ТекущаяСтрока));
	Модифицированность = Ложь;
	//rarus sergei 17.08.2016 mantis 7110 ++
	Если ИзмененаНастройкаСрокУстраненияНедостатков Тогда
		МассивИзделий = Новый Массив;
		ЗаполнитьМассивИзделий(МассивИзделий);
		Если МассивИзделий.Количество() <> 0 Тогда
			
			// rarus tenkam 10.04.2019 mantis 14195 +++
			//Режим = РежимДиалогаВопрос.ДаНет;
			//Ответ = Вопрос(НСтр("ru = 'Изменена настройка ""Cрок устранения недостатков"". Пересчитать крайнюю дату устранения недостатков в изделиях?'"), Режим, 0);
			//Если Ответ = КодВозвратаДиалога.Да Тогда
			//	ПересчитатьКрайнююДатуУстраненияНедостатков(МассивИзделий)
			//КонецЕсли; 			
			
			ДополнительныеПараметры = Новый Структура;
			ДополнительныеПараметры.Вставить("МассивИзделий",МассивИзделий);
			Оповещение = Новый ОписаниеОповещения("ПересчитатьСрокУстраненияНедостатковЗавершение", ЭтотОбъект, ДополнительныеПараметры);
			ПоказатьВопрос(Оповещение, НСтр("ru = 'Изменена настройка ""Cрок устранения недостатков"". Пересчитать крайнюю дату устранения недостатков в изделиях?'"), РежимДиалогаВопрос.ДаНет, 0);
            // rarus tenkam 10.04.2019 mantis 14195 ---
			
		КонецЕсли; 
	КонецЕсли;
	СохранитьФрагмент2();
КонецПроцедуры

// rarus tenkam 10.04.2019 mantis 14195 +++
&НаКлиенте
Процедура ПересчитатьСрокУстраненияНедостатковЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ПересчитатьКрайнююДатуУстраненияНедостатков(ДополнительныеПараметры.МассивИзделий);
		ИзмененаНастройкаСрокУстраненияНедостатков = Ложь;
	КонецЕсли;          	

КонецПроцедуры
// rarus tenkam 10.04.2019 mantis 14195 ---

&НаКлиенте
Процедура СохранитьФрагмент2()
	
	СохранитьФрагмент1();

КонецПроцедуры

&НаКлиенте
Процедура СохранитьФрагмент1()
	
	СохранитьФрагмент();

КонецПроцедуры

&НаКлиенте
Процедура СохранитьФрагмент()
	
	ИзмененаНастройкаСрокУстраненияНедостатков = Ложь;
	//rarus sergei 17.08.2016 mantis 7110 --
	
	// rarus tenkam 05.06.2018 mantis 13137 +++
	Если НужноОбновлятьДерево Тогда
		ОбъектНастройкиПравПриИзменении(Неопределено);
		НужноОбновлятьДерево = Ложь;
	КонецЕсли;		
	// rarus tenkam 05.06.2018 mantis 13137 ---

КонецПроцедуры
//rarus sergei 17.08.2016 mantis 7110 ++
&НаСервере
Процедура ПересчитатьКрайнююДатуУстраненияНедостатков(МассивИзделий)
	КоличествоИзмененныхИзделий = 0;
	Для каждого элемент Из МассивИзделий Цикл
		ИзделиеОбъект = элемент.ПолучитьОбъект();
		КрайняяДата=Справочники.Scan_Изделия.ПересчитатьКрайнююДатуУстраненияНедостатков(Элемент);
		ИзделиеОбъект.КрайняяДатаУстраненияНедостатков =  КрайняяДата;
		ИзделиеОбъект.Записать();
		КоличествоИзмененныхИзделий = КоличествоИзмененныхИзделий+1;
	КонецЦикла; 
	//rarus FominskiyAS 24.04.2019  mantis 14191 +++
	//Сообщить("Пересчитана крайняя дата устранения недостатков в "+КоличествоИзмененныхИзделий +" изделиях");
	Сообщить(НСтр("ru = 'Пересчитана крайняя дата устранения недостатков в '; en = 'Date recalculated '")+КоличествоИзмененныхИзделий +НСтр("ru = ' продуктах'; en = ' '"));
	//rarus FominskiyAS 24.04.2019  mantis 14191 ---  
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьМассивИзделий(Массив)
	// rarus tenkam 13.08.2019 mantis 14427 +++
	//Запрос = Новый Запрос;
	//Запрос.Текст = "ВЫБРАТЬ
	//               |	Scan_Продукты.Изделие
	//               |ИЗ
	//               |	Справочник.Scan_Продукты КАК Scan_Продукты
	//               |ГДЕ
	//               |	Scan_Продукты.ДатаПродажиИзделия <> &ПустаяДата
	//               |	И Scan_Продукты.ПродуктВАрхиве = &ПустаяДата";
	//Запрос.УстановитьПараметр("ПустаяДата",'00010101');
	//СписокИзделий = Запрос.Выполнить().Выгрузить();
	//Для каждого строка Из СписокИзделий Цикл
	//	Массив.Добавить(строка.Изделие);   	
	//КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Scan_КлючевыеДатыПроцессовСрезПоследних.Объект КАК ЗаказНаЗавод,
		|	Scan_КлючевыеДатыПроцессовСрезПоследних.Значение КАК ДатаПродажиИзделия
		|ПОМЕСТИТЬ ЗаказыСДатойПродажи
		|ИЗ
		|	РегистрСведений.Scan_КлючевыеДатыПроцессов.СрезПоследних(
		|			,
		|			ОбъектКлючевойДаты = ЗНАЧЕНИЕ(Перечисление.Scan_ОбъектыКлючевыхДат.ЗаказНаЗавод)
		|				И ВидКлючевойДаты = ЗНАЧЕНИЕ(Перечисление.Scan_КлючевыеДаты.ДатаПродажиИзделия)) КАК Scan_КлючевыеДатыПроцессовСрезПоследних
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Scan_Изделия.Ссылка КАК Изделие,
		|	Scan_Изделия.ЗаказНаЗавод КАК ЗаказНаЗавод,
		|	ЕСТЬNULL(Scan_КлючевыеДатыПроцессовСрезПоследних.Значение, ДАТАВРЕМЯ(1, 1, 1)) КАК ПродуктВАрхиве
		|ПОМЕСТИТЬ ИзделияПродуктВАрхиве
		|ИЗ
		|	Справочник.Scan_Изделия КАК Scan_Изделия
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Scan_КлючевыеДатыПроцессов.СрезПоследних(
		|				,
		|				ОбъектКлючевойДаты = ЗНАЧЕНИЕ(Перечисление.Scan_ОбъектыКлючевыхДат.Изделие)
		|					И ВидКлючевойДаты = ЗНАЧЕНИЕ(Перечисление.Scan_КлючевыеДаты.ПродуктВАрхиве)) КАК Scan_КлючевыеДатыПроцессовСрезПоследних
		|		ПО Scan_Изделия.Ссылка = Scan_КлючевыеДатыПроцессовСрезПоследних.Объект
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ИзделияПродуктВАрхиве.Изделие КАК Изделие,
		|	ИзделияПродуктВАрхиве.ЗаказНаЗавод КАК ЗаказНаЗавод,
		|	ЕСТЬNULL(ЗаказыСДатойПродажи.ДатаПродажиИзделия, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаПродажиИзделия
		|ПОМЕСТИТЬ ИзделияНеВАрхиве
		|ИЗ
		|	ИзделияПродуктВАрхиве КАК ИзделияПродуктВАрхиве
		|		ЛЕВОЕ СОЕДИНЕНИЕ ЗаказыСДатойПродажи КАК ЗаказыСДатойПродажи
		|		ПО ИзделияПродуктВАрхиве.ЗаказНаЗавод = ЗаказыСДатойПродажи.ЗаказНаЗавод
		|ГДЕ
		|	ИзделияПродуктВАрхиве.ПродуктВАрхиве = ДАТАВРЕМЯ(1, 1, 1)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ИзделияНеВАрхиве.Изделие КАК Изделие
		|ИЗ
		|	ИзделияНеВАрхиве КАК ИзделияНеВАрхиве
		|ГДЕ
		|	ИзделияНеВАрхиве.ДатаПродажиИзделия <> ДАТАВРЕМЯ(1, 1, 1)";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Массив.Добавить(ВыборкаДетальныеЗаписи.Изделие); 
	КонецЦикла;

	// rarus tenkam 13.08.2019 mantis 14427 ---
Конецпроцедуры
//rarus sergei 17.08.2016 mantis 7110 --

&НаКлиенте
Процедура СвернутьДерево(Команда)
	СвернутьСтрокиДерева(ДеревоПрав);
КонецПроцедуры

&НаКлиенте
Процедура РазвернутьДерево(Команда)
	Для Каждого ТекСтрока Из ДеревоПрав.ПолучитьЭлементы() Цикл
		Элементы.ДеревоПрав.Развернуть(ТекСтрока.ПолучитьИдентификатор(), Истина);
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ПеречитатьПрава(Команда)
	ОбъектНастройкиПравПриИзменении(Неопределено);
	Модифицированность = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура УстановитьПраваПоУмолчанию(Команда)
	Если Scan_ОбщегоНазначения.Scan_ЗначениеНеЗаполнено(ОбъектНастройкиПрав) Тогда
		//И ТипОбъектаНастройки <> ПредопределенноеЗначение("Перечисление.уатНазначениеПравИНастроек.Компания") Тогда
		ПоказатьПредупреждение(Неопределено, "Поле ""Объект"" не заполнено!");
		Возврат;
	КонецЕсли;
	ПоказатьВопрос(Новый ОписаниеОповещения("УстановитьПраваПоУмолчаниюЗавершение", ЭтотОбъект), "Пользователю будут установлены права по умолчанию !
		|Продолжить ?", РежимДиалогаВопрос.ДаНет, ,КодВозвратаДиалога.Нет);
КонецПроцедуры

&НаКлиенте
Процедура УстановитьПраваПоУмолчаниюЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
    
    Если РезультатВопроса <> КодВозвратаДиалога.Да Тогда Возврат;
    КонецЕсли;
    ФормированиеДереваПравОбъекта();
    УстановитьЗначенияПоУмолчанию(ДеревоПрав);

КонецПроцедуры

&НаКлиенте
Процедура СкопироватьПрофиль(Команда)
	//Если ТипОбъектаНастройки = ПредопределенноеЗначение("Перечисление.уатНазначениеПравИНастроек.Компания") Тогда
	//	ПоказатьПредупреждение(Неопределено, "Для объекта типа ""Компания"" набор прав скопировать нельзя.");
	//	Возврат;
	//КонецЕсли;
	Если НЕ ЗначениеЗаполнено(ОбъектНастройкиПрав) Тогда
		ПоказатьПредупреждение(Неопределено, "Поле ""Объект"" не заполнено!");
		Возврат;
	КонецЕсли;
	Если Не ДостПрава Тогда
		ПоказатьПредупреждение(Неопределено, "Недостаточно прав!");
		Возврат;
	КонецЕсли;
	
	Если Модифицированность Тогда
		Ответ = Неопределено;

		ПоказатьВопрос(Новый ОписаниеОповещения("СкопироватьПрофильЗавершение", ЭтотОбъект), "Данные настроек изменены!
		|Сохранить изменения?", РежимДиалогаВопрос.ДаНет);
        Возврат;		
	КонецЕсли;
	
	СкопироватьПрофильФрагмент();
КонецПроцедуры

&НаКлиенте
Процедура СкопироватьПрофильЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
    
    Ответ = РезультатВопроса;
    Если Ответ = КодВозвратаДиалога.Да Тогда
        ПерезаписатьСтраницы();
        ЗаписатьПраваИНастройки();
        ЗаполнитьТаблицуДоступностейОбъектов(ДеревоПрав.НайтиПоИдентификатору(Элементы.ДеревоПрав.ТекущаяСтрока));
        Модифицированность = Ложь;
    Иначе
        Возврат;
    КонецЕсли;		
    
    СкопироватьПрофильФрагмент();

КонецПроцедуры

&НаКлиенте
Процедура СкопироватьПрофильФрагмент()
    
    Перем ПараметрыФормы;
    
    Если НЕ Модифицированность Тогда
        ПараметрыФормы = Новый Структура("ТипОбъекта, ОбъектИсточник", ТипОбъектаНастройки, ОбъектНастройкиПрав);
        ОткрытьФорму("Обработка.Scan_УстановкаПравИНастроек.Форма.ФормаКопированиеПрав", ПараметрыФормы, ЭтаФорма,,,, Неопределено, РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
        
    КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИзФайла(Команда)
	ПоказатьВопрос(Новый ОписаниеОповещения("ЗагрузитьИзФайлаЗавершение", ЭтотОбъект), "Все текущие права будут заменены на загружаемые!
		|Продолжить ?",РежимДиалогаВопрос.ДаНет,,КодВозвратаДиалога.Нет);
КонецПроцедуры

//rarus pechek 03.03.2020 mantis 15729 +++

//было
//&НаКлиенте
//Процедура ЗагрузитьИзФайлаЗавершение(Знач Оповещение, РезультатВопроса, ДополнительныеПараметры) Экспорт
//    
//    Если РезультатВопроса<>КодВозвратаДиалога.Да Тогда ВыполнитьОбработкуОповещения(Оповещение);
// Возврат;
//    КонецЕсли;
//    ЗагрузитьНастройкиИзФайла(Новый ОписаниеОповещения("ЗагрузитьИзФайлаЗавершениеЗавершение", ЭтотОбъект, Новый Структура("Оповещение", Оповещение)));

//КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИзФайлаЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
    
    Если РезультатВопроса<>КодВозвратаДиалога.Да Тогда ВыполнитьОбработкуОповещения(ДополнительныеПараметры);
 Возврат;
    КонецЕсли;
    ЗагрузитьНастройкиИзФайла(Новый ОписаниеОповещения("ЗагрузитьИзФайлаЗавершениеЗавершение", ЭтотОбъект, Новый Структура("Оповещение", ДополнительныеПараметры)));

КонецПроцедуры

//rarus pechek 03.03.2020 mantis 15729 ---


&НаКлиенте
Процедура ЗагрузитьИзФайлаЗавершениеЗавершение(Результат, ДополнительныеПараметры1) Экспорт
    
    Оповещение = ДополнительныеПараметры1.Оповещение;
    
    
    ТипОбъектаНастройкиПриИзменении(Неопределено);
    
	Если Не Оповещение = Неопределено Тогда 
		ВыполнитьОбработкуОповещения(Оповещение);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СохранитьВФайл(Команда)
	СохранитьНастройкиВФайл();
КонецПроцедуры

&НаКлиенте
Процедура ПраваДоступаОчистить(Команда)
	ТаблицаДоступностиОбъектов.Очистить();
	УдалитьТекущиеДоступности();
КонецПроцедуры

&НаКлиенте
Процедура ПраваДоступаЗаполнить(Команда)
	Стр = ДеревоПрав.НайтиПоИдентификатору(Элементы.ДеревоПрав.ТекущаяСтрока);
	Если Стр = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПраваДоступаЗаполнитьВсе(Стр.ПравоНастройка, Стр.Значение);
КонецПроцедуры

&НаСервере
Процедура ПраваДоступаЗаполнитьВсе(ПравоНастройка, Значение)
	ТаблицаДоступностиОбъектов_ = РеквизитФормыВЗначение("ТаблицаДоступностиОбъектов");
	
	Если ВидОбъектаНастройкиПрав = "Справочники" Тогда 
		ВидОбъекта = Сред(ПравоНастройка, 15);
		
	ИначеЕсли Значение = Перечисления.Scan_ВидыПравДляДокументов.РедактированиеПоПользователям Тогда
		ВидОбъекта = "Пользователи";
		
	КонецЕсли;
	
	Текст = "ВЫБРАТЬ
	|	ВидОбъекта.Ссылка КАК Объект
	|ИЗ
	|	Справочник." + ВидОбъекта + " КАК ВидОбъекта
	|ГДЕ
	|	(НЕ ВидОбъекта.Ссылка В (&СписокОбъектов))";
	Если ВидОбъектаНастройкиПрав = "Справочники" Тогда 
		Текст = Текст + "
		|	И ВидОбъекта.ЭтоГруппа = Истина";
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = Текст;
	Запрос.УстановитьПараметр("СписокОбъектов", ТаблицаДоступностиОбъектов_.ВыгрузитьКолонку("Объект"));
	Результат = Запрос.Выполнить().Выбрать();
	
	Если Булево(Результат.Количество()) Тогда
		Модифицированность = Истина;
	КонецЕсли;
	Если ВидОбъектаНастройкиПрав = "Справочники" Тогда
		КоличествоВБазовойТаблице = ТабДоступностиСправочников.Количество();
		
	ИначеЕсли ВидОбъектаНастройкиПрав = "Документы" Тогда 
		КоличествоВБазовойТаблице = ТабДоступностиДокументов.Количество();
		
	КонецЕсли;
	Пока Результат.Следующий() Цикл
		НоваяСтрока = ТаблицаДоступностиОбъектов_.Добавить();
		НоваяСтрока.Объект = Результат.Объект;
		НоваяСтрока.Пользователь = ОбъектНастройкиПрав;
		НоваяСтрока.Право = ПравоНастройка;
		НоваяСтрока.Доступность = Истина;
		НоваяСтрока.Инд = КоличествоВБазовойТаблице;
		НоваяСтрока.Изменено = Истина;
	КонецЦикла;
	
	ЗначениеВРеквизитФормы(ТаблицаДоступностиОбъектов_, "ТаблицаДоступностиОбъектов");
КонецПроцедуры

&НаКлиенте
Процедура ПраваДоступаПодбор(Команда)
	ДобавитьПользователяИлиГруппу("ЗаполнитьПоВыбранным", "Подбор");
КонецПроцедуры

&НаКлиенте
Процедура ПраваДоступаПодборПоГруппам(Команда)
	ДобавитьПользователяИлиГруппу("ЗаполнитьВыбранныеСВложенными", "Подбор по группам (включение вложенных)");
КонецПроцедуры

&НаКлиенте
Процедура ПраваДоступаОтметитьВсе(Команда)
	УстановкаФлаговДоступностиОбъектов(Истина);
КонецПроцедуры

&НаКлиенте
Процедура ПраваДоступаСнятьВсе(Команда)
	УстановкаФлаговДоступностиОбъектов(Ложь);
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ ЭЛЕМЕНТОВ УПРАВЛЕНИЯ ФОРМЫ

&НаКлиенте
Процедура ТипОбъектаНастройкиПриИзменении(Элемент)
	// Произведем типизацию и определим доступность объекта
	ДоступностьОбъекта = Истина;
	//Если ТипОбъектаНастройки = ПредопределенноеЗначение("Перечисление.уатНазначениеПравИНастроек.Компания") Тогда
	//	ДоступностьОбъекта = Ложь;
	//	Пустышка = Неопределено;
	//	
	//ИначеЕсли ТипОбъектаНастройки = ПредопределенноеЗначение("Перечисление.уатНазначениеПравИНастроек.Организация") Тогда
	Если ТипОбъектаНастройки = ПредопределенноеЗначение("Перечисление.Scan_НазначениеПравИНастроек.Организация") Тогда
		ОрганизацияПоУмолчанию = Scan_ОбщегоНазначенияПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(ТекущийПользователь, "ОсновнаяОрганизация");
		Если ЗначениеЗаполнено(ОрганизацияПоУмолчанию) Тогда
			Пустышка = ОрганизацияПоУмолчанию;
		Иначе
			Пустышка = ПредопределенноеЗначение("Справочник.Организации.ПустаяСсылка");
		КонецЕсли;
		
	ИначеЕсли ТипОбъектаНастройки = ПредопределенноеЗначение("Перечисление.Scan_НазначениеПравИНастроек.Подразделение") Тогда
		//Если уатРаботаСМетаданными.ЕстьСправочник("ПодразделенияОрганизаций") Тогда
			Пустышка = ПредопределенноеЗначение("Справочник.Scan_Подразделения.ПустаяСсылка");
		//Иначе
		//	Пустышка = ПредопределенноеЗначение("Справочник.СтруктураПредприятия.ПустаяСсылка");
		//КонецЕсли;
		
	ИначеЕсли ТипОбъектаНастройки = ПредопределенноеЗначение("Перечисление.Scan_НазначениеПравИНастроек.Пользователь") Тогда
		Если ЗначениеЗаполнено(ТекущийПользователь) Тогда
			Пустышка = ТекущийПользователь;
		Иначе
			Пустышка = ПредопределенноеЗначение("Справочник.Пользователи.ПустаяСсылка");
		КонецЕсли;
		
	Иначе
		ТипОбъектаНастройки = ПредопределенноеЗначение("Перечисление.Scan_НазначениеПравИНастроек.Пользователь");
		Пустышка = ПредопределенноеЗначение("Справочник.Пользователи.ПустаяСсылка");
		
	КонецЕсли;
	
	Элементы.ОбъектНастройкиПрав.Доступность = ДоступностьОбъекта;
	
	// Если тип значения поменялся, произведем типизацию присвоением пустышки
	Если ТипЗнч(Пустышка) <> ТипЗнч(ОбъектНастройкиПрав) Тогда
		ОбъектНастройкиПрав = Пустышка;
	КонецЕсли;
	
	//Формирование дерева прав для заданного объекта
	ОбъектНастройкиПравПриИзменении(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ОбъектНастройкиПравПриИзменении(Элемент)
	ПраваИНастройки = Неопределено;
	Если Элементы.ДеревоПрав.ТекущиеДанные <> Неопределено Тогда
		ПраваИНастройки = Элементы.ДеревоПрав.ТекущиеДанные.ПравоНастройка;
	КонецЕсли;
	ФормированиеДереваПравОбъекта();
	
	// rarus vikhle 11.02.2022 m 17268 +++
	// Редактирование прав других пользователей доступно только в пределах отдела (касается НЕ администраторов)
	Если Не ПользовательАдминистратор 
		И ТипОбъектаНастройки = ПредопределенноеЗначение("Перечисление.Scan_НазначениеПравИНастроек.Пользователь") 
		И ОбъектНастройкиПрав <> ТекущийПользователь Тогда
		Если НЕ ПользовательВходитВГруппуТекущего(ТекущийПользователь, ОбъектНастройкиПрав) Тогда
			Элементы.ФормаУстановитьПраваПоУмолчанию.Доступность = Ложь;
			Элементы.ФормаСкопироватьПрофиль.Доступность = Ложь;
			Элементы.ДеревоПравКонтекстноеМенюУстановитьТекущееПравоПоУмолчанию.Видимость = Ложь;
			
			Элементы.ДеревоПравЗначение.ТолькоПросмотр = Истина;
			Элементы.ДеревоПравЗначениеБулево.ТолькоПросмотр = Истина;
		Иначе
			Элементы.ФормаУстановитьПраваПоУмолчанию.Доступность = Истина;
			Элементы.ФормаСкопироватьПрофиль.Доступность = Истина;
			Элементы.ДеревоПравКонтекстноеМенюУстановитьТекущееПравоПоУмолчанию.Видимость = Истина;
			
			Элементы.ДеревоПравЗначение.ТолькоПросмотр = Ложь;
			Элементы.ДеревоПравЗначениеБулево.ТолькоПросмотр = Ложь;	
			
		КонецЕсли;	
		
	ИначеЕсли Не ПользовательАдминистратор И ДостПрава Тогда	
		
		Элементы.ФормаУстановитьПраваПоУмолчанию.Доступность = Истина;
		Элементы.ФормаСкопироватьПрофиль.Доступность = Истина;
		Элементы.ДеревоПравКонтекстноеМенюУстановитьТекущееПравоПоУмолчанию.Видимость = Истина;
		
		Элементы.ДеревоПравЗначение.ТолькоПросмотр = Ложь;
		Элементы.ДеревоПравЗначениеБулево.ТолькоПросмотр = Ложь;
		
	КонецЕсли; 
	// rarus vikhle 11.02.2022 m 17268 ---	
	
	//НайденнаяСтрокаЗначение = НайтиОтмеченноеЗначениеВДеревеЗначений(ПредопределенноеЗначение("ПланВидовХарактеристик.уатПраваИНастройки.ВестиСкладскойУчетУАТ"));
	//Если НайденнаяСтрокаЗначение <> Неопределено Тогда
	//	мВестиСкладскойУчетУАТ = НайденнаяСтрокаЗначение;
	//Иначе
	//	мВестиСкладскойУчетУАТ = Ложь;
	//КонецЕсли;
	
	//НайденнаяСтрокаЗначение = НайтиОтмеченноеЗначениеВДеревеЗначений(ПредопределенноеЗначение("ПланВидовХарактеристик.уатПраваИНастройки.ВестиУчетЗатрат"));
	//Если НайденнаяСтрокаЗначение <> Неопределено Тогда
	//	мВестиУчетЗатрат = НайденнаяСтрокаЗначение;
	//Иначе
	//	мВестиУчетЗатрат = Ложь;
	//КонецЕсли;
	
	ИспользуетсяШаблон = Ложь;
	
	// доступность кнопки "Права по умолчанию"
	Элементы.ДеревоПравКонтекстноеМенюУстановитьТекущееПравоПоУмолчанию.Доступность =
		(ЗначениеЗаполнено(ОбъектНастройкиПрав) И ТипОбъектаНастройки = ПредопределенноеЗначение("Перечисление.Scan_НазначениеПравИНастроек.Пользователь"));
	
	// доступность кнопки "СкопироватьПрофиль"
	Элементы.ФормаСкопироватьПрофиль.Доступность = ЗначениеЗаполнено(ОбъектНастройкиПрав);
	
	//Для пользователей, у которых установлен шаблон прав - ничего не редактируем
	//Если уатОбщегоНазначения.уатЗначениеНеЗаполнено(ОбъектНастройкиПрав) И НЕ ТипОбъектаНастройки = ПредопределенноеЗначение("Перечисление.уатНазначениеПравИНастроек.Компания") Тогда
	//	Элементы.ДеревоПрав.ТолькоПросмотр = Истина;
	//Иначе
		Если ТипЗнч(ОбъектНастройкиПрав) = Тип("СправочникСсылка.Пользователи") Тогда
			Элементы.ДеревоПрав.ТолькоПросмотр = Ложь;
		Иначе
			Элементы.ДеревоПрав.ТолькоПросмотр = Ложь;
		КонецЕсли;
	//КонецЕсли; 
	
	// Обработка текущей страницы при несовпадении ее с основной 
	Если ПраваИНастройки = Неопределено Тогда
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаДеревоПравИНастроек;
	Иначе
		//ТекущаяСтрока = ДеревоПрав.Строки.Найти(ПраваИНастройки, "ПравоНастройка", Истина);
		ТекущаяСтрока = ПолучитьСтрокуДереваПравПоНастройке(ПраваИНастройки);
		Если ТекущаяСтрока <> Неопределено Тогда
			Элементы.ДеревоПрав.ТекущаяСтрока = ТекущаяСтрока;
		КонецЕсли;
	КонецЕсли;
	Если Элементы.НастройкаДополнительныхПравСправочниковИДокументов.Видимость Тогда
		Если ВидОбъектаНастройкиПрав = "Справочники" Тогда
			ЗаполнитьТаблицуДоступностейОбъектов(ТекущаяСтрока);
		ИначеЕсли ВидОбъектаНастройкиПрав = "Документы" Тогда
			ЗаполнитьТаблицуДоступностейОбъектов(ТекущаяСтрока);
		Иначе
			Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаДеревоПравИНастроек;
		КонецЕсли;
	Иначе
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаДеревоПравИНастроек;
	КонецЕсли;
	
	//заголовок формы и информационная надпись
	Если Элементы.ДеревоПрав.ТолькоПросмотр Тогда
		//Заголовок = "Установка прав и настроек УАТ (только чтение)";
		Заголовок = "Установка прав и настроек (только чтение)";
		Если Scan_ОбщегоНазначения.Scan_ЗначениеНеЗаполнено(ОбъектНастройкиПрав) Тогда // не заполнен объект настройки прав
			Элементы.ГруппаИнформационнаяНадпись.ТекущаяСтраница = Элементы.ГруппаИнформационнаяНадпись2;
		КонецЕсли;
	Иначе
		//Заголовок = "Установка прав и настроек УАТ";
		Заголовок = "Установка прав и настроек";		
		Если Не ДостПрава Тогда // доступно только редактирование своих настроек
			Элементы.ГруппаИнформационнаяНадпись.ТекущаяСтраница = Элементы.ГруппаИнформационнаяНадпись3;
		Иначе
			Элементы.ГруппаИнформационнаяНадпись.ТекущаяСтраница = Элементы.ГруппаИнформационнаяНадпись1;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОтображатьПраваПриИзменении(Элемент)
	//При изменении фильтра прав надо произвести запись внесенных изменений
	ЗапросСохраненияИзменений(Новый ОписаниеОповещения("ОтображатьПраваПриИзмененииЗавершение", ЭтотОбъект));
КонецПроцедуры

&НаКлиенте
Процедура ОтображатьПраваПриИзмененииЗавершение(Результат, ДополнительныеПараметры) Экспорт
    
    Если Результат = Ложь Тогда
        ОтображатьПрава = НЕ ОтображатьПрава;
        Возврат;
    КонецЕсли;
    
    ОбъектНастройкиПравПриИзменении(Неопределено);

КонецПроцедуры

&НаКлиенте
Процедура ОтображатьНастройкиПриИзменении(Элемент)
	//При изменении фильтра прав надо произвести запись внесенных изменений
	ЗапросСохраненияИзменений(Новый ОписаниеОповещения("ОтображатьНастройкиПриИзмененииЗавершение", ЭтотОбъект));
КонецПроцедуры

&НаКлиенте
Процедура ОтображатьНастройкиПриИзмененииЗавершение(Результат, ДополнительныеПараметры) Экспорт
    
    Если Результат = Ложь Тогда
        ОтображатьПрава = НЕ ОтображатьПрава;
        Возврат;
    КонецЕсли;
    
    ОбъектНастройкиПравПриИзменении(Неопределено);

КонецПроцедуры

&НаКлиенте
Процедура ДеревоПравЗначениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ТекСтрокаДерева = Элементы.ДеревоПрав.ТекущиеДанные;
	СтароеЗначениеПравНастроек = ТекСтрокаДерева.Значение;
	
	//Если ТекСтрокаДерева.ПравоНастройка.Ссылка = ПланыВидовХарактеристик.уатПраваИНастройки.ФорматВыводаКоличества
	//ИЛИ ТекСтрокаДерева.ПравоНастройка.Ссылка = ПланыВидовХарактеристик.уатПраваИНастройки.ФорматВыводаСуммы Тогда
	//	КонструкторФорматнойСтроки = Новый КонструкторФорматнойСтроки(Элемент.Значение);
	//	КонструкторФорматнойСтроки.ОткрытьМодально();
	//	Если Элемент.Значение <> КонструкторФорматнойСтроки.Текст Тогда
	//		Элемент.Значение			= КонструкторФорматнойСтроки.Текст;
	//		ТекСтрокаДерева.Изменено	= Истина;
	//		ЭтаФорма.Модифицированность	= Истина;
	//	КонецЕсли;
	//КонецЕсли;
	
	
	////Если редактируется настройка наименования продукта, отработаем сами
	
	
	Если ТекСтрокаДерева.ПравоНастройка = ПредопределенноеЗначение("ПланВидовХарактеристик.Scan_ПраваИНастройки.НастройкаВыводаНаименованияИзделия") Тогда
		СтандартнаяОбработка = Ложь;
		////ФормаПравилПрефиксации=ПолучитьФорму("ФормаПравилПрефиксации",ЭтаФорма);
		////ФормаПравилПрефиксации.НачальноеЗначениеВыбора = ТекСтрокаДерева;
		////ЗначениеНастройкиНаименования = ФормаПравилПрефиксации.ОткрытьМодально();
		////ОбработкаНастроекНаименования = ПолучитьСсылкуНаОбработкуНастроекНаименованияПродукта();
		//ФормаНастроекНаименования = ПолучитьФорму("Обработка.Scan_НастройкаНаименованияПродукта.Форма");
		//ЗначениеНастройкиНаименования = ФормаНастроекНаименования.ОткрытьМодально();
		//Если ЗначениеНастройкиНаименования <> Неопределено Тогда
		//    ТекСтрокаДерева.Значение = ЗначениеНастройкиНаименования.НастройкаНаименования;
		//    ТекСтрокаДерева.Изменено	= Истина;
		//    ЭтаФорма.Модифицированность	= Истина;       
		//КонецЕсли; 
		
		
		// Выбор настройки наименования
		Обработчик = Новый ОписаниеОповещения("НастроитьНаименование", ЭтотОбъект);
		Режим = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
		Парам = Новый Структура("СтараяНастройка", ТекСтрокаДерева.Значение);
		
		ОткрытьФорму("Обработка.Scan_НастройкаНаименованияИзделия.Форма",Парам,,,,, Обработчик, Режим);
	//rarus tenkam 09.02.2017 mantis 8303 +++
	ИначеЕсли ТекСтрокаДерева.ПравоНастройка = ПредопределенноеЗначение("ПланВидовХарактеристик.Scan_ПраваИНастройки.АдресЭлПочтыДляОтправкиПисемКонтактнымЛицам") Тогда
		СтандартнаяОбработка = Ложь;
		ЗначениеОтбора = Новый Структура("Тип", ПредопределенноеЗначение("Перечисление.ТипыКонтактнойИнформации.АдресЭлектроннойПочты"));
		ЗначениеОтбора.Вставить("Родитель", ПредопределенноеЗначение("Справочник.ВидыКонтактнойИнформации.СправочникScan_КонтактныеЛица"));
		ПараметрыФормы = Новый Структура("Отбор", ЗначениеОтбора);
		Результат = ОткрытьФорму("Справочник.ВидыКонтактнойИнформации.ФормаВыбора", ПараметрыФормы, Элемент); 
	//rarus tenkam 09.02.2017 mantis 8303 ---
	//rarus tenkam 28.04.2017 mantis 9444 +++
	ИначеЕсли ТекСтрокаДерева.ПравоНастройка = ПредопределенноеЗначение("ПланВидовХарактеристик.Scan_ПраваИНастройки.ФорматПрикрепленногоФайлаПриАвтоматическойОтправке") Тогда
		СтандартнаяОбработка = Ложь;
		ПараметрыФормы = Новый Структура("ЭтоНастройка", Истина);
		МассивФорматов = Scan_ВспомогательныеФункцииСервер.ПреобразоватьСтрокуВФорматыСохранения(ТекСтрокаДерева.Значение);
		Если МассивФорматов.Количество() <> 0 Тогда	
			НастройкиФорматов = Новый Структура("УпаковатьВАрхив",Ложь);
			НастройкиФорматов.Вставить("ФорматыСохранения",МассивФорматов);
			НастройкиФорматов.Вставить("ПереводитьИменаФайловВТранслит", Ложь);		// rarus tenkam 16.05.2019 mantis 14424 +
			ПараметрыФормы.Вставить("НастройкиФормата", НастройкиФорматов);
		КонецЕсли;
		ОписаниеОповещения = Новый ОписаниеОповещения("Подключаемый_ВыборФорматаВложений", ЭтотОбъект, ПараметрыФормы);
		ОткрытьФорму("ОбщаяФорма.ВыборФорматаВложений",ПараметрыФормы,,,,,ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	//rarus tenkam 28.04.2017 mantis 9444 ---
	
	//rarus vikhle 14.09.2020 mt 16526 +++
	ИначеЕсли ТекСтрокаДерева.ПравоНастройка = ПредопределенноеЗначение("ПланВидовХарактеристик.Scan_ПраваИНастройки.ТипыПродуктовБезНадстройки")
		ИЛИ ТекСтрокаДерева.ПравоНастройка = ПредопределенноеЗначение("ПланВидовХарактеристик.Scan_ПраваИНастройки.ТипыПродуктовСНадстройкой") 
		ИЛИ ТекСтрокаДерева.ПравоНастройка = ПредопределенноеЗначение("ПланВидовХарактеристик.Scan_ПраваИНастройки.ТипПродуктаДляАлгоритмовОповещенияОНеобходимостиИзмененияEDD") Тогда //rarus bonmak 16.02.2021 17234
		СтандартнаяОбработка = Ложь;
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("МножественныйВыбор",Истина);
		ПараметрыФормы.Вставить("ЗакрыватьПриВыборе",Истина);
		ОписаниеОповещения = Новый ОписаниеОповещения("Подключаемый_МножественныйВыборСсылок",ЭтотОбъект,"ТипыПродуктов"); //rarus vikhle 20.10.2020 mt 16328 добавил 3й параметр
		ОткрытьФорму("Справочник.Scan_ТипыПродуктов.ФормаВыбора",ПараметрыФормы,ЭтотОбъект,ПараметрыФормы,,,ОписаниеОповещения);
	//rarus vikhle 14.09.2020 mt 16526 ---

	//rarus vikhle 20.10.2020 mt 16526 +++
	ИначеЕсли ТекСтрокаДерева.ПравоНастройка = ПредопределенноеЗначение("ПланВидовХарактеристик.Scan_ПраваИНастройки.КузовостроителиДляСчетчикаDDПриИспользованииЕвропейскихКузовостроителей") Тогда 
		СтандартнаяОбработка = Ложь;
		СтруктураОтбора = Новый Структура("Недействителен,ПометкаУдаления,Кузовостроитель",Ложь,Ложь,Истина);
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Отбор",СтруктураОтбора);
		ПараметрыФормы.Вставить("МножественныйВыбор",Истина);
		ПараметрыФормы.Вставить("ЗакрыватьПриВыборе",Истина);
		ОписаниеОповещения = Новый ОписаниеОповещения("Подключаемый_МножественныйВыборСсылок",ЭтотОбъект,"Кузовостроители");
		ОткрытьФорму("Справочник.Scan_Контрагенты.ФормаВыбора",ПараметрыФормы,ЭтотОбъект,ПараметрыФормы,,,ОписаниеОповещения);
	
	ИначеЕсли ТекСтрокаДерева.ПравоНастройка = ПредопределенноеЗначение("ПланВидовХарактеристик.Scan_ПраваИНастройки.МестаХраненияДляРасчетаДатыDD") Тогда 
		СтандартнаяОбработка = Ложь;
		СтруктураОтбора = Новый Структура("Недействителен,ПометкаУдаления,Маршрут",Ложь,Ложь,Ложь);
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Отбор",СтруктураОтбора);
		ПараметрыФормы.Вставить("МножественныйВыбор",Истина);
		ПараметрыФормы.Вставить("ЗакрыватьПриВыборе",Истина);
		ОписаниеОповещения = Новый ОписаниеОповещения("Подключаемый_МножественныйВыборСсылок",ЭтотОбъект,"МестаХранения");
		ОткрытьФорму("Справочник.Scan_МестаХранения.ФормаВыбора",ПараметрыФормы,ЭтотОбъект,ПараметрыФормы,,,ОписаниеОповещения);
	//rarus vikhle 20.10.2020 mt 16526 ---
	
	//rarus vikhle 22.09.2021 m 17927 +++
	ИначеЕсли ТекСтрокаДерева.ПравоНастройка = ПредопределенноеЗначение("ПланВидовХарактеристик.Scan_ПраваИНастройки.СписокРегистровИПериодыОчисткиПредопределённыхРегистровСведений") Тогда
		СтандартнаяОбработка = Ложь;
		СтрокаНастроек 		 = Элементы.ДеревоПрав.ТекущиеДанные.Значение;
		ПараметрыФормы 		 = Новый Структура("СтрокаНастроек", СтрокаНастроек);
		ПараметрыВвода 		 = Новый Структура("ТекСтрокаДерева", ТекСтрокаДерева);
		Оповещение	  		 = Новый ОписаниеОповещения("ПослеВводаСтроки",ЭтотОбъект,ПараметрыВвода);
		ОткрытьФорму("Обработка.Scan_УстановкаПравИНастроек.Форма.ФормаНастройкиОчисткиРегистров", 
					ПараметрыФормы,,,,,
					Оповещение, 
					РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	//rarus vikhle 22.09.2021 m 17927 ---
	
	//rarus tenkam 27.02.2017 mantis 7185 +++
	ИначеЕсли ТипЗнч(ТекСтрокаДерева.Значение) = Тип("Строка") Тогда
		СтандартнаяОбработка = Ложь;
		Подсказка = Строка(ТекСтрокаДерева.ПравоНастройка) + ":";
		ПараметрыВвода = Новый Структура("ТекСтрокаДерева", ТекСтрокаДерева);
		Оповещение = Новый ОписаниеОповещения("ПослеВводаСтроки",ЭтотОбъект,ПараметрыВвода);
		ПоказатьВводСтроки(Оповещение, ТекСтрокаДерева.Значение, Подсказка, 0, Истина);	
	//rarus tenkam 27.02.2017 mantis 7185 ---
	
	//rarus vikhle 18.06.2021 mt 17637 +++
	ИначеЕсли ТекСтрокаДерева.ПравоНастройка = ПредопределенноеЗначение("ПланВидовХарактеристик.Scan_ПраваИНастройки.ШаблонСтандартныхУсловийОплатыДляSTDДляНезависимыхДилеров") //rarus vikhle 17.09.2021 mt 18288 изменил наименование права
		ИЛИ ТекСтрокаДерева.ПравоНастройка = ПредопределенноеЗначение("ПланВидовХарактеристик.Scan_ПраваИНастройки.ШаблонСтандартныхУсловийОплатыДляSTDДляОСП") Тогда //rarus vikhle 16.09.2021 mt 18288
		СтандартнаяОбработка = Ложь;
		СтруктураОтбора = Новый Структура("Родитель",ПредопределенноеЗначение("Справочник.Scan_ШаблоныУсловийОплаты.STD"));
		ПараметрыФормы = Новый Структура("Отбор",СтруктураОтбора);
		ОткрытьФорму("Справочник.Scan_ШаблоныУсловийОплаты.ФормаВыбора",ПараметрыФормы,Элемент);
	ИначеЕсли  ТекСтрокаДерева.ПравоНастройка = ПредопределенноеЗначение("ПланВидовХарактеристик.Scan_ПраваИНастройки.ШаблонСтандартныхУсловийОплатыДляUOUUOДляНезависимыхДилеров") //rarus vikhle 17.09.2021 mt 18288 изменил наименование права 
		ИЛИ ТекСтрокаДерева.ПравоНастройка = ПредопределенноеЗначение("ПланВидовХарактеристик.Scan_ПраваИНастройки.ШаблонСтандартныхУсловийОплатыДляUOUUOДляОСП") Тогда //rarus vikhle 16.09.2021 mt 18288
		СтандартнаяОбработка = Ложь;
		СтруктураОтбора = Новый Структура("Родитель",ПредопределенноеЗначение("Справочник.Scan_ШаблоныУсловийОплаты.UOUUO"));
		ПараметрыФормы = Новый Структура("Отбор",СтруктураОтбора);
		ОткрытьФорму("Справочник.Scan_ШаблоныУсловийОплаты.ФормаВыбора",ПараметрыФормы,Элемент);	
	//rarus vikhle 18.06.2021 mt 17637 ---
	КонецЕсли;
	
	// проверка является ли строка дерева прав типом перечисления ВидыПравДляСправочников
	// и если справочник иерархический то отменяем стандартную обработку и создаем список значений для выбора
	Если ТипЗнч(ТекСтрокаДерева.Значение) = Тип("ПеречислениеСсылка.Scan_ВидыПравДляСправочников") Тогда
		Если СправочникНеиерархическийИЛИсИерархиейЭлементов(Сред(ТекСтрокаДерева.ПравоНастройка, 15)) Тогда
			СтандартнаяОбработка = Ложь;
			
			спДоступностиДляНеиерархии = Новый СписокЗначений;
			спДоступностиДляНеиерархии.Добавить(ПредопределенноеЗначение("Перечисление.Scan_ВидыПравДляСправочников.Редактирование"), "Редактирование");
			спДоступностиДляНеиерархии.Добавить(ПредопределенноеЗначение("Перечисление.Scan_ВидыПравДляСправочников.Чтение")        , "Чтение");
			спДоступностиДляНеиерархии.Добавить(ПредопределенноеЗначение("Перечисление.Scan_ВидыПравДляСправочников.НетДоступа")    , "Нет доступа");
			ВыбранныйЭлемент = Неопределено;
			
			ПоказатьВыборИзСписка(Новый ОписаниеОповещения("ДеревоПравЗначениеНачалоВыбораЗавершение", ЭтотОбъект, Новый Структура("ТекСтрокаДерева", ТекСтрокаДерева)), спДоступностиДляНеиерархии, Элемент);
		КонецЕсли;
		
		//ИначеЕсли ТипЗнч(ТекСтрокаДерева.Значение) = Тип("ПеречислениеСсылка.уатВидыДвиженияГСМ") Тогда
		//	СтандартнаяОбработка = Ложь;
		//	
		//	спСписокВидовЗаправок = уатОбщегоНазначения.СписокВидовЗаправок();
		//	ПоказатьВыборИзСписка(Новый ОписаниеОповещения("ДеревоПравЗначениеНачалоВыбораЗавершениеВыбратьИЗСписка", ЭтотОбъект, Новый Структура("ТекСтрокаДерева", ТекСтрокаДерева)), спСписокВидовЗаправок, Элемент);
		//	
		//ИначеЕсли ТекСтрокаДерева.ПравоНастройка = ПредопределенноеЗначение("ПланВидовХарактеристик.уатПраваИНастройки.ОсновнаяАЗС") Тогда
		//	НайденнаяСтрокаЗначение = НайтиОтмеченноеЗначениеВДеревеЗначений(ПредопределенноеЗначение("ПланВидовХарактеристик.уатПраваИНастройки.ОсновнойВидЗаправки"));
		//	Если НайденнаяСтрокаЗначение <> Неопределено И ЗначениеЗаполнено(НайденнаяСтрокаЗначение) Тогда
		//		ОсновнойВидЗаправки = НайденнаяСтрокаЗначение;
		//	Иначе
		//		Возврат;
		//	КонецЕсли;
		//	
		//	ПараметрыОткрытия = Новый Структура("Отбор", Новый Структура("ЭтоАЗССклад",
		//		ОсновнойВидЗаправки = ПредопределенноеЗначение("Перечисление.уатВидыДвиженияГСМ.ЗаправкаСклад")
		//		ИЛИ ОсновнойВидЗаправки = ПредопределенноеЗначение("Перечисление.уатВидыДвиженияГСМ.ЗаправкаТалоны")
		//		ИЛИ ОсновнойВидЗаправки = ПредопределенноеЗначение("Перечисление.уатВидыДвиженияГСМ.ЗаправкаПластиковаяКартаСклад")));
		//	
		//	ОткрытьФорму("Справочник.уатАЗС.ФормаВыбора", ПараметрыОткрытия, Элемент);
		//	СтандартнаяОбработка = Ложь;		
		//	
		//ИначеЕсли ТекСтрокаДерева.ПравоНастройка = ПредопределенноеЗначение("ПланВидовХарактеристик.уатПраваИНастройки.ОсновнойВрач")
		//	ИЛИ ТекСтрокаДерева.ПравоНастройка = ПредопределенноеЗначение("ПланВидовХарактеристик.уатПраваИНастройки.ОсновнойДиспетчер")
		//	ИЛИ ТекСтрокаДерева.ПравоНастройка = ПредопределенноеЗначение("ПланВидовХарактеристик.уатПраваИНастройки.ОсновнойМеханик") Тогда
		//	
		//	//уатЗащищенныеФункцииКлиент.ДиалогВыбораСотрудника(Элемент, ТекСтрокаДерева.Значение, Неопределено, СтандартнаяОбработка);
		//	
	КонецЕсли;
	
	
КонецПроцедуры

//rarus tenkam 27.02.2017 mantis 7185 +++
&НаКлиенте
Процедура ПослеВводаСтроки(Строка, Параметры) Экспорт
	Если НЕ Строка = Неопределено Тогда
		ТекСтрокаДерева = Параметры.ТекСтрокаДерева;
		Если ТекСтрокаДерева.Значение <> СокрЛП(Строка) Тогда
			ТекСтрокаДерева.Значение = СокрЛП(Строка);
			ТекСтрокаДерева.Изменено	= Истина;
			ЭтаФорма.Модифицированность	= Истина; 
		КонецЕсли;
		
	КонецЕсли;
КонецПроцедуры
//rarus tenkam 27.02.2017 mantis 7185 ---

//rarus tenkam 28.04.2017 mantis 9444 +++
&НаКлиенте
Процедура Подключаемый_ВыборФорматаВложений(РезультатЗакрытия, Параметры = Неопределено) Экспорт
	Если РезультатЗакрытия = Неопределено ИЛИ
		РезультатЗакрытия = КодВозвратаДиалога.Отмена Тогда
		Возврат;
	КонецЕсли;
	ТекСтрокаДерева = Элементы.ДеревоПрав.ТекущиеДанные;
	ТекСтрокаДерева.Значение = Scan_ВспомогательныеФункцииСервер.ПреобразоватьФорматыСохраненияВСтроку(РезультатЗакрытия.ФорматыСохранения);
	ТекСтрокаДерева.Изменено	= Истина;
	ЭтаФорма.Модифицированность	= Истина;	
КонецПроцедуры
//rarus tenkam 28.04.2017 mantis 9444 ---

//rarus vikhle 14.09.2020 mt 16526 +++
&НаКлиенте
Процедура Подключаемый_МножественныйВыборСсылок(РезультатВыбора,ДопПараметры) Экспорт //rarus vikhle 20.10.2020 mt 16328 переименовал процедуру
	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ТекСтрокаДерева = Элементы.ДеревоПрав.ТекущиеДанные;
	СписокПредставлений = "";
	Для Каждого ТипПродукта Из РезультатВыбора Цикл
		//rarus vikhle 20.10.2020 mt 16328 +++
		Если ДопПараметры = "ТипыПродуктов" Тогда
			СписокПредставлений = СписокПредставлений + ?(СписокПредставлений = "","",",") + Строка(ТипПродукта);
		Иначе	
			СписокПредставлений = СписокПредставлений + ?(СписокПредставлений = "","",Символы.ПС) + Строка(ТипПродукта);
		КонецЕсли;
		//rarus vikhle 20.10.2020 mt 16328 ---
	КонецЦикла;	
	ТекСтрокаДерева.Значение = СписокПредставлений;
	ТекСтрокаДерева.Изменено	= Истина;
	ЭтаФорма.Модифицированность	= Истина;	
КонецПроцедуры
//rarus vikhle 14.09.2020 mt 16526 ---

&НаКлиенте
Процедура НастроитьНаименование(ЗначениеНастройкиНаименования, Параметры) Экспорт
	Если ЗначениеНастройкиНаименования = Неопределено ИЛИ
		ЗначениеНастройкиНаименования = КодВозвратаДиалога.Отмена Тогда
		Возврат;
	КонецЕсли;
	ТекСтрокаДерева = Элементы.ДеревоПрав.ТекущиеДанные;
	ТекСтрокаДерева.Значение = ЗначениеНастройкиНаименования.НастройкаНаименования;
	ТекСтрокаДерева.Изменено	= Истина;
	ЭтаФорма.Модифицированность	= Истина; 
КонецПроцедуры

&НаКлиенте
Процедура ДеревоПравЗначениеНачалоВыбораЗавершениеВыбратьИЗСписка(ВыбранныйЭлемент1, ДополнительныеПараметры) Экспорт
    
    ТекСтрокаДерева = ДополнительныеПараметры.ТекСтрокаДерева;
    
    
    ВыбранныйЭлемент = ВыбранныйЭлемент1;
    Если ВыбранныйЭлемент = Неопределено Тогда
        ТекСтрокаДерева.Значение = Scan_ОбщегоНазначенияТиповые.ПолучитьЗначениеРеквизита(ТекСтрокаДерева.ПравоНастройка, "ЗначениеПоУмолчанию");
    Иначе
        ТекСтрокаДерева.Значение = ВыбранныйЭлемент.Значение;
    КонецЕсли;
    ТекСтрокаДерева.Изменено = Истина;
    Модифицированность = Истина;

КонецПроцедуры

&НаКлиенте
Процедура ДеревоПравЗначениеНачалоВыбораЗавершение(ВыбранныйЭлемент1, ДополнительныеПараметры) Экспорт
    
    ТекСтрокаДерева = ДополнительныеПараметры.ТекСтрокаДерева;
    
    
    ВыбранныйЭлемент = ВыбранныйЭлемент1;
    Если ВыбранныйЭлемент = Неопределено Тогда
        ТекСтрокаДерева.Значение = Scan_ОбщегоНазначенияТиповые.ПолучитьЗначениеРеквизита(ТекСтрокаДерева.ПравоНастройка, "ЗначениеПоУмолчанию");
    Иначе
        ТекСтрокаДерева.Значение = ВыбранныйЭлемент.Значение;
    КонецЕсли;
    ТекСтрокаДерева.Изменено = Истина;
    Модифицированность = Истина;

КонецПроцедуры

&НаКлиенте
Процедура ДеревоПравЗначениеОткрытие(Элемент, СтандартнаяОбработка)
	ТекСтрокаДерева = Элементы.ДеревоПрав.ТекущиеДанные;
	Если ТипЗнч(ТекСтрокаДерева.Значение) = Тип("СправочникСсылка.Scan_ШаблоныСогласования") Тогда
		//уатЗащищенныеФункцииКлиент.ОткрытьФормуСотрудника(ТекСтрокаДерева.Значение, СтандартнаяОбработка);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ДеревоПравЗначениеПриИзменении(Элемент)
	Стр = Элементы.ДеревоПрав.ТекущиеДанные;
	Стр.Изменено = Истина;
	Если Стр.Тип = Тип("Булево") Тогда
		Стр.ЗначениеБулево = Стр.Значение;
	КонецЕсли; 
	
	//Если Стр.ПравоНастройка = ПредопределенноеЗначение("ПланВидовХарактеристик.уатПраваИНастройки.ВестиСкладскойУчетУАТ") Тогда
	//	мВестиСкладскойУчетУАТ = Стр.Значение;
	//ИначеЕсли Стр.ПравоНастройка = ПредопределенноеЗначение("ПланВидовХарактеристик.уатПраваИНастройки.ВестиУчетЗатрат") Тогда
	//	мВестиУчетЗатрат = Стр.Значение;
	//КонецЕсли;
	
	Модифицированность = Истина;
	
	Стр.ПравоДоступаРедактированиеПоПользователям = Ложь;
	
	// Обработка доступности станиц 
	Если Стр.Значение = ПредопределенноеЗначение("Перечисление.Scan_ВидыПравДляСправочников.РедактированиеПоГруппам") Тогда
		Элементы.НастройкаДополнительныхПравСправочниковИДокументов.Видимость = Истина;
		Стр.ПравоДоступаРедактированиеПоПользователям = Истина;
		
	ИначеЕсли Стр.Значение = ПредопределенноеЗначение("Перечисление.Scan_ВидыПравДляДокументов.РедактированиеПоПользователям") Тогда
		Элементы.НастройкаДополнительныхПравСправочниковИДокументов.Видимость = Истина;
		ВидДокумента = Сред(Стр.ПравоНастройка, 15);
		ЗаголовокДополнительныеПраваСправочниковИДокументов = "Доступ по авторам документов вида: " + ПредставлениеВидаДокумента(ВидДокумента);
		Стр.ПравоДоступаРедактированиеПоПользователям = Истина;
		
	//ИначеЕсли Стр.ПравоНастройка = ПредопределенноеЗначение("ПланВидовХарактеристик.уатПраваИНастройки.ТочностьОстатковТоплива") Тогда
	//	Если Стр.Значение < 0 ИЛИ Стр.Значение > 3 Тогда 
	//		Сообщить("Точность должна быть в пределах от 0 до 3!");
	//		Стр.Значение = 0;
	//		Возврат;
	//	КонецЕсли;
		
	Иначе
		Элементы.НастройкаДополнительныхПравСправочниковИДокументов.Видимость = Ложь;
		
	КонецЕсли;	
	
	Если ((ТипЗнч(Стр.Значение) = Тип("ПеречислениеСсылка.Scan_ВидыПравДляСправочников")) ИЛИ
		(ТипЗнч(Стр.Значение) = Тип("ПеречислениеСсылка.Scan_ВидыПравДляДокументов"))) И
		(СтароеЗначениеПравНастроек <> Стр.Значение) Тогда
		УдалитьТекущиеДоступности();	
	КонецЕсли;
	
	//rarus tenkam 10.08.2017 mantis 10589 +++
	//rarus abrant 18.07.2017 mantis 9146 +++
	//Если ЭтоЛьготноеРасстояние(Стр.ПравоНастройка) Тогда		 
	//	Сообщить("Льготное расстояние было изменено. Необходимо произвести перерасчет Тарифов доставки для дилеров",СтатусСообщения.ОченьВажное);
	//КонецЕсли;	
	//rarus abrant 18.07.2017 mantis 9146 ---
	//rarus tenkam 10.08.2017 mantis 10589 ---
	
	// rarus tenkam 05.06.2018 mantis 13137 +++
	Если Стр.ПравоНастройка = ПредопределенноеЗначение("ПланВидовХарактеристик.Scan_ПраваИНастройки.ИспользоватьМеханизмШаблоновДляОтправкиПисем") Тогда	
		НужноОбновлятьДерево = Истина;
	КонецЕсли;
	// rarus tenkam 05.06.2018 mantis 13137 ---
	
	//rarus vikhle 25.11.2020 mt 16846 +++
	Если Стр.ПравоНастройка = ПредопределенноеЗначение("ПланВидовХарактеристик.Scan_ПраваИНастройки.ПроцентДрайв") Тогда
		Попытка
			Стр.Значение = Строка(Число(Стр.Значение));
		Исключение
			Стр.Значение = "0";	
		КонецПопытки;	
	КонецЕсли;
	//rarus vikhle 25.11.2020 mt 16846
	
	ПроставитьПризнакЗначениеРавноДефолтному(ДеревоПрав);
КонецПроцедуры

//rarus tenkam 10.08.2017 mantis 10589 +++
////rarus abrant 18.07.2017 mantis 9146 +++
//&НаСервереБезКонтекста
//Функция ЭтоЛьготноеРасстояние(ПравоНастройка)
//	Если ПравоНастройка = ПланыВидовХарактеристик.Scan_ПраваИНастройки.ЛьготноеРасстояниеПриДоставке Тогда
//		Возврат Истина;
//	Иначе
//		Возврат Ложь;
//	КонецЕсли;
//КонецФункции
////rarus abrant 18.07.2017 mantis 9146 ---	
//rarus tenkam 10.08.2017 mantis 10589 ---
	
&НаСервереБезКонтекста
Функция ПредставлениеВидаСправочника(ВидСпр)
	Возврат Метаданные.Справочники[ВидСпр].Представление();
КонецФункции

&НаСервереБезКонтекста
Функция ПредставлениеВидаДокумента(ВидДокумента)
	Возврат Метаданные.Документы[ВидДокумента].Представление();
КонецФункции

&НаКлиенте
Процедура ДеревоПравЗначениеБулевоПриИзменении(Элемент)
	Стр = Элементы.ДеревоПрав.ТекущиеДанные;
	Стр.Значение = Стр.ЗначениеБулево;
	ДеревоПравЗначениеПриИзменении(Элемент);
КонецПроцедуры

&НаСервереБезКонтекста
Функция СправочникНеиерархическийИЛИсИерархиейЭлементов(ВидСправочника)
	Рез = (НЕ Метаданные.Справочники[ВидСправочника].Иерархический) ИЛИ (Строка(Метаданные.Справочники[ВидСправочника].ВидИерархии) = "ИерархияЭлементов");
	Возврат Рез;
КонецФункции

&НаКлиенте
Процедура ДеревоПравПриАктивизацииСтроки(Элемент)
	ТекСтрока = Элементы.ДеревоПрав.ТекущиеДанные;
	
	Попытка
		СтруктураОписаний.Свойство("_" + СокрЛП(ТекСтрока.Код), Комментарий);
		ОписаниеПрава = ТипОбъектаНастройки;
	Исключение
		Комментарий = "";
		ОписаниеПрава = "";
	КонецПопытки;
	
	Если ТекСтрока = Неопределено Тогда
		Разрешено = Ложь;
		Элементы.НастройкаДополнительныхПравСправочниковИДокументов.Видимость = Ложь;
	Иначе
		// Обработка доступности станиц 
		Если ТипЗнч(ТекСтрока.Значение) = Тип("ПеречислениеСсылка.Scan_ВидыПравДляСправочников") Тогда
			Элементы.ДеревоПрав.ТолькоПросмотр = Ложь;
			ВидСправочника = Сред(ТекСтрока.ПравоНастройка, 15);
			ЗаголовокДополнительныеПраваСправочниковИДокументов = "Доступ по группам справочников: " + ПредставлениеВидаСправочника(ВидСправочника);
			Если ТекСтрока.Значение = ПредопределенноеЗначение("Перечисление.Scan_ВидыПравДляСправочников.РедактированиеПоГруппам") Тогда
				Элементы.НастройкаДополнительныхПравСправочниковИДокументов.Видимость = Истина;
			Иначе
				Элементы.НастройкаДополнительныхПравСправочниковИДокументов.Видимость = Ложь;
			КонецЕсли;
			
		ИначеЕсли ТипЗнч(ТекСтрока.Значение) = Тип("ПеречислениеСсылка.Scan_ВидыПравДляДокументов") Тогда
			Элементы.ДеревоПрав.ТолькоПросмотр = Ложь;
			ВидДокумента = Сред(ТекСтрока.ПравоНастройка, 15);
			ЗаголовокДополнительныеПраваСправочниковИДокументов = "Доступ по всем авторам документов вида: " + ПредставлениеВидаДокумента(ВидДокумента);
			
			Если ТекСтрока.Значение = ПредопределенноеЗначение("Перечисление.Scan_ВидыПравДляДокументов.РедактированиеПоПользователям") 
			   И ТекСтрока.ПравоНастройка <> ПравоДоступауатРегламентированныйОтчет() И ТекСтрока.ПравоНастройка <> ПравоДоступауатВыгрузкаРегламентированныхОтчетов() Тогда
				Элементы.НастройкаДополнительныхПравСправочниковИДокументов.Видимость = Истина;
				ЗаголовокДополнительныеПраваСправочниковИДокументов = "Доступ по авторам документов вида: " + ПредставлениеВидаДокумента(ВидДокумента);	
			Иначе
				Элементы.НастройкаДополнительныхПравСправочниковИДокументов.Видимость = Ложь;
			КонецЕсли;
			
		Иначе
			// rarus tenkam 08.04.2019 mantis 14195 +++ убрала видимость вручную у элемента формы
			//Элементы.НастройкаДополнительныхПравСправочниковИДокументов.Видимость = Ложь;
			// rarus tenkam 08.04.2019 mantis 14195 ---
			ЗаголовокДополнительныеПраваСправочниковИДокументов = "Дополнительные права справочников и документов";	
			
		КонецЕсли;
		
		Если ТекСтрока.Группа Тогда
			Разрешено = Истина;
		Иначе
			Разрешено = (ТекСтрока.ЭтоНастройка И ДостНастройки) ИЛИ (НЕ ТекСтрока.ЭтоНастройка И ДостПрава);
			//Разрешено = Разрешено И Не ЭлементыФормы.ДеревоПрав.ОформлениеСтроки(ТекСтрока).Ячейки.Значение.ТолькоПросмотр;
			Разрешено = Разрешено ИЛИ ТекСтрока.Группа;
			Элементы.ДеревоПрав.ТолькоПросмотр = НЕ Разрешено;
			Элементы.ТаблицаДоступностиОбъектов.ТолькоПросмотр = НЕ Разрешено;
		КонецЕсли;
		
		Если ТекСтрока.ПравоНастройка = ПредопределенноеЗначение("ПланВидовХарактеристик.Scan_ПраваИНастройки.РазрешитьИзменениеЛичныхНастроек")
			И ТекущийПользователь = ОбъектНастройкиПрав Тогда
			Разрешено = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	// доступность кнопки УстановитьТекущееПравоПоУмолчанию (в контекстном меню)
	Элементы.ДеревоПравКонтекстноеМенюУстановитьТекущееПравоПоУмолчанию.Доступность = Разрешено;
КонецПроцедуры

&НаКлиенте
Процедура ДеревоПравПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	// Запретить пользователю добавлять новые строки
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоПравВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	Если Поле.Имя = "ДеревоПравПравоНастройка" Тогда
		Если ТипОбъектаНастройки <> ПредопределенноеЗначение("Перечисление.Scan_НазначениеПравИНастроек.Пользователь") Тогда
			Возврат;
		КонецЕсли;
		Если Элементы.ДеревоПрав.ТекущиеДанные.ПравоДоступаРедактированиеПоПользователям Тогда 
			Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.НастройкаДополнительныхПравСправочниковИДокументов;
		КонецЕсли;
	ИначеЕсли Поле = Элементы.ДеревоПравЗначение Тогда //rarus vikhle 28.10.2020 mt 16328 +++
		ТекущиеДанные = Элементы.ДеревоПрав.ТекущиеДанные;
		Поле.Маска = ""; //rarus vikhle 25.11.2020 mt 16846
		Если ТекущиеДанные.ПравоНастройка = ПредопределенноеЗначение("ПланВидовХарактеристик.Scan_ПраваИНастройки.КузовостроителиДляСчетчикаDDПриИспользованииЕвропейскихКузовостроителей")
			ИЛИ ТекущиеДанные.ПравоНастройка = ПредопределенноеЗначение("ПланВидовХарактеристик.Scan_ПраваИНастройки.МестаХраненияДляРасчетаДатыDD")
			ИЛИ ТекущиеДанные.ПравоНастройка = ПредопределенноеЗначение("ПланВидовХарактеристик.Scan_ПраваИНастройки.ТипыПродуктовБезНадстройки")
			ИЛИ ТекущиеДанные.ПравоНастройка = ПредопределенноеЗначение("ПланВидовХарактеристик.Scan_ПраваИНастройки.ТипыПродуктовСНадстройкой") 
			ИЛИ ТекущиеДанные.ПравоНастройка = ПредопределенноеЗначение("ПланВидовХарактеристик.Scan_ПраваИНастройки.СписокРегистровИПериодыОчисткиПредопределённыхРегистровСведений")  Тогда //rarus vikhle 22.09.2021 mt 17927
			Поле.РедактированиеТекста = Ложь;
			Поле.КнопкаОчистки = Истина;
		//rarus vikhle 25.11.2020 mt 16846 +++	
		ИначеЕсли ТекущиеДанные.ПравоНастройка = ПредопределенноеЗначение("ПланВидовХарактеристик.Scan_ПраваИНастройки.ПроцентДрайв") Тогда
			Поле.Маска = "9,9";
		//rarus vikhle 25.11.2020 mt 16846 ---	
		Иначе
			Поле.РедактированиеТекста = Истина;
			Поле.КнопкаОчистки = Неопределено;
		КонецЕсли;	
		//rarus vikhle 28.10.2020 mt 16328 ---	
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ДеревоПравПередНачаломИзменения(Элемент, Отказ)
	ТекСтрока = Элементы.ДеревоПрав.ТекущиеДанные;
	//Если НЕ мВестиСкладскойУчетУАТ И ТекСтрока.ПравоНастройка = ПредопределенноеЗначение("ПланВидовХарактеристик.уатПраваИНастройки.СпособОценкиМПЗ")
	//	ИЛИ НЕ мВестиУчетЗатрат И ТекСтрока.ПравоНастройка = ПредопределенноеЗначение("ПланВидовХарактеристик.уатПраваИНастройки.СпособРаспределенияЗатратМеждуТС") Тогда
	//	Отказ = Истина;
	//КонецЕсли;
	////rarus tenkam 05.10.2016 mantis 7183 ++
	//Если ТекСтрока.ПравоНастройка = ПредопределенноеЗначение("ПланВидовХарактеристик.Scan_ПраваИНастройки.НастройкаВыводаНаименованияИзделия") Тогда
	//	ТипПоля = Новый ОписаниеТипов("Дата");
	//	Элемент.ПодчиненныеЭлементы.ДополнительныеПоляЗначение.ВыбиратьТип = Ложь;
	//	Элемент.ПодчиненныеЭлементы.ДополнительныеПоляЗначение.ОграничениеТипа = ТипПоля;
	//	
	//КонецЕсли;
	////rarus tenkam 05.10.2016 mantis 7183 --
КонецПроцедуры

&НаКлиенте
Процедура ДеревоПравПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	ТекСтрока = Элементы.ДеревоПрав.ТекущиеДанные;
	Если ТекСтрока.ТипВремя Тогда
		Элементы.ДеревоПравЗначение.ФорматРедактирования = "ДФ=HH:mm";
		Элементы.ДеревоПравЗначение.КнопкаВыбора = Ложь;
	//rarus vikhle 25.11.2020 mt 16846 +++	
	ИначеЕсли ТекСтрока.ПравоНастройка = ПредопределенноеЗначение("ПланВидовХарактеристик.Scan_ПраваИНастройки.ПроцентДрайв") Тогда
		Элементы.ДеревоПравЗначение.ФорматРедактирования = "";
		Элементы.ДеревоПравЗначение.КнопкаВыбора = Ложь;	
	//rarus vikhle 25.11.2020 mt 16846 ---	
	Иначе
		Элементы.ДеревоПравЗначение.ФорматРедактирования = "";
		Элементы.ДеревоПравЗначение.КнопкаВыбора = Истина;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ГруппаСтраницыПриСменеСтраницы(Элемент, ТекущаяСтраница)
	ТекСтрокаДерева = ДеревоПрав.НайтиПоИдентификатору(Элементы.ДеревоПрав.ТекущаяСтрока);
	
	Попытка
		Если ТекущаяСтраница = Элементы.НастройкаДополнительныхПравСправочниковИДокументов Тогда 
			ВидОбъекта = Сред(ТекСтрокаДерева.ПравоНастройка, 15);
			Если ТекСтрокаДерева.Значение = ПредопределенноеЗначение("Перечисление.Scan_ВидыПравДляСправочников.РедактированиеПоГруппам") Тогда
				ВидОбъектаНастройкиПрав = "Справочники";
				Элементы.ПраваДоступаПодборПоГруппам.Доступность = Истина;
				Элементы.ТаблицаДоступностиОбъектовОбъект.Заголовок = "Группы справочника: " + ПредставлениеВидаСправочника(ВидОбъекта);
				ЗаполнитьТаблицуДоступностейОбъектов(ТекСтрокаДерева);
				
			ИначеЕсли ТекСтрокаДерева.Значение = ПредопределенноеЗначение("Перечисление.Scan_ВидыПравДляДокументов.РедактированиеПоПользователям") Тогда 
				ВидОбъектаНастройкиПрав = "Документы";
				Элементы.ПраваДоступаПодборПоГруппам.Доступность = Ложь;
				Элементы.ТаблицаДоступностиОбъектовОбъект.Заголовок = "Авторы документов вида: " + ПредставлениеВидаДокумента(ВидОбъекта);
				ЗаполнитьТаблицуДоступностейОбъектов(ТекСтрокаДерева);
				
			КонецЕсли;
			ДоступностьНастроекИПараметров(Ложь);
			
		Иначе
			Если ВидОбъектаНастройкиПрав = "Справочники" Тогда
				ПереписатьТабДоступностиСправочников();
				
			ИначеЕсли ВидОбъектаНастройкиПрав = "Документы" Тогда
				ПереписатьТабДоступностиДокументов();
				
			КонецЕсли;
			ВидОбъектаНастройкиПрав = "Обычный";
			ДоступностьНастроекИПараметров(Истина);
			
		КонецЕсли;
	Исключение
	КонецПопытки;
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаДоступностиОбъектовПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	Отказ = Истина;
	ДобавитьПользователяИлиГруппу("Добавить", "Выбор группы справочника");
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьПользователяИлиГруппу(КнопкаИмя, ЗаголовокФормыПодбора)
	Стр = ДеревоПрав.НайтиПоИдентификатору(Элементы.ДеревоПрав.ТекущаяСтрока);
	
	ВидОбъекта = Сред(Стр.ПравоНастройка, 15);
	
	// заполнение флага для подбора без вложенных групп
	ОдиночноеДобавление = Истина;                                      
	
	ПараметрыОткрытия = Новый Структура;
	
	// проверка подбор или обычное добавление
	Если КнопкаИмя = "Добавить" Тогда
		ПараметрыОткрытия.Вставить("ЗакрыватьПриВыборе", Истина);
	Иначе
		Если КнопкаИмя = "ЗаполнитьВыбранныеСВложенными" Тогда
			// заполнение флага для подбора со вложенными группами
			ОдиночноеДобавление = Ложь;
		КонецЕсли;
		ПараметрыОткрытия.Вставить("ЗакрыватьПриВыборе", Ложь);
	КонецЕсли;
	
	// проверка документ или справочник	
	Если Стр.Значение = ПредопределенноеЗначение("Перечисление.Scan_ВидыПравДляСправочников.РедактированиеПоГруппам") Тогда
		ПараметрыОткрытия.Вставить("ВидСправочника", ВидОбъекта);
		ПараметрыОткрытия.Вставить("Заголовок", ЗаголовокФормыПодбора);
		
		ФормаПодбора = ПолучитьФорму("Обработка.Scan_УстановкаПравИНастроек.Форма.ФормаДереваГруппСправочников", ПараметрыОткрытия, Элементы.ТаблицаДоступностиОбъектов);
		
	ИначеЕсли ВидОбъектаНастройкиПрав = "Документы" Тогда 
		Если Стр.Значение = ПредопределенноеЗначение("Перечисление.Scan_ВидыПравДляДокументов.РедактированиеПоПользователям") Тогда
			ФормаПодбора = ПолучитьФорму("Справочник.Пользователи.ФормаВыбора", ПараметрыОткрытия, Элементы.ТаблицаДоступностиОбъектов);
		КонецЕсли;
		
	КонецЕсли;
	
	ФормаПодбора.Открыть();
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаДоступностиОбъектовПередУдалением(Элемент, Отказ)
	ТекСтрокаПрава = ДеревоПрав.НайтиПоИдентификатору(Элементы.ДеревоПрав.ТекущаяСтрока);
	ТекСтрока = Элементы.ТаблицаДоступностиОбъектов.ТекущиеДанные;
	Если НЕ ЗначениеЗаполнено(ТекСтрока.Объект) Тогда
		Возврат;
	КонецЕсли;
	
	Отбор = Новый Структура;
	Отбор.Вставить("Пользователь", ТекСтрока.Пользователь);
	Отбор.Вставить("Право", ТекСтрока.Право);
	Отбор.Вставить("Объект", ТекСтрока.Объект);
	
	Если ТипЗнч(ТекСтрокаПрава.Значение) = Тип("ПеречислениеСсылка.Scan_ВидыПравДляСправочников") Тогда
		СтрокиПоиска = ТабДоступностиСправочников.НайтиСтроки(Отбор);
		Если СтрокиПоиска.Количество()=0 Тогда
			Возврат;
		КонецЕсли;
		ТабДоступностиСправочников.Удалить(СтрокиПоиска[0]);
		ТекРег = "Scan_ДоступКСправочникам";
		
	ИначеЕсли ТипЗнч(ТекСтрокаПрава.Значение) = Тип("ПеречислениеСсылка.Scan_ВидыПравДляДокументов") Тогда
		СтрокиПоиска = ТабДоступностиДокументов.НайтиСтроки(Отбор);
		Если СтрокиПоиска.Количество() = 0 Тогда
			Возврат;
		КонецЕсли;
		ТабДоступностиДокументов.Удалить(СтрокиПоиска[0]);
		ТекРег = "Scan_ДоступКДокументам";
		
	Иначе
		Возврат;
		
	КонецЕсли;
	
	УдалитьЗаписьПравоДоступа(Отбор, ТекРег);
КонецПроцедуры

&НаСервереБезКонтекста
Процедура УдалитьЗаписьПравоДоступа(Отбор, ТекРег)
	Рег = РегистрыСведений[ТекРег].СоздатьМенеджерЗаписи();
	Рег.Пользователь = Отбор.Пользователь;
	Рег.Право = Отбор.Право;
	Рег.Объект = Отбор.Объект;
	Рег.Удалить();
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаДоступностиОбъектовДоступностьПриИзменении(Элемент)
	Стр = Элементы.ТаблицаДоступностиОбъектов.ТекущиеДанные;
	Стр.Изменено = Истина;
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаДоступностиОбъектовОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ТекущаяСтрока = ДеревоПрав.НайтиПоИдентификатору(Элементы.ДеревоПрав.ТекущаяСтрока);
	Если ТипЗнч(ТекущаяСтрока.Значение) = Тип("ПеречислениеСсылка.Scan_ВидыПравДляСправочников") Тогда
		Модифицированность = Истина;
		
		// проверка подбора (просто подбор или подбор со всеми вложенными группами)
		Если ОдиночноеДобавление Тогда
			//Если Текущая строка уже есть то просто меняем значение ее доступности
			мсвСтрокПоиска = ТаблицаДоступностиОбъектов.НайтиСтроки(Новый Структура("Объект", ВыбранноеЗначение.Значение));
			Если мсвСтрокПоиска.Количество() > 0 Тогда
				ТекСтрока = мсвСтрокПоиска[0];
				Если ТекСтрока.Доступность <> ВыбранноеЗначение.УстанавливатьПометку Тогда 
					ТекСтрока.Изменено = Истина;
					ТекСтрока.Доступность = ВыбранноеЗначение.УстанавливатьПометку;
				КонецЕсли;
				Возврат;
			КонецЕсли;
			НоваяСтрока = ТаблицаДоступностиОбъектов.Добавить();
			НоваяСтрока.Объект = ВыбранноеЗначение.Значение;
			НоваяСтрока.Пользователь = ОбъектНастройкиПрав;
			НоваяСтрока.Право = ТекущаяСтрока.ПравоНастройка;
			НоваяСтрока.Доступность = ВыбранноеЗначение.УстанавливатьПометку;
			НоваяСтрока.Инд = ТабДоступностиСправочников.Количество();
			НоваяСтрока.Изменено = Истина;
			
		Иначе
			ВидСправочника = Сред(ТекущаяСтрока.ПравоНастройка, 15);
			
			мсвЭлементовГруппы = ОбработкаВыбораПолучитьОбъектыГруппы(ВидСправочника, ВыбранноеЗначение.Значение);
			
			Для Каждого ТекЭлемСпр Из мсвЭлементовГруппы Цикл
				//Если Текущая строка уже есть то просто меняем значение ее доступности
				мсвСтрокПоиска = ТаблицаДоступностиОбъектов.НайтиСтроки(Новый Структура("Объект", ТекЭлемСпр));
				Если мсвСтрокПоиска.Количество() > 0 Тогда
					ТекСтрока = мсвСтрокПоиска[0];
					Если ТекСтрока.Доступность <> ВыбранноеЗначение.УстанавливатьПометку Тогда 
						ТекСтрока.Доступность = ВыбранноеЗначение.УстанавливатьПометку;
						ТекСтрока.Изменено = Истина;
					КонецЕсли;
					Продолжить;
				КонецЕсли;
				
				НоваяСтрока = ТаблицаДоступностиОбъектов.Добавить();
				НоваяСтрока.Объект = ТекЭлемСпр;
				НоваяСтрока.Пользователь = ОбъектНастройкиПрав;
				НоваяСтрока.Право = ТекущаяСтрока.ПравоНастройка;
				НоваяСтрока.Доступность = ВыбранноеЗначение.УстанавливатьПометку;
				НоваяСтрока.Инд = ТабДоступностиСправочников.Количество();
				НоваяСтрока.Изменено = Истина;
			КонецЦикла;
			
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(ТекущаяСтрока.Значение) = Тип("ПеречислениеСсылка.Scan_ВидыПравДляДокументов") Тогда
		Если НЕ ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
			Возврат;
		КонецЕсли;
		
		//Если Текущая строка уже есть то просто меняем значение ее доступности
		мсвСтрокПоиска = ТаблицаДоступностиОбъектов.НайтиСтроки(Новый Структура("Объект", ВыбранноеЗначение));
		Если мсвСтрокПоиска.Количество() > 0 Тогда
			ТекСтрока = мсвСтрокПоиска[0];
			ТекСтрока.Изменено = Истина;
			ТекСтрока.Доступность = Истина;
			Возврат;
		КонецЕсли;
		
		НовыйДокумент = ТаблицаДоступностиОбъектов.Добавить();
		НовыйДокумент.Объект = ВыбранноеЗначение;
		НовыйДокумент.Пользователь = ОбъектНастройкиПрав;
		НовыйДокумент.Право = ТекущаяСтрока.ПравоНастройка;
		НовыйДокумент.Доступность = Истина;
		НовыйДокумент.Инд = ТабДоступностиДокументов.Количество();
		НовыйДокумент.Изменено = Истина;
		
	КонецЕсли;
КонецПроцедуры

&НаСервереБезКонтекста
Функция ОбработкаВыбораПолучитьОбъектыГруппы(ВидСправочника, ВыбГруппа)
	Текст = "ВЫБРАТЬ
	|	" + ВидСправочника + ".Ссылка КАК Объект
	|ИЗ
	|	Справочник." + ВидСправочника + " КАК " + ВидСправочника + "
	|ГДЕ
	|	" + ВидСправочника + ".ЭтоГруппа = Истина
	| И " + ВидСправочника + ".Ссылка В ИЕРАРХИИ (&ВыбГруппа)";
	
	Запрос = Новый Запрос;
	Запрос.Текст = Текст;
	Запрос.УстановитьПараметр("ВыбГруппа", ВыбГруппа);
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Объект");
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьСсылкуНаОбработкуНастроекНаименованияПродукта()
	Возврат Обработки.Scan_НастройкаНаименованияИзделия;
КонецФункции


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	// уровень доступа
	Если РольДоступна("АдминистраторСистемы") тогда
		ДостПрава     = Истина;
		ДостНастройки = Истина;
		
		ПользовательАдминистратор = Истина; // rarus vikhle 11.02.2022 m 17268
	Иначе
		ДостПрава = Scan_ПраваИНастройки.Scan_Право("РазрешитьИзменениеПравДоступа");
		ДостНастройки = Scan_ПраваИНастройки.Scan_Право("РазрешитьИзменениеЛичныхНастроек");
		Если НЕ (ДостПрава ИЛИ ДостНастройки) Тогда
			//rarus FominskiyAS 24.04.2019  mantis 14191 +++
			//Сообщить("Нет прав на редактирование прав и настроек !");
			Сообщить(НСтр("ru = 'Нет прав на редактирование прав и настроек !'; en = 'No edit rights!'"));
			//rarus FominskiyAS 24.04.2019  mantis 14191 ---  
			Отказ = Истина;
			Возврат;
		КонецЕсли; 	
	КонецЕсли;
	
	ТекущийПользователь = ПользователиКлиентСервер.ТекущийПользователь();	//rarus tenkam 09.06.2016 mantis 7072 +
		
	// Создадим структуру описаний (такого типа реквизитов нет, используем произвольный)
	СтруктураОписаний = Новый Структура();
	//
	//// проверка на объединение с типовой
	//ЭтоОбъединеннаяКонфигурация = уатОбщегоНазначенияПовтИсп.ЭтоОбъединеннаяКонфигурация();
	//
	//// Проверим на объединение с модулем логистики
	//ЭтоОбъединеннаяКонфигурацияМодульЛогистики = уатОбщегоНазначенияПовтИсп.ОбъединеннаяКонфигурацияМодульЛогистики();
	//
	//уатОбщегоНазначенияСервер.НастроитьПолеПодразделение(Элементы.ОбъектНастройкиПрав,, Метаданные.Обработки.уатУстановкаПравИНастроек.Реквизиты.ОбъектНастройкиПрав.Тип);
	Scan_СборСтатистики.Scan_ПриОткрытииОбработки(РеквизитФормыВЗначение("Объект").Метаданные().Синоним); // Rarus tenkam 11.04.2022 mantis 18433 +

КонецПроцедуры

&НаКлиенте
Процедура ПриПовторномОткрытии()
	//Сохраняем внесенные изменения
	ЗапросСохраненияИзменений(Новый ОписаниеОповещения("ПриПовторномОткрытииЗавершение", ЭтотОбъект)); 
КонецПроцедуры

&НаКлиенте
Процедура ПриПовторномОткрытииЗавершение(Результат, ДополнительныеПараметры) Экспорт
    
    Если Результат = Ложь Тогда
        Возврат;
    КонецЕсли;
    
    // повторим все действия как при открытии
    Отказ_ = Ложь;
    ПриОткрытии(Отказ_);

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Scan_ВспомогательныеФункцииКлиент.ПроверитьПользователяПортала();//rarus vikhle 07.05.2020 mt 15695
	// сначала заполним все таблицы данных для дальнейшей обработки
	Состояние("Получение дерева прав и настроек ...");
	ПолучитьИерархиюПрав();
	Состояние("Получение структуры описания прав и настроек ...");
	ПолучитьСтруктуруОписаний();
	Состояние("Получение дополнительных прав доступа справочников и документов ...");
	ПолучитьДоступностьСправочников();
	ПолучитьДоступностьДокументов();

	//// Установка объекта по начальному объекту выбора
	//Если НачальноеЗначениеВыбора <> Неопределено Тогда
	//	ОбъектНастройкиПрав = НачальноеЗначениеВыбора;
	//КонецЕсли;
	
	//Установка типа в случае передачи объекта
	Если ТипЗнч(ОбъектНастройкиПрав) = Тип("СправочникСсылка.Организации") Тогда
		ТипОбъектаНастройки = ПредопределенноеЗначение("Перечисление.Scan_НазначениеПравИНастроек.Организация");
	ИначеЕсли ТипЗнч(ОбъектНастройкиПрав) = Тип("СправочникСсылка.Пользователи") Тогда
		ТипОбъектаНастройки = ПредопределенноеЗначение("Перечисление.Scan_НазначениеПравИНастроек.Пользователь");
	Иначе
		ТипОбъектаНастройки = ПредопределенноеЗначение("Перечисление.Scan_НазначениеПравИНастроек.Пользователь");
		ОбъектНастройкиПрав = ТекущийПользователь;	// по умолчанию выведем себя.
	КонецЕсли;
	
	// Установка доступности в соответствии с правами доступа
	ОтображатьНастройки = ДостНастройки;
	Если НЕ ДостПрава Тогда
		ОтображатьПрава = Ложь;
		ТипОбъектаНастройки = ПредопределенноеЗначение("Перечисление.Scan_НазначениеПравИНастроек.Пользователь");
		ОбъектНастройкиПрав = ТекущийПользователь;
		Элементы.ОтображатьПрава.Доступность = Ложь;
		Элементы.ОтображатьНастройки.Доступность = Ложь;
		Элементы.ТипОбъектаНастройки.Доступность = Ложь;
		//Элементы.ПраваДляВыбор.Доступность = Ложь;
		//Элементы.ОсновныеДействияФормы.Кнопки.Подменю.Кнопки.УстановитьПраваПоУмолчанию.Доступность = Ложь;
		//Элементы.ОсновныеДействияФормы.Кнопки.СкопироватьПрофиль.Доступность = Ложь;
		Элементы.ФормаУстановитьПраваПоУмолчанию.Доступность = Ложь;
		Элементы.ФормаСкопироватьПрофиль.Доступность = Ложь;
	КонецЕсли; 
	
	//Формирование дерева прав для заданного объекта 
	ТипОбъектаНастройкиПриИзменении(Неопределено);	// Перенесено
	
	Если НЕ ДостПрава Тогда
		Элементы.ОбъектНастройкиПрав.Доступность = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	ЗапросСохраненияИзменений(Неопределено);
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПользовательВходитВГруппуТекущего(ТекПользователь, ВыбранныйПользователь) // rarus vikhle 11.02.2022 m 17268 +++
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ГруппыПользователейСостав.Ссылка КАК Ссылка,
	               |	ГруппыПользователейСостав.Пользователь КАК Пользователь
	               |ПОМЕСТИТЬ ГруппыТекПользователя
	               |ИЗ
	               |	Справочник.ГруппыПользователей.Состав КАК ГруппыПользователейСостав
	               |ГДЕ
	               |	НЕ ГруппыПользователейСостав.Ссылка = &ВсеПользователи
	               |	И ГруппыПользователейСостав.Пользователь = &ТекПользователь
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ГруппыПользователейСостав.Ссылка КАК Ссылка,
	               |	ГруппыПользователейСостав.Пользователь КАК Пользователь
	               |ИЗ
	               |	Справочник.ГруппыПользователей.Состав КАК ГруппыПользователейСостав
	               |ГДЕ
	               |	НЕ ГруппыПользователейСостав.Ссылка = &ВсеПользователи
	               |	И ГруппыПользователейСостав.Пользователь = &ВыбранныйПользователь
	               |	И ГруппыПользователейСостав.Ссылка В ИЕРАРХИИ
	               |			(ВЫБРАТЬ
	               |				ГруппыТекПользователя.Ссылка КАК Ссылка
	               |			ИЗ
	               |				ГруппыТекПользователя КАК ГруппыТекПользователя)";
	Запрос.УстановитьПараметр("ВсеПользователи", 		Справочники.ГруппыПользователей.ВсеПользователи);
	Запрос.УстановитьПараметр("ТекПользователь", 		ТекПользователь);
	Запрос.УстановитьПараметр("ВыбранныйПользователь", 	ВыбранныйПользователь); 
	
	РезультатЗапроса = Запрос.Выполнить();
		
	Возврат НЕ РезультатЗапроса.Пустой();
	
КонецФункции // rarus vikhle 11.02.2022 m 17268 ---	
	
