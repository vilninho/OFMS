// rarus tenkam 17.04.2020 mantis 15715 +++
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Пользователь = Параметры.Пользователь;
	Если НЕ ЗначениеЗаполнено(Пользователь) Тогда
		Пользователь = ПользователиКлиентСервер.АвторизованныйПользователь();
	КонецЕсли;
	ЗаполнитьНастройкиПользователя();
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНастройкиПользователя()
	МассивНастроек = Обработки.Scan_РМОтделаЛогистики.ПолучитьНастройкиОтображенияЗакладок(Пользователь);
	Для Каждого ТекСтрока Из МассивНастроек Цикл
		НоваяСтрока = НастройкиОтображенияЗакладок.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекСтрока);
		НоваяСтрока.Закладка = Обработки.Scan_РМОтделаЛогистики.ПолучитьСинонимЗакладки(ТекСтрока.Закладка);
		НоваяСтрока.СтароеЗначение = НоваяСтрока.Значение;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ВключитьВсе(Команда)
	Для Каждого ТекСтрока Из НастройкиОтображенияЗакладок Цикл
		ТекСтрока.Значение = Истина;	
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ОтключитьВсе(Команда)
	Для Каждого ТекСтрока Из НастройкиОтображенияЗакладок Цикл
		ТекСтрока.Значение = Ложь;	
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура Сохранить(Команда)
	СохранитьНаСервере();
	ОбновитьПовторноИспользуемыеЗначения();		
	Закрыть();
КонецПроцедуры

&НаСервере
Процедура СохранитьНаСервере()
	Для Каждого ТекСтрока Из НастройкиОтображенияЗакладок Цикл
		Если ТекСтрока.СтароеЗначение <> ТекСтрока.Значение Тогда			
			МенеджерРС = РегистрыСведений.Scan_ПраваИНастройки.СоздатьМенеджерЗаписи();
			МенеджерРС.Объект = Пользователь;
			МенеджерРС.ПравоНастройка = ТекСтрока.Настройка;
			МенеджерРС.Значение = ТекСтрока.Значение;
			МенеджерРС.Пользователь = ПользователиКлиентСервер.АвторизованныйПользователь();		
			МенеджерРС.Записать();
			
			Рег2 = РегистрыСведений.Scan_ИсторияИзмененияПравИНастроек.СоздатьМенеджерЗаписи();
			Рег2.Период = ТекущаяДата();
			Рег2.Объект = Пользователь;
			Рег2.ПравоНастройка = ТекСтрока.Настройка;
			Рег2.Значение = ТекСтрока.Значение;
			Рег2.Пользователь = ПользователиКлиентСервер.АвторизованныйПользователь();
			Рег2.Записать();			
		КонецЕсли;
	КонецЦикла	
КонецПроцедуры
// rarus tenkam 17.04.2020 mantis 15715 ---