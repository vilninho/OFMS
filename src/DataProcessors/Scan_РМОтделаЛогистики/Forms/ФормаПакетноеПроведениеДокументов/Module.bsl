
&НаСервере
Процедура КомандаПоискДокументовНаСервере() //rarus bonmak 18.05.2020 14375 ++	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Scan_ДвижениеИзделий.Ссылка КАК Документ
		|ИЗ
		|	Документ.Scan_ДвижениеИзделий КАК Scan_ДвижениеИзделий
		|ГДЕ
		|	Scan_ДвижениеИзделий.НакладнаяТС ПОДОБНО &НакладнаяТС
		|	И НЕ Scan_ДвижениеИзделий.Проведен
		|	И НЕ Scan_ДвижениеИзделий.ПометкаУдаления";
	
	Запрос.УстановитьПараметр("НакладнаяТС", НомерНакладной);
	
	ДокументыДвижениеИзделий.Очистить();
	ДокументыДвижениеИзделий.Загрузить(Запрос.Выполнить().Выгрузить());
КонецПроцедуры //rarus bonmak 18.05.2020 14375 --

&НаКлиенте
Процедура КомандаПоискДокументов(Команда) //rarus bonmak 18.05.2020 14375 ++
	Если НЕ ЗначениеЗаполнено(НомерНакладной) Тогда
		Сообщить(НСтр("ru = 'Не указан номер накладной!'; en = 'The invoice number is not specified!'"));
		Возврат;	
	КонецЕсли;	
	КомандаПоискДокументовНаСервере();
КонецПроцедуры //rarus bonmak 18.05.2020 14375 --

&НаСервере
Процедура КомандаПровестиДокументыНаСервере() //rarus bonmak 18.05.2020 14375 ++
	Для Каждого СтрДокумент Из ДокументыДвижениеИзделий Цикл
		Если СтрДокумент.Документ.Проведен Тогда
			Продолжить;
		КонецЕсли;
		
		ОбъектДокумент = СтрДокумент.Документ.ПолучитьОбъект();
		ОбъектДокумент.МестоХранения = МестоХранения;
		Попытка
			ОбъектДокумент.Записать(РежимЗаписиДокумента.Проведение);
		Исключение
			ТекстСообщения = Нстр("ru = 'Не удалось провести документ '; en = 'Failed to post document '") + СтрДокумент.Документ + Нстр("ru = ' по причине:'; en = ' because of:'") + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			Сообщить(ТекстСообщения);
		КонецПопытки;	
	КонецЦикла;
КонецПроцедуры //rarus bonmak 18.05.2020 14375 --

&НаКлиенте
Процедура КомандаПровестиДокументы(Команда) //rarus bonmak 18.05.2020 14375 ++
	Если НЕ ЗначениеЗаполнено(МестоХранения) Тогда
		Сообщить(НСтр("ru = 'Не указано место хранения!'; en = 'Storage location not specified!'"));
		Возврат;	
	КонецЕсли;
	КомандаПровестиДокументыНаСервере();
КонецПроцедуры //rarus bonmak 18.05.2020 14375 --

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка) //rarus bonmak 18.05.2020 14375 ++
	Если Параметры.Свойство("НомерНакладной") Тогда
		НомерНакладной = Параметры.НомерНакладной;
		КомандаПоискДокументовНаСервере();
	КонецЕсли;	
КонецПроцедуры //rarus bonmak 18.05.2020 14375 --
