
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ДанныеДляПечати.ЗагрузитьЗначения(Параметры.ДанныеДляПечати);
	
	ЗаполнитьРазделыДляПечати();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРазделыДляПечати()
	
	Если ДанныеДляПечати.Количество() > 0 Тогда
		ПечатныйПрайсЛист = ДанныеДляПечати[0].Значение;
	Иначе
		ПечатныйПрайсЛист = Неопределено;
		//!!
		// Выдать сообщение
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ПечатныйПрайсЛист", ПечатныйПрайсЛист);
	Запрос.Текст = "ВЫБРАТЬ
	|	Scan_ПечатныйПрайсЛистРазделыПрайсЛиста.Наименование КАК Раздел
	|ИЗ
	|	Документ.Scan_ПечатныйПрайсЛист.РазделыПрайсЛиста КАК Scan_ПечатныйПрайсЛистРазделыПрайсЛиста
	|ГДЕ
	|	Scan_ПечатныйПрайсЛистРазделыПрайсЛиста.Ссылка = &ПечатныйПрайсЛист";
	
	РезультатЗапроса = Запрос.Выполнить();
	РазделыДляПечати.ЗагрузитьЗначения(РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Раздел"));
	
КонецПроцедуры

&НаКлиенте
Процедура Печать(Команда)
	
	УправлениеПечатьюКлиент.ВыполнитьКомандуПечати("Документ.Scan_ПечатныйПрайсЛист",
                            "ПФ_MXL_ПечатныйПрайсЛист",
                            ПолучитьОбъектыДляПечати(),
                            ЭтотОбъект,
                            ПолучитьПараметрыПечати());
	Закрыть();
	
КонецПроцедуры

&НаСервере
Функция ПолучитьОбъектыДляПечати()
	
	Массив = ДанныеДляПечати.ВыгрузитьЗначения();
	Возврат Массив;
	
КонецФункции

&НаСервере
Функция ПолучитьПараметрыПечати()
	
	ПараметрыПечати = Новый Структура;
	
	Если ДанныеДляПечати.Количество() > 0 Тогда
		ПечатныйПрайсЛист = ДанныеДляПечати[0].Значение;
		ПараметрыПечати.Вставить("ЗаголовокФормы", ПечатныйПрайсЛист.НаименованиеПрайсЛиста);
		ПараметрыПечати.Вставить("ИмяФайлаПечатнойФормы", ПечатныйПрайсЛист.НаименованиеПрайсЛиста);
	Иначе
		ПараметрыПечати.Вставить("ЗаголовокФормы", "Печатный прайс-лист");
	КонецЕсли;
	
	ПараметрыПечати.Вставить("РазделыДляПечати", РазделыДляПечати);
	
	Возврат ПараметрыПечати;
	
КонецФункции

&НаКлиенте
Процедура УстановитьПометки(Команда)
	
	РазделыДляПечати.ЗаполнитьПометки(Истина);
	
КонецПроцедуры 

&НаКлиенте
Процедура СнятьПометки(Команда)
	
	РазделыДляПечати.ЗаполнитьПометки(Ложь);
	
КонецПроцедуры

