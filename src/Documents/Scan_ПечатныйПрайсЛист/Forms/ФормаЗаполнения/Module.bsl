
Перем СпецификацииПоРазделамСоотв;

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("Калькуляция") Тогда
		Калькуляция = Параметры.Калькуляция;
	КонецЕсли;
	
	Если Параметры.Свойство("РазделыПрайсЛиста") Тогда
		РазделыПрайсЛиста = Параметры.РазделыПрайсЛиста;
	КонецЕсли;  
	
	Если Параметры.Свойство("АдресСпецификацииПоРазделамСоотв") Тогда
		СпецификацииПоРазделамСоотв = ПолучитьИзВременногоХранилища(Параметры.АдресСпецификацииПоРазделамСоотв);
	Иначе
		СпецификацииПоРазделамСоотв = Новый Соответствие;
	КонецЕсли;
	
	ЗаполнитьСпецификацииПоРазделам();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСпецификацииПоРазделам()
	
	Если Не Калькуляция.Пустая() Тогда
		ДобавитьЭлементыСпецификацииПоРазделам();
		ЗаполнитьТаблицуСпецификацииПоРазделам();
		
		Если СпецификацииПоРазделам.Количество() = 0 Тогда
			Элементы.СпецификацииПоРазделам.Видимость    = Ложь;
			Элементы.ДекорацияОтсутствиеДанных.Видимость = Истина;
			
			Элементы.ФормаЗаполнить.Видимость = Ложь;
		Иначе
			Элементы.СпецификацииПоРазделам.Видимость    = Истина;
			Элементы.ДекорацияОтсутствиеДанных.Видимость = Ложь;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьЭлементыСпецификацииПоРазделам()
	
	МассивРеквизитов = Новый Массив;
	
	// Спецификация
	МассивТипов = Новый Массив;
	МассивТипов.Добавить(Тип("ДокументСсылка.Scan_СпецификацияСтандартнойМодели"));
	НоваяКолонка = Новый РеквизитФормы("Спецификация", Новый ОписаниеТипов(МассивТипов), "СпецификацииПоРазделам", "Спецификация");
	МассивРеквизитов.Добавить(НоваяКолонка);
	
	МассивТипов = Новый Массив;
	МассивТипов.Добавить(Тип("Строка"));
	НоваяКолонка = Новый РеквизитФормы("НомерСпецификации", Новый ОписаниеТипов(МассивТипов), "СпецификацииПоРазделам", "STD");
	МассивРеквизитов.Добавить(НоваяКолонка);
	
	// Колонки по количеству разделов
	Для Каждого Раздел Из РазделыПрайсЛиста Цикл
		МассивТипов = Новый Массив;
		МассивТипов.Добавить(Тип("Булево"));
		
		НоваяКолонка = Новый РеквизитФормы("Раздел"+Раздел.Значение, Новый ОписаниеТипов(МассивТипов), "СпецификацииПоРазделам", Раздел.Представление);
		МассивРеквизитов.Добавить(НоваяКолонка);
	КонецЦикла;
	
	// Цены
	КЧ = Новый КвалификаторыЧисла(15,2);
	
	МассивТипов = Новый Массив;
	МассивТипов.Добавить(Тип("Число"));
	
	НоваяКолонка = Новый РеквизитФормы("ЦенаДляДилера", Новый ОписаниеТипов(МассивТипов,,,КЧ), "СпецификацииПоРазделам", "Цена для Дилера");
	МассивРеквизитов.Добавить(НоваяКолонка);
	
	НоваяКолонка = Новый РеквизитФормы("ЦенаДляКлиента", Новый ОписаниеТипов(МассивТипов,,,КЧ), "СпецификацииПоРазделам", "Цена для Клиента");
	МассивРеквизитов.Добавить(НоваяКолонка);
	
	// Добавление реквизитов
	ИзменитьРеквизиты(МассивРеквизитов);
	
	// Элемент формы для спецификации
	НовыйЭлементФормы = Элементы.Добавить("СпецификацииПоРазделамНомерСпецификации", Тип("ПолеФормы"), Элементы.СпецификацииПоРазделам);
	НовыйЭлементФормы.Вид = ВидПоляФормы.ПолеВвода;
	НовыйЭлементФормы.ПутьКДанным = "СпецификацииПоРазделам.НомерСпецификации";
	НовыйЭлементФормы.ТолькоПросмотр = Истина;
	
	// Элементы формы для разделов
	Для Каждого Раздел Из РазделыПрайсЛиста Цикл
		НовыйЭлементФормы = Элементы.Добавить("Раздел" + Раздел.Значение, Тип("ПолеФормы"), Элементы.СпецификацииПоРазделам);
		НовыйЭлементФормы.Вид = ВидПоляФормы.ПолеВвода;
		НовыйЭлементФормы.ПутьКДанным = "СпецификацииПоРазделам." + "Раздел"+Раздел.Значение;
		НовыйЭлементФормы.Вид = ВидПоляФормы.ПолеФлажка;
		НовыйЭлементФормы.РежимРедактирования = РежимРедактированияКолонки.Непосредственно;
		
		НовыйЭлементФормы.УстановитьДействие("ПриИзменении", "ФлажокРазделаПриИзменении");
	КонецЦикла;
	
	// Элементы формы для цен
	НовыйЭлементФормы = Элементы.Добавить("СпецификацииПоРазделамЦенаДляДилера", Тип("ПолеФормы"), Элементы.СпецификацииПоРазделам);
	НовыйЭлементФормы.Вид = ВидПоляФормы.ПолеВвода;
	НовыйЭлементФормы.ПутьКДанным = "СпецификацииПоРазделам.ЦенаДляДилера";
	НовыйЭлементФормы.ТолькоПросмотр = Истина;
	
	НовыйЭлементФормы = Элементы.Добавить("СпецификацииПоРазделамЦенаДляКлиента", Тип("ПолеФормы"), Элементы.СпецификацииПоРазделам);
	НовыйЭлементФормы.Вид = ВидПоляФормы.ПолеВвода;
	НовыйЭлементФормы.ПутьКДанным = "СпецификацииПоРазделам.ЦенаДляКлиента";
	НовыйЭлементФормы.ТолькоПросмотр = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуСпецификацииПоРазделам()
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Калькуляция", Калькуляция);
	Запрос.Текст = "ВЫБРАТЬ
	|	ЦеныСпецификаций.СтандартнаяСпецификация КАК Спецификация,
	|	ЦеныСпецификаций.НомерСпецификации КАК НомерСпецификации,
	|	СУММА(ЦеныСпецификаций.ЦенаДляДилера) КАК ЦенаДляДилера,
	|	СУММА(ЦеныСпецификаций.ЦенаДляКлиента) КАК ЦенаДляКлиента
	|ИЗ
	|	(ВЫБРАТЬ
	|		Scan_КалькуляцияРасчетныеСоставляющие.СтандартнаяСпецификация КАК СтандартнаяСпецификация,
	|		Scan_КалькуляцияРасчетныеСоставляющие.СтандартнаяСпецификация.НомерСпецификации КАК НомерСпецификации,
	|		Scan_КалькуляцияРасчетныеСоставляющие.Значение КАК ЦенаДляДилера,
	|		0 КАК ЦенаДляКлиента
	|	ИЗ
	|		Документ.Scan_Калькуляция.РасчетныеСоставляющие КАК Scan_КалькуляцияРасчетныеСоставляющие
	|	ГДЕ
	|		Scan_КалькуляцияРасчетныеСоставляющие.Ссылка = &Калькуляция
	|		И Scan_КалькуляцияРасчетныеСоставляющие.СоставляющаяРасчета = ЗНАЧЕНИЕ(Справочник.Scan_СоставляющиеРасчетаЦеныПродуктов.ЦенаДилера)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Scan_КалькуляцияРасчетныеСоставляющие.СтандартнаяСпецификация,
	|		Scan_КалькуляцияРасчетныеСоставляющие.СтандартнаяСпецификация.НомерСпецификации,
	|		0,
	|		Scan_КалькуляцияРасчетныеСоставляющие.Значение
	|	ИЗ
	|		Документ.Scan_Калькуляция.РасчетныеСоставляющие КАК Scan_КалькуляцияРасчетныеСоставляющие
	|	ГДЕ
	|		Scan_КалькуляцияРасчетныеСоставляющие.Ссылка = &Калькуляция
	|		И Scan_КалькуляцияРасчетныеСоставляющие.СоставляющаяРасчета = ЗНАЧЕНИЕ(Справочник.Scan_СоставляющиеРасчетаЦеныПродуктов.ЦенаКлиента)) КАК ЦеныСпецификаций
	|
	|СГРУППИРОВАТЬ ПО
	|	ЦеныСпецификаций.СтандартнаяСпецификация,
	|	ЦеныСпецификаций.НомерСпецификации";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = СпецификацииПоРазделам.Добавить();
		НоваяСтрока.Спецификация      = Выборка.Спецификация;
		НоваяСтрока.НомерСпецификации = Выборка.НомерСпецификации;
		НоваяСтрока.ЦенаДляДилера     = Выборка.ЦенаДляДилера;
		НоваяСтрока.ЦенаДляКлиента    = Выборка.ЦенаДляКлиента;
		
		ПолученныйРаздел = СпецификацииПоРазделамСоотв.Получить(Выборка.Спецификация);
		Если ПолученныйРаздел <> Неопределено Тогда
			НоваяСтрока["Раздел"+ПолученныйРаздел] = Истина;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ФлажокРазделаПриИзменении(Элемент)
	
	ЗначениеФлажка = Элементы.СпецификацииПоРазделам.ТекущиеДанные[Элемент.Имя];
	
	Если ЗначениеФлажка Тогда
		Для Каждого Раздел Из РазделыПрайсЛиста Цикл
			ИмяКолонки = "Раздел" + Раздел.Значение;
			Если Элемент.Имя = ИмяКолонки Тогда
				Продолжить;
			КонецЕсли;
			
			Элементы.СпецификацииПоРазделам.ТекущиеДанные[ИмяКолонки] = Ложь;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Заполнить(Команда)
	
	Закрыть(ПоместитьТаблицуЗаполненияНаСервере());
	
КонецПроцедуры

&НаСервере
Функция ПоместитьТаблицуЗаполненияНаСервере()
	
	Возврат ПоместитьВоВременноеХранилище(ДанныеФормыВЗначение(СпецификацииПоРазделам,Тип("ТаблицаЗначений")), Новый УникальныйИдентификатор);
	
КонецФункции
