//rarus tenkam 18.11.2017 9427 11952 +++

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если ЭтоНовый() Тогда
		ДатаСоздания = ТекущаяДата();
		Автор = ПользователиКлиентСервер.ТекущийПользователь();
	КонецЕсли;
	ЭтотОбъект.ДополнительныеСвойства.Вставить("ЭтоНовый", ЭтотОбъект.ЭтоНовый());

КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	
	// регистр Scan_ДвиженияКомплектующих Расход
	Движения.Scan_ДвиженияКомплектующих.Записывать = Истина;
	Для Каждого ТекСтрокаСоставЗаявки Из СоставЗаявки Цикл
		
		//13.02.2018 +++
		//Если уходим в минус, то запрещать проведение
		КоличествоСписания = РегистрыНакопления.Scan_ДвиженияКомплектующих.ПолучитьКоличествоСписания(Склад, ТекСтрокаСоставЗаявки.Изделие, ТекСтрокаСоставЗаявки.Комплектующие, ДокументОснование);
		КоличествоПоступленияБезТекДок = РегистрыНакопления.Scan_ДвиженияКомплектующих.ПолучитьКоличествоПоступления(Склад, ТекСтрокаСоставЗаявки.Изделие, ТекСтрокаСоставЗаявки.Комплектующие, ДокументОснование, Ссылка);
		КоличествоОстаток = КоличествоСписания - КоличествоПоступленияБезТекДок; 
		Если КоличествоОстаток < ТекСтрокаСоставЗаявки.Количество Тогда
			Сообщить("Не удалось провести документ. Количество поступления в строке " + ТекСтрокаСоставЗаявки.НомерСтроки + " превышает количество из заявки на комплектующие!"); 
			Отказ = Истина;	
		КонецЕсли;
		//13.02.2018 ---
		
		Движение = Движения.Scan_ДвиженияКомплектующих.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		Движение.Период = Дата;
		Движение.Склад = Склад;
		Движение.Изделие = ТекСтрокаСоставЗаявки.Изделие;
		Движение.Комплектующие = ТекСтрокаСоставЗаявки.Комплектующие;
		Движение.ЗаявкаНаКомплектующие = ДокументОснование;
		Движение.Количество = ТекСтрокаСоставЗаявки.Количество;
		Движение.Пользователь = ПользователиКлиентСервер.ТекущийПользователь();
	КонецЦикла;
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	Для Индекс = 0 по СоставЗаявки.Количество()-1 Цикл
		СтрокаТЧ = СоставЗаявки.Получить(Индекс);
		Если Не (СтрокаТЧ.КодНеисправнойДетали.Комплектующие) Тогда
			Сообщение = Новый СообщениеПользователю();
			Сообщение.Текст = "В строке " + Индекс + " указан код неисправной детали, у которого не установлен флаг Комплектующие";
			Сообщение.Поле = "СоставЗаявки[" + Индекс + "].КодНеисправнойДетали";
			Сообщение.УстановитьДанные(ЭтотОбъект);
			Сообщение.Сообщить();
			Отказ = Истина;
		КонецЕсли;
	КонецЦикла;			
КонецПроцедуры      

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.Scan_ЗаявкаНаКомплектующие") Тогда
		// Заполнение шапки
		ТекПользователь = ПользователиКлиентСервер.ТекущийПользователь();  
		Автор = ТекПользователь;  
		ВалютаДокумента = ДанныеЗаполнения.ВалютаДокумента;
		ДатаСоздания = ТекущаяДатаСеанса();
		Комментарий = "Документ создан на основании " +  ДанныеЗаполнения;
		Менеджер = ТекПользователь; 
		Организация = ТекПользователь.Организация;
		//rarus vikhle 22.04.2020 mt 15695 +++
		//ПодразделениеКомпании = ТекПользователь.Подразделение;
		ПодразделениеКомпании = ТекПользователь.ПодразделениеОрганизации;
		//rarus vikhle 22.04.2020 mt 15695 ---
		ШтрихКод = ДанныеЗаполнения.ШтрихКод; 
		Склад = ДанныеЗаполнения.Склад; 		
		ДокументОснование = ДанныеЗаполнения.Ссылка;
		ХозОперация = Справочники.Scan_ХозяйственныеОперации.ПоступлениеКомплектующих;
		//rarus vikhle 22.04.2020 mt 15695 +++
		Scan_ВспомогательныеФункцииСервер.ЗаполнитьКомпаниюИКонтрагента(ТекПользователь,Компания,Контрагент);	
		//rarus vikhle 22.04.2020 mt 15695 ---

		Для Каждого ТекСтрокаСоставЗаявки Из ДанныеЗаполнения.СоставЗаявки Цикл
			//12.02.2018 +++
			КолвоТребуемоеПоступление = ТекСтрокаСоставЗаявки.Количество - ТекСтрокаСоставЗаявки.КоличествоПоступления;
			Если КолвоТребуемоеПоступление <> 0 Тогда  			
			//12.02.2018 ---
			
				НоваяСтрока = СоставЗаявки.Добавить();
				НоваяСтрока.Изделие = ТекСтрокаСоставЗаявки.Изделие;
				НоваяСтрока.КодНеисправнойДетали = ТекСтрокаСоставЗаявки.КодНеисправнойДетали;
				//12.02.2018 +++
				//НоваяСтрока.Количество = ТекСтрокаСоставЗаявки.Количество;
				НоваяСтрока.Количество = КолвоТребуемоеПоступление;
				//12.02.2018 ---
				НоваяСтрока.Комплектующие = ТекСтрокаСоставЗаявки.Комплектующие;
				
			КонецЕсли;	//12.02.2018 +
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	Если НЕ Отказ Тогда
		ОбновитьКоличествоВЗаявке();		
	КонецЕсли;	
	
КонецПроцедуры

Процедура ОбновитьКоличествоВЗаявке()
	
	ДокОбъект = ДокументОснование.ПолучитьОбъект();
	НужноПерезаписать = Ложь;
	Для Каждого ТекСтрока Из СоставЗаявки Цикл
		
		ПараметрыОтбора = Новый Структура("Комплектующие", ТекСтрока.Комплектующие);
		ПараметрыОтбора.Вставить("Изделие", ТекСтрока.Изделие);
		НайденныеСтроки = ДокОбъект.СоставЗаявки.НайтиСтроки(ПараметрыОтбора);
		Если НайденныеСтроки.Количество() <> 0 Тогда
			СтрокаЗаявки = НайденныеСтроки[0];
		Иначе
			Продолжить;
		КонецЕсли;
		
		КоличествоПоступления = РегистрыНакопления.Scan_ДвиженияКомплектующих.ПолучитьКоличествоПоступления(Склад, ТекСтрока.Изделие, ТекСтрока.Комплектующие, ДокументОснование, Ссылка);
		КоличествоПоступления = КоличествоПоступления + ТекСтрока.Количество;
		КоличествоВЗаявке = СтрокаЗаявки.КоличествоПоступления;
		Если КоличествоВЗаявке <> КоличествоПоступления Тогда
			НужноПерезаписать = Истина;
			СтрокаЗаявки.КоличествоПоступления = КоличествоПоступления;
		КонецЕсли;
	КонецЦикла;
	
	Если НужноПерезаписать Тогда
		Попытка
			ДокОбъект.Записать(РежимЗаписиДокумента.Проведение);
		Исключение
		КонецПопытки;
	КонецЕсли; 	
КонецПроцедуры

//rarus tenkam 18.11.2017 9427 11952 ---
