
Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Для Каждого СтрокаМенеджеры Из Менеджеры Цикл
		Если     СтрокаМенеджеры.Дилер = Справочники.Scan_Компании.СканияРусь 
			И Не СтрокаМенеджеры.Менеджер.МенеджерСканияРусь
			Тогда
			ВывестиСообщениеПол(Нстр("ru = 'Для дилера ""Скания Русь"" указан сотрудник без признака ""менеджер Скания-Русь""'; 
			                         |en = 'Для дилера ""Скания Русь"" указан сотрудник без признака ""менеджер Скания-Русь""'"),
			                         ЭтотОбъект,"ОтветственныеМенеджеры[#].Менеджер",СтрокаМенеджеры.НомерСтроки);
			Отказ = Истина;
		Конецесли;
	КонецЦикла;
	
	КопияОтветственных = Менеджеры.Выгрузить();
	КоличествоОбщее = КопияОтветственных.Количество();
	КопияОтветственных.Свернуть("Дилер");
	КоличествоСгруппированных = КопияОтветственных.Количество();
	
	Если КоличествоОбщее <> КоличествоСгруппированных Тогда
		ВывестиСообщениеПол(Нстр("ru = 'В таблице закрепления есть повторяющиеся дилеры'; en = 'There are duplicate dealers in the table'"),ЭтотОбъект,"Менеджеры");
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	//rarus vikhle 28.02.2022 m 18879 +++
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		
		ДанныеSetAdditionalProperty = Scan_ВспомогательныеФункцииСервер.ПолучитьДанныеSetAdditionalProperty(ЭтотОбъект);
		ДополнительныеСвойства.Вставить("ДанныеSetAdditionalProperty", ДанныеSetAdditionalProperty);	
		
	КонецЕсли;
	
	Scan_ВспомогательныеФункцииСервер.ПроверитьМенеджеровДокументаЗакрепления(ЭтотОбъект, Отказ);
	//rarus vikhle 28.02.2022 m 18879 ---	
		
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", ЭтотОбъект.Ссылка);
	Запрос.Текст = "ВЫБРАТЬ
	               |	Scan_ЗакреплениеМенеджеровОКПЗаДилерамиМенеджеры.Ссылка.ДатаРаспоряжения КАК Период,
	               |	Scan_ЗакреплениеМенеджеровОКПЗаДилерамиМенеджеры.НомерСтроки КАК НомерСтроки,
	               |	Scan_ЗакреплениеМенеджеровОКПЗаДилерамиМенеджеры.Дилер КАК Дилер,
	               |	Scan_ЗакреплениеМенеджеровОКПЗаДилерамиМенеджеры.Менеджер КАК Менеджер,
	               |	Scan_ЗакреплениеМенеджеровОКПЗаДилерамиМенеджеры.Ссылка.Менеджер КАК Пользователь,
	               |	Scan_ЗакреплениеМенеджеровОКПЗаДилерамиМенеджеры.Ссылка КАК Регистратор
	               |ИЗ
	               |	Документ.Scan_ЗакреплениеМенеджеровОКПЗаДилерами.Менеджеры КАК Scan_ЗакреплениеМенеджеровОКПЗаДилерамиМенеджеры
	               |ГДЕ
	               |	Scan_ЗакреплениеМенеджеровОКПЗаДилерамиМенеджеры.Ссылка = &Ссылка";
	РезультатЗапроса = Запрос.Выполнить();
		
	Движения.Scan_ЗакреплениеМенеджеровОКПЗаДилерами.Загрузить(РезультатЗапроса.Выгрузить());
	Движения.Scan_ЗакреплениеМенеджеровОКПЗаДилерами.Записать();
	
	// rarus vikhle 01.03.2022 m 18879 +++
	Если ДополнительныеСвойства.Свойство("ДанныеSetAdditionalProperty") Тогда
		
		Scan_ВспомогательныеФункцииСервер.ОтправитьЗакрепленныхМенеджеровВ1БД(ДополнительныеСвойства.ДанныеSetAdditionalProperty, 
									Справочники.Scan_ДополнительныеРеквизиты1БД.РегиональныйМенеджерОКП.ИмяПредопределенныхДанных); 
						
	КонецЕсли;	
	// rarus vikhle 01.03.2022 m 18879 ---	
		
КонецПроцедуры

