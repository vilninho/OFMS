// Rarus tenkam 28.06.2022 mantis 18726 АПК + (Стандартные области)
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаявкаПеревозчику = Параметры.ЗаявкаПеревозчику;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("П1", ЗаявкаПеревозчику);
	Запрос.Текст = "ВЫБРАТЬ
	|	Scan_ЗаявкаПеревозчикуСоставЗаявки.Изделие КАК Продукт,
	|	ИСТИНА КАК Печатать
	|ИЗ
	|	Документ.Scan_ЗаявкаПеревозчику.СоставЗаявки КАК Scan_ЗаявкаПеревозчикуСоставЗаявки
	|ГДЕ
	|	Scan_ЗаявкаПеревозчикуСоставЗаявки.Ссылка = &П1";
	РезультатЗапроса = Запрос.Выполнить();
	
	Продукты.Загрузить(РезультатЗапроса.Выгрузить());
	
	ЗаполнятьКонтрагентов = Истина;
	
	ПроверитьНаличиеСпецификацииSPII();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы
// Код процедур и функций
#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура УстановитьФлажки(Команда)
	
	Для Каждого СтрокаПродукта Из Продукты Цикл
		СтрокаПродукта.Печатать = Истина;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьФлажки(Команда)
	
	Для Каждого СтрокаПродукта Из Продукты Цикл
		СтрокаПродукта.Печатать = Ложь;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ОК(Команда)
	
	НайденныеСтроки = Продукты.НайтиСтроки(Новый Структура("Печатать", Истина));
	Если НайденныеСтроки.Количество() > 0 Тогда
		МассивОбъектов = Новый Массив;
		МассивОбъектов.Добавить(ЗаявкаПеревозчику);
		УправлениеПечатьюКлиент.ВыполнитьКомандуПечати("Документ.Scan_ЗаявкаПеревозчику",        //Менеджер печати
																	"ПФ_MXL_ДетализированныйАкт",//Идентификатор
																	МассивОбъектов,              //Объекты печати
																	ЭтотОбъект,                  //Владелец формы - форма из которой вызывается печать
																	ПолучитьПараметрыПечати());  //Параметры печати - произвольные параметры для передачи в менеджер печати
		Закрыть();
	Иначе
		Сообщить(НСтр("ru='Не удалось сформировать детализированный акт - не выбран ни один продукт'"));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПолучитьПараметрыПечати()
	
	МассивПродуктов = Новый Массив;
	
	НайденныеСтроки = Продукты.НайтиСтроки(Новый Структура("Печатать", Истина));
	Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл
		МассивПродуктов.Добавить(НайденнаяСтрока.Продукт);
	КонецЦикла;
	
	ПараметрыПечати = Новый Структура;
	ПараметрыПечати.Вставить("МассивПродуктов", МассивПродуктов);
	ПараметрыПечати.Вставить("ЗаполнятьКонтрагентов", ЗаполнятьКонтрагентов);
	
	Возврат ПараметрыПечати;
	
КонецФункции

&НаСервере
Процедура ПроверитьНаличиеСпецификацииSPII()
	
	МассивПродуктов = Новый Массив;
	
	НайденныеСтроки = Продукты.НайтиСтроки(Новый Структура("Печатать", Истина));
	Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл
		МассивПродуктов.Добавить(НайденнаяСтрока.Продукт);
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("П1", МассивПродуктов);
	Запрос.Текст = "ВЫБРАТЬ
	|	Scan_Изделия.Ссылка КАК Продукт,
	|	Scan_Изделия.СпецификацияПродукта КАК СпецификацияПродукта
	|ПОМЕСТИТЬ ВТ_СпецификацияПродукта
	|ИЗ
	|	Справочник.Scan_Изделия КАК Scan_Изделия
	|ГДЕ
	|	Scan_Изделия.Ссылка В(&П1)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_СпецификацияПродукта.Продукт КАК Продукт,
	|	ВТ_СпецификацияПродукта.СпецификацияПродукта КАК СпецификацияПродукта
	|ИЗ
	|	ВТ_СпецификацияПродукта КАК ВТ_СпецификацияПродукта
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Scan_ВерсииБазовыхСпецификаций КАК Scan_ВерсииБазовыхСпецификаций
	|		ПО ВТ_СпецификацияПродукта.СпецификацияПродукта = Scan_ВерсииБазовыхСпецификаций.Владелец
	|			И (НЕ Scan_ВерсииБазовыхСпецификаций.ПометкаУдаления)
	|			И (Scan_ВерсииБазовыхСпецификаций.ВидСпецификации = ЗНАЧЕНИЕ(Справочник.Scan_ВидыСпецификацийПродуктов.SPIIСпецификация))
	|ГДЕ
	|	Scan_ВерсииБазовыхСпецификаций.Ссылка ЕСТЬ NULL";
	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			Сообщить(СтрШаблон(НСтр("ru='Обратите внимание, по продукту %1 спецификация SPII отсутствует!'"), Выборка.Продукт));
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

