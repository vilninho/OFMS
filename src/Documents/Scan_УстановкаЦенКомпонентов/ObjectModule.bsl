//rarus BProg_Gladkov 11.12.2019 0014560 +-

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	ТекущийПользователь 	= ПользователиКлиентСервер.ТекущийПользователь() ;
	Организация 			= ТекущийПользователь.Организация;
	ПодразделениеКомпании 	= ТекущийПользователь.ПодразделениеОрганизации;
	Автор 					= ТекущийПользователь;
	Менеджер 				= ТекущийПользователь;
	Дата 					= ТекущаяДата();
	ВалютаДокумента 		= Справочники.Валюты.НайтиПоКоду("643");
	//rarus vikhle 22.04.2020 mt 15695 +++
	Scan_ВспомогательныеФункцииСервер.ЗаполнитьКомпаниюИКонтрагента(ТекущийПользователь,Компания,Контрагент);	
	//rarus vikhle 22.04.2020 mt 15695 ---
		
	//rarus BProg_Gladkov 13.01.2020 0014560 +++
	АвторЗапроса 			= Автор;
	//rarus BProg_Gladkov 13.01.2020 0014560 ---
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	//rarus vikhle 17.11.2020 mt 16638 +++ условие по хоз операции
	Если ХозОперация = Справочники.Scan_ХозяйственныеОперации.УстановкаЦенКомпонентов Тогда
		СформироватьДвижения_ЦеныКомпонентовСпецификаций(Отказ);
		СформироватьДвижения_КвотыSOrderFFUПлан(Отказ);
	Иначе
		Движения.Scan_КвотыSOrderFFUПлан.Записывать 			= Истина;
		Движения.Scan_ЦеныКомпонентовСпецификаций.Записывать	= Истина;
	КонецЕсли;	
	//rarus vikhle 17.11.2020 mt 16638 ---
	
КонецПроцедуры

Процедура СформироватьДвижения_ЦеныКомпонентовСпецификаций(Отказ)
	Движения.Scan_ЦеныКомпонентовСпецификаций.Записывать 	= Истина;
	Если НЕ ЗапросСогласованЗаводами тогда//rarus BProg_Dekin 31.03.2020 mantis 0014560 +- Если НЕ ЗапросСогласованЗаводом тогда
		Возврат;
	КонецЕсли;
	
	ВалютаРеглУчета = Константы.Scan_ВалютаРегламентированногоУчета.Получить();
	Если ВалютаРеглУчета.Пустая() Тогда
		ТекстСообщения = НСтр("ru = 'Не заполнена валюта регламентированого учета'; 
			 				  |en = 'The currency of the regulated accounting is not filled'"); 
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект,,,Отказ);
		Возврат;
	КонецЕсли;
	
	ЦенаРегл = РаботаСКурсамиВалют.ПересчитатьВВалюту(ЦенаЗапроса, ВалютаДокумента, ВалютаРеглУчета, Дата);
	
	ДвижениеЦеныКомпонентовСпецификаций = Движения.Scan_ЦеныКомпонентовСпецификаций.Добавить();
	ДвижениеЦеныКомпонентовСпецификаций.Период 		= Дата;
	ДвижениеЦеныКомпонентовСпецификаций.Опции 		= Компонент;
	ДвижениеЦеныКомпонентовСпецификаций.Цена 		= ЦенаЗапроса;
	ДвижениеЦеныКомпонентовСпецификаций.ЦенаРегл 	= ЦенаРегл;
КонецПроцедуры

Процедура СформироватьДвижения_КвотыSOrderFFUПлан(Отказ)
	Движения.Scan_КвотыSOrderFFUПлан.Записывать = Истина;
	Если НЕ ЗапросСогласованЗаводами Тогда //rarus BProg_Dekin 31.03.2020 mantis 0014560 +- Если НЕ ЗапросСогласованЗаводом тогда
		Возврат;
	КонецЕсли;

	ДвижениеКвотыSOrderFFUПлан = Движения.Scan_КвотыSOrderFFUПлан.Добавить();
	ДвижениеКвотыSOrderFFUПлан.Период				= Дата;
	ДвижениеКвотыSOrderFFUПлан.Опция 				= Компонент;
	ДвижениеКвотыSOrderFFUПлан.КвотаSOrderFFU 		= КоличествоИзделийВЗапросе;
	ДвижениеКвотыSOrderFFUПлан.ДатаС 				= ПериодДействияЗапросаС;
	ДвижениеКвотыSOrderFFUПлан.ДатаПо 				= ПериоддействияЗапросаПо;
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
//rarus BProg_Gladkov 13.01.2020 0014560 +++
	Если ЗначениеЗаполнено(ПериодДействияЗапросаС) Тогда 
		Отказ = РегистрыСведений.Scan_ИнформацияПоPartPeriod.ПроверитьПартПериод(ПериодДействияЗапросаС);
		Если Отказ тогда 
			Возврат;
		КонецЕсли;
	КонецЕсли;
	Если ЗначениеЗаполнено(ПериодДействияЗапросаПо) Тогда 
		Отказ  = РегистрыСведений.Scan_ИнформацияПоPartPeriod.ПроверитьПартПериод(ПериодДействияЗапросаПо);
		Если Отказ тогда 
			Возврат;
		КонецЕсли;
	КонецЕсли;
	КрайнийСрокРазмещенияЗаказаНаЗавод = Документы.Scan_УстановкаЦенКомпонентов.ПолучитьКрайнийСрокРазмещенияЗаказНаЗаводПоПартПериоду(ПериодДействияЗапросаПо);
	Если КрайнийСрокРазмещенияЗаказаНаЗавод = Неопределено тогда
		ТекстСообщения = НСтр("ru = 'Внимание! По указанному Part Period в поле «По» нет записей в регистре «Информация по Part Period». Добавьте информацию в регистр.'; 
							  |en = 'Attention! For the specified Part Period, in the ""To"" field there are no entries in the register ""Information by Part Period"" . Add information to the register.'");

		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,,Отказ);
	КонецЕсли;
//rarus BProg_Gladkov 13.01.2020 0014560 ---
	
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	//rarus BProg_Dekin 31.03.2020 mantis 0014560 ++
	СтруктураПоиска = Новый Структура("Согласовано", Истина);
	СогласованныеСтроки 	= СогласованиеЗаводами.НайтиСтроки(СтруктураПоиска);
	ВсеСтрокиСогласованны 	= СогласованныеСтроки.Количество() = СогласованиеЗаводами.Количество();
	
	ЗапросСогласованЗаводами = ЗначениеЗаполнено(СогласованиеЗаводами) И ВсеСтрокиСогласованны;
	//rarus BProg_Dekin 31.03.2020 mantis 0014560 --
КонецПроцедуры
