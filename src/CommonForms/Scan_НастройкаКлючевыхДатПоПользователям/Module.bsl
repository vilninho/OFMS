//rarus BProg_Dekin 31.10.2019 0014452 +- Добалена форма.
#Область ОбработчикиСобытийФормы 

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Отказ = НЕ Scan_ПраваИНастройки.Scan_Право("ПользовательЯвляетсяСотрудникомСканияРусь");
	
	НастройкиИзРегистра = РегистрыСведений.Scan_НастройкиКлючевыхДатПоПользователям.ПолучитьНастройки(Перечисления.Scan_ОбъектыКлючевыхДат.ЗаказНаЗавод);
	Для каждого Строка Из НастройкиИзРегистра Цикл
		НоваяСтрока = НастройкиКлючевыхДат.Добавить();
		НоваяСтрока.КлючеваяДата = Строка.КлючеваяДата;
		НоваяСтрока.Использование = ?(Строка.Использование = Неопределено, 2, Строка.Использование);
	КонецЦикла;
	
	УстановитьЗначенияФлаговЛегенды(ЭтаФорма);
	Scan_СборСтатистики.Scan_ПриОткрытииФормы(ЭтотОбъект.ИмяФормы); // Rarus tenkam 11.04.2022 mantis 18433 +

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы 

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	Отказ = Ложь;
	ЗаписатьНаСервере(Отказ);
	Если НЕ Отказ Тогда
		ПараметрыЗакрытия = Новый Структура();
		ПараметрыЗакрытия.Вставить("ПеречитатьСписокКлючевыхДат", Истина);
		// Закрываем форму параметров
		ЭтаФорма.Закрыть(ПараметрыЗакрытия);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ЗаписатьНаСервере(Отказ = Ложь)
	УстановитьПривилегированныйРежим(Истина);
	Для Каждого СтрокаНастроекДат Из НастройкиКлючевыхДат Цикл
		Использование = ?(СтрокаНастроекДат.Использование = 2, Неопределено, Булево(СтрокаНастроекДат.Использование));
		Отказ = РегистрыСведений.Scan_НастройкиКлючевыхДатПоПользователям.ЗаписьЗначенияРегистраСведения(СтрокаНастроекДат.КлючеваяДата, ПараметрыСеанса.ТекущийПользователь, Использование);	
		Если Отказ Тогда
			Возврат;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура УстановитьФлажки(Команда)
	Для Каждого Строка Из НастройкиКлючевыхДат Цикл
		Строка.Использование = Истина;	
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура СнятьФлажки(Команда)
	Для Каждого Строка Из НастройкиКлючевыхДат Цикл
		Строка.Использование = Ложь;	
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ЗатенитьФлажки(Команда)
	Для Каждого Строка Из НастройкиКлючевыхДат Цикл
		Строка.Использование = 2;	
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ФлагЛегендыПриИзменении(Элемент)
	УстановитьЗначенияФлаговЛегенды(ЭтаФорма);
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьЗначенияФлаговЛегенды(ЭтаФорма)
	ЭтаФорма.ФлагИстина 		= 1;
	ЭтаФорма.ФлагЛожь 			= 0;
	ЭтаФорма.ФлагНеопределено 	= 2;
КонецПроцедуры

#КонецОбласти
